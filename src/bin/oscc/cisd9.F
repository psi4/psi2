      SUBROUTINE CISD9(IPQ,IPL,ITR,ITRO,ITRV,ITRT,ITV,
     .                 EIG,ORBSYM,FLOV,TOFF,TADD,ZLX,T2O,T2,
     .                 CCC,BBB,BB2,FZO,FZV,
     .                 HH,FOCK,FFO,FFV,SFO,SFV,
     .                 AR1,AR2,AR3,AR4,AR5,AR6,JAR,KAR,GGO,GGV,
     .                 T1O,T1,AUX1,AUX2,AUX3,XVAL,VVAL,VPAL,
     .                 SUX1,SUX2,SUX3,
     .                 AOFF,AADD,BOFF,BADD,COFF,CADD,
     .                 DOFF,DADD,EOFF,EADD,FOFF,FADD,
     .                 BUF,IBUF,
     .                 AIN,BIN,CIN,DIN,EIN,FIN,BUFINT,NIT,I79,
     .                 ITYP,IVAL,TVAL,NCSF,NDIMT1)
      IMPLICIT INTEGER (A-Z)
      COMMON/INTS/NSGOO,NSGVV,NSGOV,NSHOV,NSLOV,NSLVO
      COMMON/PARAM/ ESCF,ENUC,NXE,
     .              EPSI,NGO,NMIN ,MINDIM,MAXDIM,MAXIT,CONVI,
     .              NO,NV,NT,NOV,NM,NV2,NT2,IMX,NONO,NVNV,NDIMT2,NTIN,
     .              NSYMHF,NIRRED,MAXOO,MAXVV,MAXOV,
     .              LEVEL,INTBUF,DIISFL,ITAP69,ITAP71,ITAP98,ITAP99,RSTR
      CHARACTER*4 OPTION,RSTR
      REAL*8 GOO,GVV,GOV,HOV,LOV,LVO,AX,BX,CX,DX,XE,XEO,CONV,II,
     .       EREF,ESCF,ENUC,NXE,NXEO,TMP,TMV,
     .       VAL,VAL1,VAL2,VAL3,DELTA,VALA,PAL,DELT,T1NORM,
     .       INT1,INT2,XES,TFAC1,TFAC2,XEOS,VALJ,VALK
      REAL*8 FOCK(NT2),FFO(NO,NO),FFV(NV,NV),SFO(NO*NO),SFV(NV*NV),
     .       VVAL(NT), VPAL(NT), AUX3(NV,NV),SUX3(NV*NV),
     .       AR1(MAXOV,MAXOV),AR2(MAXOV,MAXOV),
     .       AR3(MAXVV,MAXOO),AR4(MAXVV,MAXOO),
     .       AR5(MAXOO,MAXVV),AR6(MAXOO,MAXVV),
     .       JAR(NV*NO),KAR(NV*NO),GGO(NO,NO),GGV(NV,NV),
     .       T1O(NO,NV),T1(NO,NV),AUX1(NV,NO),AUX2(NV,NO),XVAL(NT),
     .       SUX1(NV*NO),SUX2(NV*NO),BUFINT(NV*NV),
     .       EIG(NT),BUF(INTBUF),HH(*),
     .       AIN(NSGOO),BIN(NV,NV),CIN(NSGOV),
     .       DIN(NSHOV),EIN(NSLOV),FIN(NSLVO),
     .       T2O(NDIMT2),T2(NDIMT2),
     .       CCC(MAXDIM),BBB(MAXDIM+1,MAXDIM+2),BB2(MAXDIM+1,MAXDIM+2),
     .       TVAL(*)
      INTEGER IBUF(INTBUF*2),NAB(8),
     .        AOFF(1),BOFF(1),COFF(1),DOFF(1),EOFF(1),FOFF(1),
     .        AADD(1),BADD(1),CADD(1),DADD(1),EADD(1),FADD(1),
     .        IPQ(NT2),IPL(NT),ITR(NOV),ITRO(IMX),
     .        ITRV(NM),ITRT(NM),ITV(NV),
     .        TOFF(NO,NO,2),TADD(NV,NV),ZLX(NV,NV),ITYP(*),
     .        ORBSYM(NT),FLOV(NIRRED,4),FZO(NO),FZV(NV),IVAL(*)
C
      NIT=0
      CONV=10.D0**(-CONVI)
      NXEO=0.0D0
C
      IT=0
      IFLAG=0
      NCOUNT=0
C
C >>> READ SYMMETRY-PACKED INTEGRALS
C
      CALL ZERO (FOCK,NT2)
C
      CALL RDINTS(60,AIN,NSGOO,BUF,IBUF,INTBUF,NO,NV,
     .            AOFF,AADD,ITR,ITR,FOCK)
C     CALL RDINTS(61,BIN,NSGVV,BUF,IBUF,INTBUF,NO,NV,
C    .            BOFF,BADD,ITV,ITV,FOCK)
      CALL RDINTS(62,CIN,NSGOV,BUF,IBUF,INTBUF,NO,NV,
     .            COFF,CADD,ITV,ITR,FOCK)
      CALL RDINTS(63,DIN,NSHOV,BUF,IBUF,INTBUF,NO,NV,
     .            DOFF,DADD,ITR,ITR,FOCK)
      CALL RDINTS(64,EIN,NSLOV,BUF,IBUF,INTBUF,NO,NV,
     .            EOFF,EADD,ITR,ITR,FOCK)
      CALL RDINTS(65,FIN,NSLVO,BUF,IBUF,INTBUF,NO,NV,
     .            FOFF,FADD,ITV,ITR,FOCK)
C
C >>> FOCK MATRIX IN MO BASIS
C
      CALL FOCK2 (NT,HH,FOCK,EREF,ENUC,EIG,
     .            IPQ,ITR,ITV,NO,NV,AIN,EIN,CIN,DIN,
     .            AOFF,AADD,EOFF,EADD,COFF,CADD,DOFF,DADD,
     .            ORBSYM,FLOV,NIRRED,I79,FZO,FZV)
C     ESCF=EREF
C
C >>> SET INITIAL VALUES OF T1 AND T2
C
      IF(RSTR.EQ.'YES ')THEN
      CALL RFILE(ITAP69)
      CALL SREAD(ITAP69,JU,1)
      CALL SREAD(ITAP69,JU,1)
      CALL SREAD(ITAP69,T1,INTOWP(NO*NV))
      CALL SREAD(ITAP69,T2,INTOWP(NDIMT2))
      CALL RCLOSE(ITAP69,3)
C
      ELSE
C
      CALL ZERO(T1,NO*NV)
      DO 13496 I=1,NO
      IF(FZO(I).EQ.1)GO TO 13496
      ISYM=ORBSYM(I)
      IY=IPQ(I)+I
      DO 13486 J=1,NO
      IF(FZO(J).EQ.1)GO TO 13486
      JSYM=ORBSYM(J)
      JISYM=IEOR(JSYM,ISYM)
      JJ=IPQ(J)+J
      DO 13476 A=1,NV
      IF(FZV(A).EQ.1)GO TO 13476
      ASYM=ORBSYM(A+NO)
      BSYM=IEOR(ASYM,JISYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      AA=IPQ(A+NO)+A+NO
      IA=ITR(A)+I
      IF(LB.GT.A)LB=A
      DO 13466 B=FB,LB
      BY=IPQ(B+NO)+B+NO
      JB=ITR(B)+J
      IAJB=DOFF(MAX0(IA,JB))+DADD(MIN0(IA,JB))
      IJAB=TOFF(I,J,1)+TADD(A,B)
      VAL=FOCK(IY)+FOCK(JJ)-FOCK(AA)-FOCK(BY)
      T2(IJAB)=DIN(IAJB)/VAL
13466 CONTINUE
13476 CONTINUE
13486 CONTINUE
13496 CONTINUE
      ENDIF
C
C >>> MP2 CORRELATION ENERGY
C
      CALL PCORR(T2,T1,DIN,NXE,TOFF,TADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK,
     .           DOFF,DADD,NM,ITR)
C
   50 CONTINUE
C
      NIT=NIT+1
C
C >>> TRANSFER T1 TO BEGIN NEW ITERATION
C
      DO 90 U=1,NO
      USYM=ORBSYM(U)
      FBE=FLOV(USYM+1,3)-NO
      LBE=FLOV(USYM+1,4)-NO
      DO 90 BE=FBE,LBE
      T1O(U,BE)=T1(U,BE)
   90 CONTINUE
C
C >>> TRANSFER T2
C
      DO 33491 I=1,NDIMT2
      T2O(I)=T2(I)
33491 CONTINUE
C
      NXEO=NXE
      CALL ZERO(T1,NO*NV)
      CALL ZERO(T2,NDIMT2)
C1      CALL TIMIT(4,6)
C
C >>> GO INTERMEDIATE
C
      CALL ZERO(GGO,NO*NO)
      DO 12190 U=1,NO
      IF(FZO(U).EQ.1)GO TO 12190
      USYM=ORBSYM(U)
      DO 12180 I=1,NO
      IF(FZO(I).EQ.1)GO TO 12180
      ISYM=ORBSYM(I)
      IF(USYM.NE.ISYM)GO TO 12180
      VAL=0.0D0
      IU=ITR(MAX0(I,U))+MIN0(I,U)
CFOK  IF(U.NE.I)VAL=VAL+FOCK(UI)
      GGO(U,I)=VAL
12180 CONTINUE
12190 CONTINUE
C
C >>> GV INTERMEDIATE
C
      CALL ZERO(GGV,NV*NV)
      DO 12290 A=1,NV
      IF(FZV(A).EQ.1)GO TO 12290
      ASYM=ORBSYM(A+NO)
      DO 12280 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 12280
      BESYM=ORBSYM(BE+NO)
      IF(BESYM.NE.ASYM)GO TO 12280
      VAL=0.0D0
      BEA=ITV(MAX0(A,BE))+MIN0(A,BE)
CFOK  IF(A.NE.BE)VAL=VAL+FOCK(BEA)
      GGV(BE,A)=VAL
12280 CONTINUE
12290 CONTINUE
C
C >>> B*TAU TERM
C
      ITAP71=71
      CALL RFILE(ITAP71)
      POINT=1
      DO 14092 TSYM=1,NIRRED
        LAB=0
        DO 7623 A=1,NV
C       IF(FZV(A).EQ.1)GO TO 7623
        ASYM=ORBSYM(A+NO)
C       BSYM=IEOR(ASYM,TSYM-1)
C       FB=FLOV(BSYM+1,3)-NO
C       LB=FLOV(BSYM+1,4)-NO
C       DO 7621 B=FB,LB
        DO 7621 B=1,NV
        BSYM=ORBSYM(B+NO)
        ABSYM=IEOR(ASYM,BSYM)
        IF(ABSYM.NE.TSYM-1)GO TO 7621
        LAB=LAB+1
 7621   CONTINUE
 7623   CONTINUE
        NAB(TSYM)=LAB
C       WRITE(*,*)'TSYM',TSYM, ' NAB(TSYM)=',NAB(TSYM)
C
      XAB=0
      DO 19490 A=1,NV
      IF(FZV(A).EQ.1)GO TO 19490
      ASYM=ORBSYM(A+NO)
      DO 19480 B=1,A
      IF(FZV(B).EQ.1)GO TO 19480
      BSYM=ORBSYM(B+NO)
      BASYM=IEOR(BSYM,ASYM)
      IF(BASYM.NE.TSYM-1)GO TO 19480
      XAB=XAB+1
      XIJ=0
      DO 19470 I=1,NO
      IF(FZO(I).EQ.1)GO TO 19470
      ISYM=ORBSYM(I)
      JSYM=IEOR(ISYM,BASYM)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      IF(LJ.GT.I)LJ=I
      DO 19460 J=FJ,LJ
      XIJ=XIJ+1
      IJAB=TOFF(I,J,1)+TADD(A,B)
      JIAB=TOFF(J,I,1)+TADD(A,B)
      VAL1=T2O(IJAB)
      VAL2=T2O(JIAB)
      AR3(XAB,XIJ)=VAL1+VAL2
      AR4(XAB,XIJ)=VAL1-VAL2
19460 CONTINUE
19470 CONTINUE
19480 CONTINUE
19490 CONTINUE
      LIMO=XIJ
      LIMV=XAB
C
      DO 14091 BE=1,NV
C     IF(FZV(BE).EQ.1)GO TO 14091
      BESYM=ORBSYM(BE+NO)
      DO 14081 GA=1,BE
C     IF(FZV(GA).EQ.1)GO TO 14081
      GASYM=ORBSYM(GA+NO)
      BEGAS=IEOR(GASYM,BESYM)
      IF(BEGAS.NE.TSYM-1)GO TO 14081
        LAB=NAB(TSYM)
        IF(FZV(BE).EQ.1.OR.FZV(GA).EQ.1)THEN
        POINT=POINT+INTOWP(LAB)
        GO TO 14081
        ENDIF
        CALL WREADW(ITAP71,BUFINT,INTOWP(LAB),POINT,POINT)
C       WRITE(*,*)'PAIR READ'
        NADD=0
        DO 14925 A=1,NV
C         IF(FZV(A).EQ.1)GO TO 14925
          ASYM=ORBSYM(A+NO)
C         BSYM=IEOR(ASYM,BEGAS)
C         FB=FLOV(BSYM+1,3)-NO
C         LB=FLOV(BSYM+1,4)-NO
C         DO 14920 B=FB,LB
          DO 14920 B=1,NV
          BSYM=ORBSYM(B+NO)
          ABSYM=IEOR(ASYM,BSYM)
          IF(ABSYM.NE.TSYM-1)GO TO 14920
             NADD=NADD+1
             BIN(A,B)=BUFINT(NADD)
C            WRITE(*,*)BE,A,GA,B,BIN(A,B)
14920        CONTINUE
14925     CONTINUE
C
          XBA=0
          DO 14025 A=1,NV
          IF(FZV(A).EQ.1)GO TO 14025
          ASYM=ORBSYM(A+NO)
          BEA=ITV(MAX0(BE,A)   )+MIN0(BE,A)
          GAA=ITV(MAX0(GA,A)   )+MIN0(GA,A)
          BSYM=IEOR(ASYM,BEGAS)
          FB=FLOV(BSYM+1,3)-NO
          LB=FLOV(BSYM+1,4)-NO
          IF(LB.GT. A)LB = A
             DO 14020 B=FB,LB
             XBA=XBA+1
             GAB=ITV(MAX0(GA,B)   )+MIN0(GA,B)
             BEB=ITV(MAX0(BE,B)   )+MIN0(BE,B)
             TMP=BIN(A,B)+BIN(B,A)
             TMV=BIN(A,B)-BIN(B,A)
             SFV(XBA)= TMP*0.5D0
            SUX3(XBA)= TMV*0.5D0
14020        CONTINUE
             IF(ASYM.EQ.BSYM)SFV(XBA)=SFV(XBA)*0.5D0
14025 CONTINUE
      DO 14077 UV=1,LIMO
      TMP = 0.0D0
      TMV = 0.0D0
      DO 14029 AB=1,LIMV
      TMP=TMP+ SFV(AB)* AR3(AB,UV)
      TMV=TMV+SUX3(AB)* AR4(AB,UV)
14029 CONTINUE
      SUX1(UV)=TMP
      SUX2(UV)=TMV
14077 CONTINUE
      UV=0
      DO 14068 U=1,NO
      IF(FZO(U).EQ.1)GO TO 14068
      USYM=ORBSYM(U)
      VSYM=IEOR(USYM,BEGAS)+1
      SV=FLOV(VSYM,1)
      LV=FLOV(VSYM,2)
      IF(LV.GT.U )LV=U
      DO 14063 V=SV,LV
      UV=UV+1
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)=T2(UVBEGA)+SUX1(UV)+SUX2(UV)
      IF(U.NE.V)THEN
      VUBEGA=TOFF(V,U,1)+TADD(BE,GA)
      T2(VUBEGA)=T2(VUBEGA)+SUX1(UV)-SUX2(UV)
      ENDIF
14063 CONTINUE
14068 CONTINUE
14081 CONTINUE
14091 CONTINUE
14092 CONTINUE
      CALL RCLOSE (ITAP71,3)
C
C     NEW J&K LOOPS
C
      DO 16695 TSYM=1,NIRRED
      XVG=0
      DO 16790 V=1,NO
      IF(FZO(V).EQ.1)GO TO 16790
      VSYM=ORBSYM(V)
      DO 16770 GA=1,NV
      IF(FZV(GA).EQ.1)GO TO 16770
      GASYM=ORBSYM(GA+NO)
      VGASYM=IEOR(VSYM,GASYM)
      IF(VGASYM.NE.TSYM-1)GO TO 16770
      XVG=XVG+1
      XAI=0
      DO 16750 I=1,NO
      IF(FZO(I).EQ.1)GO TO 16750
      ISYM=ORBSYM(I)
      ASYM=IEOR(ISYM,VGASYM)+1
      FA=FLOV(ASYM,3)-NO
      LA=FLOV(ASYM,4)-NO
      DO 16730 A=FA,LA
      XAI=XAI+1
      ZL=ZLX(A,GA)
      IVAGA=TOFF(I,V,ZL)+TADD(A,GA)
      VIAGA=TOFF(V,I,ZL)+TADD(A,GA)
      AR1(XAI,XVG)= T2O(IVAGA)+T2O(IVAGA)-T2O(VIAGA)
      AR2(XAI,XVG)=-T2O(VIAGA)
16730 CONTINUE
16750 CONTINUE
16770 CONTINUE
16790 CONTINUE
      LIM=XVG
C
      DO 16690 U=1,NO
      IF(FZO(U).EQ.1)GO TO 16690
      USYM=ORBSYM(U)
      DO 16680 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 16680
      BESYM=ORBSYM(BE+NO)
      BEUSYM=IEOR(BESYM,USYM)
      IF(BEUSYM.NE.TSYM-1)GO TO 16680
C
      BEU=ITR(BE)+U
      XAI=0
      DO 12490 I=1,NO
      IF(FZO(I).EQ.1)GO TO 12490
      ISYM=ORBSYM(I)
      ASYM=IEOR(ISYM,BEUSYM)+1
      FA=FLOV(ASYM,3)-NO
      LA=FLOV(ASYM,4)-NO
      IU=ITR(MAX0(I,U))+MIN0(I,U)
      DO 12470 A=FA,LA
      IA=ITR(A)+I
      BEUIA=DOFF(MAX0(BEU,IA))+DADD(MIN0(BEU,IA))
      VALJ=DIN(BEUIA)
      BEA=ITV(MAX0(BE,A))+MIN0(BE,A)
      BEAIU=COFF(BEA)+CADD(IU)
      VALK=CIN(BEAIU)
      XAI=XAI+1
      JAR(XAI)=VALJ+VALJ-VALK
      KAR(XAI)=VALK
12470 CONTINUE
12490 CONTINUE
C
      DO 15661 VGA=1,LIM
      VAL1=0.0D0
      VAL2=0.0D0
      DO 15391 AI=1,LIM
      VAL1=VAL1+JAR(AI)*AR1(AI,VGA)
      VAL2=VAL2+KAR(AI)*AR2(AI,VGA)
15391 CONTINUE
      SUX1(VGA)=VAL1*0.5D0
      SUX2(VGA)=VAL2*0.5D0
15661 CONTINUE
C
      VGA=0
      DO 15660 V=1,NO
      IF(FZO(V).EQ.1)GO TO 15660
      VSYM=ORBSYM(V)
      GASYM=IEOR(VSYM,BEUSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      DO 15410  GA=FGA,LGA
      VGA=VGA+1
      ZL=ZLX(BE,GA)
      IF(BE.GE.GA)THEN
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)=T2(UVBEGA)+SUX1(VGA)+SUX2(VGA)
      VUBEGA=TOFF(V,U,1)+TADD(BE,GA)
      T2(VUBEGA)=T2(VUBEGA)+SUX2(VGA)+SUX2(VGA)
      ENDIF
      IF(BE.LE.GA)THEN
      VUGABE=TOFF(V,U,1)+TADD(GA,BE)
      T2(VUGABE)=T2(VUGABE)+SUX1(VGA)+SUX2(VGA)
      UVGABE=TOFF(U,V,1)+TADD(GA,BE)
      T2(UVGABE)=T2(UVGABE)+SUX2(VGA)+SUX2(VGA)
      ENDIF
15410 CONTINUE
15660 CONTINUE
C
16680 CONTINUE
16690 CONTINUE
16695 CONTINUE
C
C     A*TAU TERM
C
      DO 12392 TSYM=1,NIRRED
      XAB=0
      DO 33590 A=1,NV
      IF(FZV(A).EQ.1)GO TO 33590
      ASYM=ORBSYM(A+NO)
      DO 33580 B=1,A
      IF(FZV(B).EQ.1)GO TO 33580
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      IF(ABSYM.NE.TSYM-1)GO TO 33580
      XAB=XAB+1
      XIJ=0
      DO 33570 I=1,NO
      IF(FZO(I).EQ.1)GO TO 33570
      ISYM=ORBSYM(I)
      JSYM=IEOR(ISYM,ABSYM)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      IF(LJ.GT.I)LJ=I
      DO 33560 J=FJ,LJ
      XIJ=XIJ+1
      IJAB=TOFF(I,J,1)+TADD(A,B)
      JIAB=TOFF(J,I,1)+TADD(A,B)
      VAL1=T2O(IJAB)
      VAL2=T2O(JIAB)
      AR5(XIJ,XAB)=VAL1+VAL2
      AR6(XIJ,XAB)=VAL1-VAL2
33560 CONTINUE
33570 CONTINUE
33580 CONTINUE
33590 CONTINUE
      LIMO=XIJ
      LIMV=XAB
C
      DO 12390 U=1,NO
      IF(FZO(U).EQ.1)GO TO 12390
      USYM=ORBSYM(U)
      DO 12380 V=1,U
      IF(FZO(V).EQ.1)GO TO 12380
      VSYM=ORBSYM(V)
      VUSYM=IEOR(VSYM,USYM)
      IF(VUSYM.NE.TSYM-1)GO TO 12380
C
      JI=0
      DO 12340 I=1,NO
      IF(FZO(I).EQ.1)GO TO 12340
      ISYM=ORBSYM(I)
      IU=ITR(MAX0(I,U))+MIN0(I,U)
      IV=ITR(MAX0(I,V))+MIN0(I,V)
      JSYM=IEOR(ISYM,VUSYM)
      FJ=FLOV(JSYM+1,1)
      LJ=FLOV(JSYM+1,2)
      IF(LJ.GT.I)LJ=I
      DO 12330 J=FJ,LJ
      JI=JI+1
      JV=ITR(MAX0(J,V))+MIN0(J,V)
      JU=ITR(MAX0(J,U))+MIN0(J,U)
      IUJV=AOFF(MAX0(IU,JV))+AADD(MIN0(IU,JV))
      IVJU=AOFF(MAX0(IV,JU))+AADD(MIN0(IV,JU))
      TMP=AIN(IUJV)+AIN(IVJU)
      TMV=AIN(IUJV)-AIN(IVJU)
       SFO(JI)= TMP*0.5D0
      SUX1(JI)= TMV*0.5D0
12330 CONTINUE
      IF(ISYM.EQ.JSYM) SFO(JI)= SFO(JI)*0.5D0
12340 CONTINUE
C
      DO 14370 BEGA=1,LIMV
      TMP=0.0D0
      TMV=0.0D0
      DO 14350 IJ=1,LIMO
      TMP=TMP+ SFO(IJ)* AR5(IJ,BEGA)
      TMV=TMV+SUX1(IJ)* AR6(IJ,BEGA)
14350 CONTINUE
      SUX3(BEGA)=TMP
       SFV(BEGA)=TMV
14370 CONTINUE
C
      BEGA=0
      DO 52374 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 52374
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(BESYM,VUSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      IF(LGA.GT.BE)LGA=BE
      DO 52364 GA=FGA,LGA
      BEGA=BEGA+1
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)=T2(UVBEGA)+SUX3(BEGA)+SFV(BEGA)
      IF(U.NE.V)THEN
      VUBEGA=TOFF(V,U,1)+TADD(BE,GA)
      T2(VUBEGA)=T2(VUBEGA)+SUX3(BEGA)-SFV(BEGA)
      ENDIF
52364 CONTINUE
52374 CONTINUE
C
12380 CONTINUE
12390 CONTINUE
12392 CONTINUE
C
C     ADD D INTEGRAL
C
      DO 13091 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13091
      USYM=ORBSYM(U)
      DO 13081 V=1,NO
      IF(FZO(V).EQ.1)GO TO 13081
      VSYM=ORBSYM(V)
      VUSYM=IEOR(VSYM,USYM)
      DO 13071 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 13071
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(BESYM,VUSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      BEU=ITR(BE)+U
      IF(LGA.GT.BE)LGA=BE
      DO 13061 GA=FGA,LGA
      GAV=ITR(GA)+V
      BEUGAV=DOFF(MAX0(BEU,GAV))+DADD(MIN0(BEU,GAV))
      VVAL(GA)=DIN(BEUGAV)
13061 continue
      DO 13062 GA=FGA,LGA
      TMP = VVAL(GA)
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      VAL=T2O(UVBEGA)*NXE
      T2(UVBEGA)=T2(UVBEGA)+TMP-VAL
13062 CONTINUE
13063 CONTINUE
13071 CONTINUE
13081 CONTINUE
13091 CONTINUE
C
C     E INT * T1
C
      DO 13491 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13491
      USYM=ORBSYM(U)
      DO 13481 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 13481
      BESYM=ORBSYM(BE+NO)
      BEUSYM=IEOR(BESYM,USYM)
      BEU=ITR(BE)+U
      DO 13441 V=1,NO
      IF(FZO(V).EQ.1)GO TO 13441
      VSYM=ORBSYM(V)
      GASYM=IEOR(VSYM,BEUSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      FI=FLOV(GASYM,1)
      LI=FLOV(GASYM,2)
      DO 13443 GA=FGA,LGA
      XVAL(GA) = 0.D0
13443 CONTINUE
      DO 13431 I=FI,LI
      IV=ITR(MAX0(I,V))+MIN0(I,V)
      BEUIV=EOFF(BEU)+EADD(IV)
      TMP = -EIN(BEUIV)
      DO 13421 GA=FGA,LGA
      TMV = XVAL(GA)
      TMV = TMV     +  TMP    *T1O(I,GA)
      xval(ga) = tmv
13421 CONTINUE
13431 CONTINUE
      DO 13435 GA=FGA,LGA
      IF(BE.GE.GA)THEN
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)=T2(UVBEGA)+XVAL(GA)
      ENDIF
      IF(BE.LE.GA)THEN
      VUGABE=TOFF(V,U,1)+TADD(GA,BE)
      T2(VUGABE)=T2(VUGABE)+XVAL(GA)
      ENDIF
13435 CONTINUE
13441 CONTINUE
13481 CONTINUE
13491 CONTINUE
C
C     FIN * T1
C
      DO 13591 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13591
      USYM=ORBSYM(U)
      DO 13581 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 13581
      BESYM=ORBSYM(BE+NO)
      BEUSYM=IEOR(BESYM,USYM)
      BEU=ITR(BE)+U
      DO 13541 GA=1,NV
      IF(FZV(GA).EQ.1)GO TO 13541
      GASYM=ORBSYM(GA+NO)
      VSYM=IEOR(GASYM,BEUSYM)+1
      SV=FLOV(VSYM,1)
      LV=FLOV(VSYM,2)
      FA=FLOV(VSYM,3)-NO
      LA=FLOV(VSYM,4)-NO
      DO 23521 V=SV,LV
      xval(v) = 0.d0
23521 continue
      DO 13531 A=FA,LA
      GAA=ITV(MAX0(GA,A))+MIN0(GA,A)
      BEUGAA=FOFF(GAA)+FADD(BEU)
      TMV=FIN(BEUGAA)
      DO 13521 V=SV,LV
      TMP = XVAL(V)
      TMP    =TMP    +TMV       *T1O(V,A)
      XVAL(V) = TMP
13521 CONTINUE
13531 CONTINUE
      DO 13535 V=SV,LV
      IF(BE.GE.GA)THEN
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)=T2(UVBEGA)+XVAL(V)
      ENDIF
      IF(BE.LE.GA)THEN
      VUGABE=TOFF(V,U,1)+TADD(GA,BE)
      T2(VUGABE)=T2(VUGABE)+XVAL(V)
      ENDIF
13535 CONTINUE
13541 CONTINUE
13581 CONTINUE
13591 CONTINUE
C
C     DIVIDE BY MO ENERGIES
C
      DO 13391 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13391
      USYM=ORBSYM(U)
      UU=IPQ(U)+U
      DO 13381 V=1,NO
      IF(FZO(V).EQ.1)GO TO 13381
      VSYM=ORBSYM(V)
      VUSYM=IEOR(VSYM,USYM)
      VV=IPQ(V)+V
      DO 13371 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 13371
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(BESYM,VUSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      BEBE=IPQ(BE+NO)+BE+NO
      IF(LGA.GT.BE)LGA=BE
      DO 13361 GA=FGA,LGA
      GAGA=IPQ(GA+NO)+GA+NO
      DELTA=FOCK(UU)+FOCK(VV)-FOCK(BEBE)-FOCK(GAGA)
      UVBEGA=TOFF(U,V,1)+TADD(BE,GA)
      T2(UVBEGA)= T2(UVBEGA)/DELTA
13361 CONTINUE
13371 CONTINUE
13381 CONTINUE
13391 CONTINUE
C
C     (2D-C) * T1
C
      DO 13291 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13291
      USYM=ORBSYM(U)
      FBE=FLOV(USYM+1,3)-NO
      LBE=FLOV(USYM+1,4)-NO
      FI=FLOV(USYM+1,1)
      LI=FLOV(USYM+1,2)
      UU=IPQ(U)+U
      DO 13271 BE=FBE,LBE
      BEBE=IPQ(BE+NO)+BE+NO
      BEU=ITR(BE)+U
      VAL=0.0D0
      DO 13248 I=1,NO
      IF(FZO(I).EQ.1)GO TO 13248
      IU=ITR(MAX0(I,U))+MIN0(I,U)
      ISYM=ORBSYM(I)
      FA=FLOV(ISYM+1,3)-NO
      LA=FLOV(ISYM+1,4)-NO
      DO 13245 A=FA,LA
      ZL=ZLX(A,BE)
      IA=ITR(A)+I
      BEUIA=DOFF(MAX0(BEU,IA))+DADD(MIN0(BEU,IA))
      BEA=ITV(MAX0(BE,A))+MIN0(BE,A)
      IUBEA=COFF(BEA)+CADD(IU)
      VAL=VAL+(2.0D0*DIN(BEUIA)-CIN(IUBEA))*T1O(I,A)
13245 CONTINUE
13248 CONTINUE
CFOK  VAL=VAL+FOCK(BEU)
      T1(U,BE)=T1(U,BE)+VAL
13261 CONTINUE
13271 CONTINUE
13281 CONTINUE
13291 CONTINUE
C
C     E * T2
C
      CALL ZERO(AUX1,NO*NV)
      DO 13691 I=1,NO
      IF(FZO(I).EQ.1)GO TO 13691
      ISYM=ORBSYM(I)
      DO 13681 J=1,NO
      IF(FZO(J).EQ.1)GO TO 13681
      JSYM=ORBSYM(J)
      JISYM=IEOR(JSYM,ISYM)
      DO 13671 A=1,NV
      IF(FZV(A).EQ.1)GO TO 13671
      ASYM=ORBSYM(A+NO)
      IA=ITR(A)+I
      BESYM=IEOR(ASYM,JISYM)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
      FU=FLOV(BESYM,1)
      LU=FLOV(BESYM,2)
      DO 13601 BE=FBE,LBE
      ZL=ZLX(A,BE)
      IJABE=TOFF(I,J,ZL)+TADD(A,BE)
      JIABE=TOFF(J,I,ZL)+TADD(A,BE)
      VAL1=T2O(IJABE)
      VAL2=T2O(JIABE)
      XVAL(BE)=VAL1+VAL1-VAL2
13601 CONTINUE
      DO 13661 U=FU,LU
      JU=ITR(MAX0(J,U))+MIN0(J,U)
      IAJU=EOFF(IA)+EADD(JU)
      TMP = -EIN(IAJU)
      DO 13651 BE=FBE,LBE
      tmv = aux1(be,u)
      tmv       = tmv      + tmp    *XVAL(BE)
      aux1(be,u) = tmv
13651 CONTINUE
13661 CONTINUE
13671 CONTINUE
13681 CONTINUE
13691 CONTINUE
clj Transpose aux1(1:nv,1:no)
      do 102 i = 1, nv
          do 202 j = 1, no
             aux3(j,i) = aux1(i,j)
202          continue
102       continue
C
C     F * T2
C
      DO 13791 I=1,NO
      IF(FZO(I).EQ.1)GO TO 13791
      ISYM=ORBSYM(I)
      DO 13781 A=1,NV
      IF(FZV(A).EQ.1)GO TO 13781
      ASYM=ORBSYM(A+NO)
      IA=IPQ(A+NO)+I
      AISYM=IEOR(ASYM,ISYM)
      IA=ITR(A)+I
      DO 13771 B=1,NV
      IF(FZV(B).EQ.1)GO TO 13771
      BSYM=ORBSYM(B+NO)
      USYM=IEOR(BSYM,AISYM)+1
      FU=FLOV(USYM,1)
      LU=FLOV(USYM,2)
      FBE=FLOV(USYM,3)-NO
      LBE=FLOV(USYM,4)-NO
      ZL=ZLX(A,B)
      DO 13711 U=FU,LU
      IUAB=TOFF(I,U,ZL)+TADD(A,B)
      UIAB=TOFF(U,I,ZL)+TADD(A,B)
      VAL1=T2O(IUAB)
      VAL2=T2O(UIAB)
      XVAL(U)=VAL1+VAL1-VAL2
13711 CONTINUE
      DO 13761 BE=FBE,LBE
      BEB=ITV(MAX0(BE,B))+MIN0(BE,B)
      IABEB=FOFF(BEB)+FADD(IA)
      DO 13751 U=FU,LU
      tmv = aux3(u,be)
      TMV = TMV       +FIN(IABEB)*XVAL(U)
      aux3(u,be) = tmv
13751 CONTINUE
13761 CONTINUE
13771 CONTINUE
13781 CONTINUE
13791 CONTINUE
C
C     DIVIDE BY MO ENERGIES
C
      DO 13891 U=1,NO
      IF(FZO(U).EQ.1)GO TO 13891
      USYM=ORBSYM(U)
      FBE=FLOV(USYM+1,3)-NO
      LBE=FLOV(USYM+1,4)-NO
      UU=IPQ(U)+U
      DO 13871 BE=FBE,LBE
      BEBE=IPQ(BE+NO)+BE+NO
      DELTA=FOCK(UU)-FOCK(BEBE)
      AUX3(U,BE)=AUX3(U,BE)-T1O(U,BE)*NXE
      T1(U,BE)=(T1(U,BE)+AUX3(U,BE))/DELTA
13871 CONTINUE
13891 CONTINUE
C
C     CORRELATION ENERGY
C
      WRITE(6,*)' '
      CALL PCORR(T2,T1,DIN,NXE,TOFF,TADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK,
     .           DOFF,DADD,NM,ITR)
C
C     WRITE OUT T1,T2 TO FILE69
C
      CALL RFILE(ITAP69)
      CALL SWRIT(ITAP69,NO*NV,1)
      CALL SWRIT(ITAP69,NDIMT2,1)
      CALL SWRIT(ITAP69,T1,INTOWP(NO*NV))
      CALL SWRIT(ITAP69,T2,INTOWP(NDIMT2))
      CALL SWRIT(ITAP69,EREF,INTOWP(1))
      CALL SWRIT(ITAP69,NXE,INTOWP(1))
      CALL RCLOSE(ITAP69,3)
C
C     CHECK CONVERGENCE
C
      CALL TDIFF(T2,T1,T2O,T1O,T1NORM,DELT,NO,NV,NDIMT2,
     .           ORBSYM,FLOV,NIRRED,TOFF,TADD,ZLX,FZO,FZV,NXE,NXEO)
      WRITE(6,6011)T1NORM,DELT
      WRITE(4,6011)T1NORM,DELT
 6011 FORMAT(13X,'T1 NORM =',D15.8,5X,'DELT=',D15.8)
      IF(DELT.LT.CONV) THEN
      WRITE(6,9020) CONV
 9020 FORMAT(///,32('*'),/,'* CORRELATION ENERGY CONVERGED *',3X,D12.2,
     . /,32('*'))
      NT1=NDIMT1
      NT2=NSHOV
      CALL TWRIT (T1,T2,ORBSYM,FLOV,NIRRED,TOFF,TADD,IVAL,TVAL,
     .            ZLX,FZO,FZV,NO,NV,NSYMHF,ITYP,NT1,NT2,NCSF)
      RETURN
      END IF
C
      IF(NIT.GE.MAXIT)THEN
      WRITE(6,9030)
 9030 FORMAT(///,32('*'),/,'* ITERATIONS EXHAUSTED *',/,32('*'))
      RETURN
      ENDIF
C
C     DIIS EXTRAPOLATION
C
      IF(DIISFL.EQ.2) GO TO 1490
      CALL DIISD (T1O,T1,T2O,T2,NO,NV,NDIMT2,NIT,IT,IFLAG,
     .            EPSI,NGO,NDIIS,MINDIM,MAXDIM,
     .            CCC,BBB,ITAP98,ITAP99,ITC,BB2)
      CALL PCORR(T2,T1,DIN,NXE,TOFF,TADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK,
     .           DOFF,DADD,NM,ITR)
 1490 CONTINUE
C
      GO TO 50
C
      END
