      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C***********************************************************************
C*  MODIFICATION FOR FENTRY                                            *
C*  BY: YUKIO YAMAGUCHI                                                *
C*  DATE: FEBRUARY 16, 1989                                            *
C***********************************************************************
C*  LAST UPDATED ON JANUARY 19, 1989 BY YUKIO YAMAGUCHI*
C***********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
C2-16-89 DIMENSION CC(360000),IA(1)
      DIMENSION CC(MAXCOR),IA(1)
      DIMENSION I30(200),COORD(3,50)
      DIMENSION I48(1024)
      LOGICAL ISOMOL
      CHARACTER*1 RAXIS
      CHARACTER*25 RTYPE
      DIMENSION ATM(36)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB103/PARA,WAVE,CONST,CYCL,CONV
      COMMON/VIB104/AIXX,AIYY,AIZZ,SUMW
      COMMON/VIB105/FACT3,FACT4
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB107/ITOP,IVSGL,IVDBL,IVTRP
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB109/CLIMIT,FLIM1,FLIM2
      COMMON/VIB110/ISIGMA
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB202/R(1275)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      COMMON/VIB206/RAXIS(3),RTYPE(6)
      COMMON/VIB207/TABCD(3,3,3,3),PABC(3,3,3)
      COMMON/VIB208/TAABB(3,3),TABAB(3,3)
      COMMON/VIB209/SALP(3)
C2-16-89  EQUIVALENCE (CC,IA)
      DATA ATM / 1.007825D+00 , 4.00260D+00 , 7.01600D+00 ,
     *           9.01218D+00  , 11.00931D+00 , 12.00000D+00 ,
     *           14.00307D+00 , 15.99491D+00 , 18.99840D+00 ,
     *           19.99244D+00 , 22.98977D+00 , 23.98504D+00 ,
     *           26.98154D+00 , 27.97693D+00 , 30.97376D+00 ,
     *           31.97207D+00 , 34.96885D+00 , 39.96238D+00 ,
     *           38.96371D+00 , 39.96259D+00 , 44.95591D+00 ,
     *           47.94795D+00 , 50.94396D+00 , 51.94051D+00 ,
     *           54.93805D+00 , 55.93494D+00 , 58.93320D+00 ,
     *           57.93535D+00 , 62.92960D+00 , 63.92915D+00 ,
     *           68.92558D+00 , 73.92118D+00 , 74.92160D+00 ,
     *           79.91652D+00 , 78.91834D+00 , 83.91151D+00 /
      DATA PH,BK,AVN / 6.626176D+00 , 1.380662D+00 , 6.022045D+00 /
      DATA A0,HE,EV / 0.52917706D+00 , 4.359813653D+00 , 1.6021892D+00 /
      DATA CL,TEMP / 2.99792458D+00 , 300.0D+00 /
      DATA PI,ALN / 3.1415926536D+00 , 2.3025850930D+00 /
    1 FORMAT(//,2X,' THE ANHARMONIC CONSTANTS PROGRAM'/)
    2 FORMAT(10I5)
    3 FORMAT(F20.10)
    4 FORMAT(//,2X,' PARAMETERS FOR THE CALCULATION'//
     * 2X,' ISOTOP = ',I5/
     * 2X,' IGEOMT = ',I5/
     * 2X,' ITHREE = ',I5/
     * 2X,' IFOUR  = ',I5/
     * 2X,' IFREQ  = ',I5/
     * 2X,' ICORIO = ',I5/
     * 2X,' IFERM1 = ',I5/
     * 2X,' IFERM2 = ',I5/
     * 2X,' ISIGMA = ',I5/
     * 2X,' IPRNT  = ',I5/
     * 2X,' NATOM  = ',I5/
     * 2X,' N3N    = ',I5/
     * 2X,' NATRI  = ',I5/
     * 2X,' N3TOT  = ',I5/
     * 2X,' N4TOT  = ',I5/)
    5 FORMAT(//,2X,' SCF ENERGY = ',F20.10/)
    6 FORMAT(//,2X,' MOLECULAR GEOMETRY (IN BOHR)'/
     1 5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
    7 FORMAT(4F20.10)
    8 FORMAT(2X,I7,5X,4F20.10)
    9 FORMAT(//,2X,' MOLECULAR GEOMETRY (IN A)'/
     1 5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
   10 FORMAT(F20.10)
   11 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   12 FORMAT('1',//,2X,' THE ANHARMONIC CONSTANTS PROGRAM'/)
C
      call drum
      CALL TSTART(6)
cets041389  CALL NOUNFL
C
C   PHYSICAL CONSTANTS
      PARA=1.0D+00/AVN
      WAVE=1.0D+04*DSQRT(AVN)/(2.0D+00*PI*CL)
      CONST=1.0D+02*(PH*AVN)/(8.0D+00*PI*PI*CL)
      CYCL=1.0D+06*(PH*AVN)/(8.0D+00*PI*PI)
      CONV=1.0D-01*(PH*CL)/(2.0D+00*BK*TEMP)
C   ALAM IS IN AMU-1.A-2
      ALAM=4.0D-02*(PI*PI*CL)/(PH*AVN)
C   SQLAM IS IN AMU-1/2.A-1
      SQLAM=DSQRT(ALAM)
C   FACT3 IS IN CM-1
      FACT3=1.0D+06/(SQLAM*SQLAM*SQLAM*PH*CL)
C   FACT4 IS IN CM-1
      FACT4=1.0D+06/(ALAM*ALAM*PH*CL)
C
      INPUT=5
      ITAPE6=6
      ITAP15=15
      ITAP30=30
      ITAP48=48
      NSECT=1024
      call ffile(itap15,' ',0)
C2-16-89  MAXCOR=360000
C
      CALL LOCATE(INPUT,'# ANHARM #',IERR)
C
      WRITE(6,1)
C
C***INPUT PARAMETERS FOR THE CALCULATION***
C     ISOTOP IS A PARAMETER FOR ISOTOPIC MOLECULES
C     ISOTOP =0 AND 1 :USE REGULAR ATOMIC MASS
C     ISOTOP =2 OR MORE :NUMBER OF ISOTOPIC MOLECULES
C     IGEOMT IS A PARAMETER FOR INPUT DATA
C     IGEOMT =0 :READ IN GEOMETRY FROM TAPE 30
C     IGEOMT =5 :READ IN GEOMETRY FROM TAPE 5
C     IGEOMT =48:READ IN GEOMETRY FROM FILE48
C     ITHREE IS A PARAMETER FOR THIRD DERIVATIVES
C     ITHREE =0 :DO NOT READ IN THIRD DERIVATIVES
C     ITHREE =1 OR 20:READ IN THIRD DERIVATIVES FROM FILE20
C     ITHREE =48:READ IN THIRD DERIVATIVES FROM FILE48
C     IFOUR IS A PARAMETER FOR FOURTH DERIVATIVES
C     IFOUR  =0 :DO NOT READ IN FOURTH DERIVATIVES
C     IFOUR  =1 OR 24:READ IN FOURTH DERIVATIVES FROM FILE24
C     IFOUR  =48:READ IN FOURTH DERIVATIVES FROM FILE48
C     IFREQ  =0 :CALCULATE 3N-6(OR 3N-5) SETS OF ANHARMONIC CONSTANTS
C     IFREQ  =N :CALCULATE IFREQ SETS OF ANHARMONIC CONSTANTS
C     ICORIO =0 :USE A DEFAULT VALU FOR CORIOLIS RESONANCE
C     ICORIO =1 :READ IN A THRESHOLD VALU FOR CORIOLIS RESONANCE
C     IFERM1 =0 :USE A DEFAULT VALU FOR TYPE 1 FERMI RESONANCE
C                TYPE1----W(I)=W(J)
C     IFERM1 =1 :READ IN A THRESHOLD VALU FOR TYPE 1 FERMI RESONANCE
C     IFERM2 =0 :USE A DEFAULT VALU FOR TYPE 2 FERMI RESONANCE
C                TYPE2---W(I)+W(J)=W(K)
C     IFERM2 =1 :READ IN A THRESHOLD VALU FOR TYPE 2 FERMI RESONANCE
C     ISIGMA =0 :USE A(0') VALUE TO EVALUATE SIGMA VALUE THAT IS NEEDED
C                FOR CENTRIFUGAL DISTORTION CONSTANTS
C     ISIGMA =1 :USE A(E)  VALUE TO EVALUATE SIGMA VALUE THAT IS NEEDED
C                FOR CENTRIFUGAL DISTORTION CONSTANTS
      READ(5,2) ISOTOP,IGEOMT,ITHREE,IFOUR,IFREQ,ICORIO,IFERM1,IFERM2,
     1          ISIGMA,IPRNT
      IF(IGEOMT.EQ.0) IGEOMT=30
      IF(ITHREE.EQ.1) ITHREE=20
      IF(IFOUR.EQ.1) IFOUR=24
C
      REWIND ITAP15
      READ(ITAP15,2) NATOM,NSTORE
C
      IOFF(1)=0
      DO 101 I=1,149
      IOFF(I+1)=IOFF(I)+I
  101 CONTINUE
C
      IF(IGEOMT.EQ.30) THEN
        CALL RFILE(ITAP30)
        CALL WREADW(ITAP30,I30,200,101,JUNK)
        IEND=I30(1)
        MPOINT=I30(2)
        MCONST=I30(3)
        MCALCS=I30(4)
        NCALCS=I30(5)
        NATOM=I30(19)
        N3N=NATOM*3
        JUNK=101+MCONST
        CALL WREADW(ITAP30,IPT,1,JUNK,JUNK)
        CALL WREADW(ITAP30,CHG,NATOM*2,IPT,JUNK)
        IGEOP=100+MCONST+MPOINT+NCALCS
        CALL WREADW(ITAP30,LOCCAL,1,IGEOP,JUNK)
        IGEOP=LOCCAL+60
        CALL WREADW(ITAP30,I30,20,IGEOP,IGEOP)
        CALL WREADW(ITAP30,COORD,N3N*2,IGEOP,JUNK)
        JUNK=JUNK+INTOWP(1)
        CALL WREADW(ITAP30,ESCF,2,JUNK,JUNK)
        CALL RCLOSE(ITAP30,3)
      END IF
C
      IF(IGEOMT.EQ.48) THEN
        CALL RFILE(ITAP48)
        CALL RREAD(ITAP48,I48,NSECT,2)
        NATOM=I48(4)
        N3N=I48(5)
        CALL RREAD(ITAP48,COORD,N3N*2,5)
        CALL RREAD(ITAP48,W,NATOM*2,6)
        CALL RCLOSE(ITAP48,3)
      END IF
C
      N3N=NATOM*3
      NATRI=IOFF(N3N+1)
      N3TOT=N3N*(N3N+1)*(N3N+2)/6
      N4TOT=N3N*(N3N+1)*(N3N+2)*(N3N+3)/24
C
C   SET AN ARRAY FOR ANHARMONICITY CALCULATION
      DO 102 I=1,N3N
  102 NFRQ(I)=I
      CALL LOCATE(INPUT,'# ANHARM #',IERR)
      READ(5,2) ISOTOP,IGEOMT,ITHXXX,IFXXX,IFREQ,ICORIO,IFERM1,IFERM2,
     1          ISIGMA,IPRNT
      IF(ISOTOP.EQ.0) ISOTOP=1
      IF(IFREQ.NE.0) THEN
        NFREQ=IABS(IFREQ)
        READ(5,2) (NFRQ(I),I=1,NFREQ)
      END IF
      IF(ICORIO.NE.0) THEN
        READ(5,3) CLIMIT
      ELSE
        CLIMIT=100.0D+00
      END IF
      IF(IFERM1.NE.0) THEN
        READ(5,3) FLIM1
      ELSE
        FLIM1=100.0D+00
      END IF
      IF(IFERM2.NE.0) THEN
        READ(5,3) FLIM2
      ELSE
        FLIM2=100.0D+00
      END IF
C
      WRITE(6,4) ISOTOP,IGEOMT,ITHREE,IFOUR,IFREQ,ICORIO,IFERM1,IFERM2,
     1           ISIGMA,IPRNT,NATOM,N3N,NATRI,N3TOT,N4TOT
C
      WRITE(6,5) ESCF
      WRITE(6,6)
      DO 103 I=1,NATOM
      IF(IGEOMT.EQ.5) GO TO 201
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
      II=CHG(I)
      IF(IGEOMT.EQ.48) GO TO 202
      W(I)=ATM(II)
      GO TO 202
  201 CONTINUE
      READ(5,7) COORD(1,I),COORD(2,I),COORD(3,I),W(I)
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
  202 CONTINUE
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  103 CONTINUE
      CALL DIST(1)
      WRITE(6,9)
      DO 104 I=1,NATOM
      X(I)=X(I)*A0
      Y(I)=Y(I)*A0
      Z(I)=Z(I)*A0
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  104 CONTINUE
      CALL DIST(1)
C
C   DYNAMIC ALLOCATION
C   IC1  : FX   (N3N*N3N)
C   IC2  : ELX  (N3N*N3N)
C   IC3  : ELXM (N3N*N3N)
C   IC4  : UX   (N3N*N3N)
C   IC5  : EPX  (N3N*N3N)
C   IC6  : EPXM (N3N*N3N)
C   IC7  : ZETA (N3N*N3N*3)
C   IC8  : AXY  (3*3*N3N)
C   IC9  : BXY  (3*3*N3N)
C   IC10 : CXY  (3*3*N3N)
C   IC11 : BXYZ (3*3*3*N3N)
C   IC12 : CXYZ (3*3*3*N3N)
C   IC13 : F33  (N3TOT)
C   IC14 : F3X  (N3N*N3N*N3N)
C   IC15 : F3Q  (N3N*N3N*N3N)
C   IC16 : F44  (N4TOT)
C   IC17 : F4X  (N3N*N3N*N3N*N3N)
C   IC18 : F4Q  (N3N*N3N*N3N*N3N)
C
C   DYNAMIC ALLOCATION OF CORE MEMORY AND CLEARANCE OF IT
      IC1=1
      IC2=IC1+N3N*N3N
      IC3=IC2+N3N*N3N
      IC4=IC3+N3N*N3N
      IC5=IC4+N3N*N3N
      IC6=IC5+N3N*N3N
      IC7=IC6+N3N*N3N
      IC8=IC7+N3N*N3N*3
      IC9=IC8+3*3*N3N
      IC10=IC9+3*3*N3N
      IC11=IC10+3*3*N3N
      IC12=IC11+3*3*3*N3N
      IC13=IC12+3*3*3*N3N
      IC14=IC13+N3TOT
      IC15=IC14+N3N*N3N*N3N
      IC16=IC15+N3N*N3N*N3N
      IC17=IC16+N4TOT
      IC18=IC17+N3N*N3N*N3N*N3N
      ICKEEP=IC18+N3N*N3N*N3N*N3N
      CALL ZERO(CC(1),ICKEEP)
C
C   FORM THE FORCE CONSTANT MATRIX
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN FCONST'/)
      ICMAX=ICKEEP
      WRITE(3,11) ICMAX,MAXCOR
C.................FX......
      CALL FCONST(CC(IC1))
C
C    CALCULATE THE CENTER OF MASS AND MOMENTS OF INERTIA
C    FOR THE PARENT MOLECULE
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN MOMENT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................UX      EE.......
      CALL MOMENT(CC(IC4),CC(IC19))
C
      ISOMOL=.FALSE.
      NISO=0
  400 NISO=NISO+1
      IF(NISO.EQ.1) GO TO 401
      IF(NISO.GT.ISOTOP) GO TO 220
      WRITE(6,12)
      ISOMOL=.TRUE.
C
C   READ IN ISOTOPIC ATOMIC MASS
      WRITE(6,6)
      DO 105 I=1,NATOM
      READ(5,10) W(I)
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  105 CONTINUE
      CALL DIST(1)
      WRITE(6,9)
      DO 106 I=1,NATOM
      X(I)=X(I)*A0
      Y(I)=Y(I)*A0
      Z(I)=Z(I)*A0
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  106 CONTINUE
      CALL DIST(1)
C
C    CALCULATE THE CENTER OF MASS AND MOMENTS OF INERTIA
C    FOR AN ISOTOPIC MOLECULE
      CALL ZERO(CC(IC2),ICKEEP)
      WRITE(3,22)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................UX      EE.......
      CALL MOMENT(CC(IC4),CC(IC19))
C
C   THE NORMAL COORDINATE ANALYSIS
C
C   THE FX MATRIX METHOD
  401 CONTINUE
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN VIBFX'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N
      IC22=IC21+N3N
      IC23=IC22+N3N
      IC24=IC23+NATRI
      ICMAX=IC24+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C................FX      ELX     ELXM    UX      EPX     EPXM....
      CALL VIBFX(CC(IC1),CC(IC2),CC(IC3),CC(IC4),CC(IC5),CC(IC6),
C................EE       VALU     FV1      FV2      FXM      DD.......
     1           CC(IC19),CC(IC20),CC(IC21),CC(IC22),CC(IC23),CC(IC24),
C................CC.......
     2           CC(IC24))
C
C   CORIOLIS COUPLING CONSTANTS
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN CORIOL'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N*3
      WRITE(3,11) ICMAX,MAXCOR
C.................EPXM    ZETA    EE       FF       AM.......
      CALL CORIOL(CC(IC6),CC(IC7),CC(IC19),CC(IC20),CC(IC21))
C
C   CALCULATE AXY MATRICES
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN ABCMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................EPXM    AXY     BXY     CXY      EE.......
      CALL ABCMAT(CC(IC6),CC(IC8),CC(IC9),CC(IC10),CC(IC19))
C
C   CALCULATE TAU MATRICES
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN TAUMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................AXY     BXY.....
      CALL TAUMAT(CC(IC8),CC(IC9))
C
C   CALCULATE BXYZ MATRICES
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN BCXYZ'/)
      ICMAX=ICKEEP
      WRITE(3,11) ICMAX,MAXCOR
C................ZETA    BXY     BXYZ     CXYZ.....
      CALL BCXYZ(CC(IC7),CC(IC9),CC(IC11),CC(IC12))
C
C   READ CUBIC AND QUARTIC FORCE CONSTANTS FROM THE TAPE48
      IF(ITHREE.NE.48.AND.IFOUR.NE.48) GO TO 402
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN READ48'/)
      IC19=ICKEEP
      ICMAX=IC19+NVIB*NVIB*NVIB*NVIB
      WRITE(3,11) ICMAX,MAXCOR
C.................F3Q      F4Q      F3X      F4X      DD       CC.......
      CALL READ48(CC(IC15),CC(IC18),CC(IC19),CC(IC19),CC(IC19),CC(IC19))
C
C   CALCULATE CUBIC FORCE CONSTANTS
  402 CONTINUE
      IF(ITHREE.EQ.0) GO TO 303
      IF(ITHREE.EQ.48) GO TO 303
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN FORCE3'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ELX     F33      F3X      F3Q      EE       DD.......
      CALL FORCE3(CC(IC2),CC(IC13),CC(IC14),CC(IC15),CC(IC19),CC(IC20),
C.................GG.......
     1            CC(IC21))
C
C   CALCULATE PHI MATRICES
  303 CONTINUE
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN PHIMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................BXY     CXY      BXYZ     CXYZ     F3Q      EE.......
      CALL PHIMAT(CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC15),CC(IC19))
C
C   CALCULATE QUARTIC FORCE CONSTANTS
      IF(IFOUR.EQ.0) GO TO 304
      IF(IFOUR.EQ.48) GO TO 304
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN FORCE4'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ELX     F44      F4X      F4Q      EE       DD.......
      CALL FORCE4(CC(IC2),CC(IC16),CC(IC17),CC(IC18),CC(IC19),CC(IC20),
C.................GG.......
     1            CC(IC21))
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C%%%ANHARMONICITY ANALYSIS %%%
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C   SELECT THE ROTATIONAL TYPE OF THE MOLECULE
  304 CONTINUE
      GO TO (501,502,503,504,504,505),ITOP
C
C:::::::::::::::::::::::
C:::DIATOMIC MOLECULE:::
C:::::::::::::::::::::::
  501 CONTINUE
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN DIATOM'/)
C%    IC19=ICKEEP
C%    ICMAX=IC19+N3N*N3N
C%    WRITE(3,11) ICMAX,MAXCOR
C%................F3Q      F4Q      EE.......
C%    CALL DIATOM(CC(IC15),CC(IC18),CC(IC19))
      WRITE(6,*) '    '
      WRITE(6,*) '  ***************************************************'
      WRITE(6,*) '  SUBROUTINE FOR A DIATOMIC MOLECULE IS NOT AVAILABLE'
      WRITE(6,*) '  ***************************************************'
      WRITE(6,*) '    '
      GO TO 215
C
C::::::::::::::::::::::::::::::::
C:::LINEAR POLYATOMIC MOLECULE:::
C::::::::::::::::::::::::::::::::
  502 CONTINUE
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN LINEAR'/)
C%    IC19=ICKEEP
C%    IC20=IC19+N3N*N3N
C%    ICMAX=IC20+N3N*N3N
C%    WRITE(3,11) ICMAX,MAXCOR
C%................ZETA    CXY      F3Q      CLTYP....
C%    CALL LTYPLI(CC(IC7),CC(IC10),CC(IC15),CC(IC19))
C%................ZETA    AXY     F3Q      EE       RVCNST...
C%    CALL ROVLIR(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C%................BXY     CXY      F3Q      EE.......
C%    CALL CENTLI(CC(IC9),CC(IC10),CC(IC15),CC(IC19))
C%    IF(IVDBL.LE.1) GO TO 205
C%................F3Q      F4Q      CLTYP....
C%    CALL VLTYLI(CC(IC15),CC(IC18),CC(IC19))
C%205 CONTINUE
C%................ZETA    F3Q      F4Q      XIJ      GIJ......
C%    CALL LINEAR(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
      WRITE(6,*) '    '
      WRITE(6,*) '  *************************************************'
      WRITE(6,*) '  SUBROUTINE FOR A LINEAR MOLECULE IS NOT AVAILABLE'
      WRITE(6,*) '  *************************************************'
      WRITE(6,*) '    '
      GO TO 215
C
C:::::::::::::::::::::::::::::
C:::ASYMMETRIC TOP MOLECULE:::
C:::::::::::::::::::::::::::::
  503 CONTINUE
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN ASYTOP'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      ICMAX=IC20+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ZETA    AXY     F3Q      EE       RVCNST...
      CALL ROVASY(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................F3Q      EE.......
      CALL CENTAS(CC(IC15),CC(IC19))
C.................ZETA    F3Q      F4Q      XIJ......
      CALL ASYTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19))
      GO TO 215
C
C::::::::::::::::::::::::::::
C:::SYMMETRIC TOP MOLECULE:::
C::::::::::::::::::::::::::::
  504 CONTINUE
      WRITE(3,36)
   36 FORMAT(//,2X,' NOW YOU ARE IN SYMTOP'/)
C%    IC19=ICKEEP
C%    IC20=IC19+N3N*N3N
C%    ICMAX=IC20+N3N*N3N
C%    WRITE(3,11) ICMAX,MAXCOR
C%................ZETA    AXY     F3Q      EE       CLTYP....
C%    CALL LTYPSY(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C%................ZETA    AXY     F3Q      EE       RVCNST...
C%    CALL ROVSYM(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C%................F3Q      EE.......
C%    CALL CENTSY(CC(IC15),CC(IC19))
C%    IF(IVDBL.LE.1) GO TO 206
C%................ZETA    AXY     F3Q      F4Q      CLTYP....
C%    CALL VLTYSY(CC(IC7),CC(IC8),CC(IC15),CC(IC18),CC(IC19))
C%206 CONTINUE
C%................ZETA    F3Q      F4Q      XIJ      GIJ......
C%    CALL SYMTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
      WRITE(6,*) '    '
      WRITE(6,*) '  ***********************************************'
      WRITE(6,*) '  SUBROUTINE FOR A SYMMETRIC TOP IS NOT AVAILABLE'
      WRITE(6,*) '  ***********************************************'
      WRITE(6,*) '    '
      GO TO 215
C
C::::::::::::::::::::::::::::
C:::SPHERICAL TOP MOLECULE:::
C::::::::::::::::::::::::::::
  505 CONTINUE
      WRITE(3,37)
   37 FORMAT(//,2X,' NOW YOU ARE IN SPHTOP'/)
C%    IC19=ICKEEP
C%    IC20=IC19+N3N*N3N
C%    ICMAX=IC20+N3N*N3N
C%    WRITE(3,11) ICMAX,MAXCOR
C%................ZETA    AXY     F3Q      EE       RVCNST...
C%    CALL ROVSPH(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C%................F3Q      EE.......
C%    CALL CENTSP(CC(IC15),CC(IC19))
C%................ZETA    F3Q      F4Q      XIJ      GIJ......
C%    CALL SPHTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
      WRITE(6,*) '    '
      WRITE(6,*) '  ***********************************************'
      WRITE(6,*) '  SUBROUTINE FOR A SPHERICAL TOP IS NOT AVAILABLE'
      WRITE(6,*) '  ***********************************************'
      WRITE(6,*) '    '
C
C##########################################
C   RETURN FOR ANOTHER ISOTOPIC MOLECULE
C##########################################
  215 CONTINUE
      GO TO 400
C
  220 CONTINUE
      CALL TSTOP(6)
C
      STOP
      END
