      SUBROUTINE POLDER(UA,UF,SSAF,SSFG,ZETA,SA,EA,EF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ZETA(NTRI,NTYPEP)
      DIMENSION SSAF(NTRI,N3N,3),SSFG(NTRI,3,3),SA(NTRI,N3N)
      DIMENSION UA(NBASIS,NBASIS,N3N),UF(NBASIS,NBASIS,3)
      DIMENSION EA(NBASIS,NBASIS,N3N),EF(NBASIS,NBASIS,3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/EAFGS/AF1(3,3,150),AF2(3,3,150),AF3(3,3,150),AFT(3,3,150)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SECTF/ISF(153),IHF(153),IFF(153),IEF(153),IBF(153),IUF(153)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      DATA ZERO,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' ZETA MATRIX, ITYP = ',I5/)
    2 FORMAT(//,2X,' SA MATRIX, IABC = ',I5/)
    3 FORMAT(//,2X,' EA MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' EF MATRIX, IXYZ = ',I5/)
    5 FORMAT(2X,3I5,5X,4F10.6)
C
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
      IF(IPRNT.LE.2) GO TO 101
      WRITE(6,1) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=ZERO
C
      CALL SREW(ITAP44)
      DO 103 IABC=1,N3N
      ISA=ISF(IABC)
      IEA=IEF(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,ISA)
      CALL RREAD(ITAP44,EA(1,1,IABC),NBASQ,IEA)
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,2) IABC
      CALL PRINT(SA(1,IABC),NTRI,NBASIS,6)
      WRITE(6,3) IABC
      CALL MATOUT(EA(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  103 CONTINUE
      DO 104 IXYZ=1,3
      IEX=IEF(IXYZ+N3N)
      CALL RREAD(ITAP44,EF(1,1,IXYZ),NBASQ,IEX)
      IF(IPRNT.LE.4) GO TO 104
      WRITE(6,4) IXYZ
      CALL MATOUT(EF(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  104 CONTINUE
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::F COORDINATE:::
      DO 120 JXYZ=1,3
C
C:::G COORDINATE:::
      DO 110 KXYZ=1,3
C
      VALU1=ZERO
      VALU2=ZERO
      VALU3=ZERO
C
      DO 108 I=1,NBASIS
      ITYP=MOTYP(I)
      DO 108 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=VALU1+SSAF(IJ,IABC,JXYZ)*EF(I,J,KXYZ)
     1           +SSFG(IJ,JXYZ,KXYZ)*EA(I,J,IABC)
     2           +SSAF(IJ,IABC,KXYZ)*EF(I,J,JXYZ)
      VAL=ZERO
      DO 107 K=1,NBASIS
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      FAC1=(UF(J,K,KXYZ)*TWO+UF(K,J,KXYZ))*ZETA(IK,ITYP)
     1     +UF(K,I,KXYZ)*ZETA(JK,ITYP)
      FAC2=(UA(J,K,IABC)*TWO+UA(K,J,IABC))*ZETA(IK,ITYP)
     1     +UA(K,I,IABC)*ZETA(JK,ITYP)
      FAC3=(UF(J,K,JXYZ)*TWO+UF(K,J,JXYZ))*ZETA(IK,ITYP)
     1     +UF(K,I,JXYZ)*ZETA(JK,ITYP)
      VALU2=VALU2+SSAF(IJ,IABC,JXYZ)*FAC1
     1           +SSFG(IJ,JXYZ,KXYZ)*FAC2
     2           +SSAF(IJ,IABC,KXYZ)*FAC3
      DO 106 L=1,NBASIS
      VAL=VAL+SA(IK,IABC)*(UF(K,L,JXYZ)*UF(L,J,KXYZ)
     1                    +UF(K,L,KXYZ)*UF(L,J,JXYZ))
  106 CONTINUE
  107 CONTINUE
      VALU3=VALU3+ZETA(IJ,ITYP)*VAL
  108 CONTINUE
C
      VALU1=VALU1*TWO
      VALU2=VALU2*TWO
      VALU3=-VALU3*FOUR
      VALUX=VALU1+VALU2+VALU3
      AF1(JXYZ,KXYZ,IABC)=VALUX
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,5) IABC,JXYZ,KXYZ,VALU1,VALU2,VALU3,VALUX
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
