      SUBROUTINE PQMAT(DP,DQ,DM,NABC,BUFI,LBLI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION BUFI(MAXBF2),LBLI(MAXBF4)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      DATA ZERO,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(2X,6I5,F15.8)
    2 FORMAT(//,2X,' DM MATRIX'/)
C
C    DP : COULOMB DENSITY TYPE SO MATRIX
C    DQ : EXCHANGE DENSITY TYPE SO MATRIX
C    DM : HALF TRANSFORMED SO MATRIX
C
C   FORM DM MATRICES
      DO 101 IABC=1,NABC
      DO 101 I=1,NTRI
      DM(I,IABC)=ZERO
  101 CONTINUE
C
      CALL RFILE(ITAP36)
  500 CALL SREAD(ITAP36,LBLI,MAXBF4)
      DO 160 I=1,MAXBUF
      IBA=LBLI(I+I-1)
      IBB=LBLI(I+I)
      IF(IBA.EQ.0) GO TO 501
      VALU=BUFI(MAXBUF+I)
      CALL UNPACK(II,JJ,KK,LL,IX,IBA,IBB)
C     IF(IPRNT.LE.9) GO TO 301
C     WRITE(6,1) I,II,JJ,KK,LL,IX,VALU
  301 GO TO (201,202,203,204,205,206,207,208,209,210,211,212,213,214),IX
C
C (II/II)   1=(11/11)
C
  201 IIII=IOFF(II+1)
      DO 112 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+(DP(IIII,IABC)+DQ(IIII,IABC))*VALU
  112 CONTINUE
      GO TO 160
C
C  (II/KK)  2=(22/11)
C
  202 IIII=IOFF(II+1)
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      DO 116 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIII,IABC)*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(IIKK,IABC)*VALU
  116 CONTINUE
      GO TO 160
C
C  (II/IL)  3=(22/21)
C
  203 IIII=IOFF(II+1)
      IILL=IOFF(II)+LL
      DO 120 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+(DP(IILL,IABC)+DQ(IILL,IABC))*VALU*TWO
      DM(IILL,IABC)=DM(IILL,IABC)+(DP(IIII,IABC)+DQ(IIII,IABC))*VALU
  120 CONTINUE
      GO TO 160
C
C  (IJ/JJ)  4=(21/11)
C
  204 IIJJ=IOFF(II)+JJ
      JJJJ=IOFF(JJ+1)
      DO 124 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(JJJJ,IABC)+DQ(JJJJ,IABC))*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+(DP(IIJJ,IABC)+DQ(IIJJ,IABC))*VALU*TWO
  124 CONTINUE
      GO TO 160
C
C  (IJ/IJ)  5=(21/21)
C
  205 IIJJ=IOFF(II)+JJ
      IIII=IOFF(II+1)
      JJJJ=IOFF(JJ+1)
      DO 128 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIII,IABC)=DM(IIII,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IIII,IABC)*VALU
  128 CONTINUE
      GO TO 160
C
C  (II/KL)  6=(33/21)
C
  206 IIII=IOFF(II+1)
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      DO 132 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+DP(KKLL,IABC)*VALU*TWO
      DM(KKLL,IABC)=DM(KKLL,IABC)+DP(IIII,IABC)*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(IILL,IABC)*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(IIKK,IABC)*VALU
  132 CONTINUE
      GO TO 160
C
C  (IJ/KK)  7=(32/11)
C
  207 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=IOFF(JJ)+KK
      DO 136 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIJJ,IABC)*VALU*TWO
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IIKK,IABC)*VALU
  136 CONTINUE
      GO TO 160
C
C  (IJ/KK)  8=(31/22)
C
  208 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=JJ+IOFF(KK)
      DO 140 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIJJ,IABC)*VALU*TWO
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IIKK,IABC)*VALU
  140 CONTINUE
      GO TO 160
C
C (IJ/IL)  9=(32/31)
C
  209 IIJJ=IOFF(II)+JJ
      IILL=IOFF(II)+LL
      IIII=IOFF(II+1)
      JJLL=IOFF(JJ)+LL
      DO 144 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(IILL,IABC)*TWO+DQ(IILL,IABC))*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIII,IABC)=DM(IIII,IABC)+DQ(JJLL,IABC)*VALU*TWO
      DM(JJLL,IABC)=DM(JJLL,IABC)+DQ(IIII,IABC)*VALU
  144 CONTINUE
      GO TO 160
C
C  (IJ/JL)  10=(32/21)
C
  210 IIJJ=IOFF(II)+JJ
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJJJ=IOFF(JJ+1)
      DO 148 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(JJLL,IABC)*TWO+DQ(JJLL,IABC))*VALU
      DM(JJLL,IABC)=DM(JJLL,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IILL,IABC)*VALU*TWO
  148 CONTINUE
      GO TO 160
C
C  (IJ/KJ)  11=(31/21)
C
  211 IIJJ=IOFF(II)+JJ
      KKJJ=IOFF(KK)+JJ
      IIKK=IOFF(II)+KK
      JJJJ=IOFF(JJ+1)
      DO 152 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(KKJJ,IABC)*TWO+DQ(KKJJ,IABC))*VALU
      DM(KKJJ,IABC)=DM(KKJJ,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IIKK,IABC)*VALU*TWO
  152 CONTINUE
      GO TO 160
C
C  (IJ/KL)  12=(43/21)
C
  212 IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJKK=IOFF(JJ)+KK
      GO TO 220
C
C  (IJ/KL)  13=(42/31)
C
  213 IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJKK=JJ+IOFF(KK)
      GO TO 220
C
C  (IJ/KL)  14=(41/32)
C
  214 IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      JJLL=JJ+IOFF(LL)
      IILL=IOFF(II)+LL
      JJKK=JJ+IOFF(KK)
C
  220 DO 156 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKLL,IABC)*VALU*TWO
      DM(KKLL,IABC)=DM(KKLL,IABC)+DP(IIJJ,IABC)*VALU*TWO
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJLL,IABC)*VALU
      DM(JJLL,IABC)=DM(JJLL,IABC)+DQ(IIKK,IABC)*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IILL,IABC)*VALU
  156 CONTINUE
C
  160 CONTINUE
      GO TO 500
C
  501 CALL SREW(ITAP36)
C
      IF (IPRNT.LE.4) GO TO 502
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,NABC,NTRI,NABC,6)
C
  502 CONTINUE
      RETURN
      END
