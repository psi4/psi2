      SUBROUTINE AFMAT(FA,HF,HAF,HH,TT)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION HH(NTRI),TT(NTRI)
      DIMENSION FA(NTRI,N3N,NTYPEP),HF(NTRI,3,NTYPEP)
      DIMENSION HAF(NBASIS,NBASIS,N3N,3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/DIPOL/DIPDA(135),DIPDN(135)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SECTF/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' FA MATRIX, ITYP = ',I5,' IABC = ',I5/)
    2 FORMAT(//,2X,' DIPDA MATRIX'/)
    3 FORMAT(//,2X,' DIPDN MATRIX'/)
    4 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5,' ITYP = ',I5/)
    5 FORMAT(//,2X,' HAF MATRIX, IABC = ',I5,' IXYZ = ',I5/)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
C
C   FORM DERIVATIVE GENERALIZED LAGRANGIAN MATRICES
C   FOR NUCLEAR PERTURBATION
      DO 101 ITYP=1,NTYPEP
      DO 101 IABC=1,N3N
      DO 101 I=1,NTRI
      FA(I,IABC,ITYP)=ZERO
  101 CONTINUE
      DO 104 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HH,NTRI2,IH)
      DO 103 ITYP=1,NTREAD
      FAC=FOCC(ITYP)*HALF
      DO 102 I=1,NTRI
  102 FA(I,IABC,ITYP)=HH(I)*FAC
  103 CONTINUE
  104 CONTINUE
C
      DO 107 ITYP=1,NTREAD
      DO 106 IABC=1,N3N
      CALL SREAD(JTAPE1,TT,NTRI2)
      DO 105 I=1,NTRI
      FA(I,IABC,ITYP)=FA(I,IABC,ITYP)+TT(I)
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 106
      WRITE(6,1) ITYP,IABC
      CALL PRINT(FA(1,IABC,ITYP),NTRI,NBASIS,6)
  106 CONTINUE
  107 CONTINUE
C
C   FORM DERIVATIVE GENERALIZED LAGRANGIAN MATRICES
C   FOR ELECTRI FIELD PERTURBATION
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,2)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,3)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
C
  201 CONTINUE
      DO 110 ITYP=1,NTYPEP
      DO 110 IXYZ=1,3
      DO 110 I=1,NTRI
      HF(I,IXYZ,ITYP)=ZERO
  110 CONTINUE
      DO 113 IXYZ=1,3
      CALL SREAD(ITAP43,HH,NTRI2)
      DO 112 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 111 I=1,NTRI
  111 HF(I,IXYZ,ITYP)=HH(I)*FAC
      IF(IPRNT.LE.2) GO TO 112
      WRITE(6,4) ITYP,IXYZ
      CALL PRINT(HF(1,IXYZ,ITYP),NTRI,NBASIS,6)
  112 CONTINUE
  113 CONTINUE
C
      DO 116 IABC=1,N3N
      DO 115 IXYZ=1,3
      CALL SREAD(ITAP43,HH,NTRI2)
      DO 114 I=1,NBASIS
      ITYP=MOTYP(I)
      FAC=FOCC(ITYP)*HALF
      DO 114 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      HAF(I,J,IABC,IXYZ)=HH(IJ)*FAC
  114 CONTINUE
      IF(IPRNT.LE.4) GO TO 115
      WRITE(6,5) IABC,IXYZ
      CALL MATOUT(HAF(1,1,IABC,IXYZ),NBASIS,NBASIS,NBASIS,NBASIS,6)
  115 CONTINUE
  116 CONTINUE
C
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
C
      RETURN
      END
