      SUBROUTINE POLAD1(UA,UF,FA,HF,HAF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION UA(NBASIS,NBASIS,N3N),UF(NBASIS,NBASIS,3)
      DIMENSION FA(NTRI,N3N,NTYPEP),HF(NTRI,3,NTYPEP)
      DIMENSION HAF(NBASIS,NBASIS,N3N,3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/DIPOL/DIPDA(135),DIPDN(135)
      COMMON/EAFGS/AF1(3,3,150),AF2(3,3,150),AF3(3,3,150),AFT(3,3,150)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SECTF/ISF(153),IHF(153),IFF(153),IEF(153),IBF(153),IUF(153)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA ZERO,HALF,FOUR / 0.0D+00 , 0.5D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' DDER MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    2 FORMAT(//,2X,' EAF MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    3 FORMAT(2X,3I5,5X,5F10.6)
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::F COORDINATE:::
      DO 120 JXYZ=1,3
C
C:::G COORDINATE:::
      DO 110 KXYZ=1,3
C
      VALU1=ZERO
      VALU2=ZERO
      DO 102 I=1,NBASIS
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=VALU1+UF(I,J,JXYZ)*HAF(J,I,IABC,KXYZ)
     1           +UF(I,J,KXYZ)*HAF(J,I,IABC,JXYZ)
      DO 101 K=1,NBASIS
      KTYP=MOTYP(K)
      VALU2=VALU2+FA(IJ,IABC,KTYP)*UF(I,K,JXYZ)*UF(J,K,KXYZ)
     1           +HF(IJ,JXYZ,KTYP)*UF(I,K,KXYZ)*UA(J,K,IABC)
     2           +HF(IJ,KXYZ,KTYP)*UA(I,K,IABC)*UF(J,K,JXYZ)
  101 CONTINUE
  102 CONTINUE
C
      VALU1=-VALU1*FOUR
      VALU2=-VALU2*FOUR
      VALUY=VALU1+VALU2
      AF2(JXYZ,KXYZ,IABC)=VALUY
      VALUX=AF1(JXYZ,KXYZ,IABC)
      VALUT=VALUX+VALUY
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,3) IABC,JXYZ,KXYZ,VALU1,VALU2,VALUX,VALUY,VALUT
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      RETURN
      END
