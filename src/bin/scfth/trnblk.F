       SUBROUTINE TRNBLK(F,C,T1,T2,NUMSYM,NSYM,NUMBLK,NNPBLK,JOB)
C
C***PURPOSE: TO TRANSFORM A ONE-ELECTRON SYMMETRY-BLOCKED TRIANGULAR
C            MATRIX F WITH A SYMMETRY BLOCKED SQUARE MATRIX C.
C
C            IF JOB = 0 , USE C
C               JOB = 1 , USE C(TRANSPOSE), IE. BACK-TRANSFORM.
C
C PAUL SAXE              5 NOVEMBER 1984                LANL
C
      IMPLICIT INTEGER (A-Z)
C
      REAL*8 F(NNPBLK),C(NUMBLK),T1(*),T2(*)
      INTEGER NUMSYM(NSYM)
C
C     ----- LOOP OVER SYMMETRY BLOCKS -----
C
      PTTRI=1
      PTSQR=1
      DO 1 SYM=1,NSYM
         NUMS=NUMSYM(SYM)
         IF (NUMS.LE.0) GO TO 1
         NNPS=(NUMS+1)*NUMS/2
C
C        ----- UNPACK THE TRIANGULAR F MATRIX -----
C
         CALL TRTOSQ(T1,F(PTTRI),NUMS,NNPS)
C
C        ----- TRANSFORM -----
C
         IF (JOB.EQ.0) THEN
            CALL EBC(T2,T1,C(PTSQR),NUMS,NUMS,NUMS)
            CALL EBTC(T1,C(PTSQR),T2,NUMS,NUMS,NUMS)
         ELSE IF (JOB.EQ.1) THEN
            CALL EBCT(T2,T1,C(PTSQR),NUMS,NUMS,NUMS)
            CALL EBC(T1,C(PTSQR),T2,NUMS,NUMS,NUMS)
         ELSE
            STOP 13
         END IF
C
C        ----- REPACK THE TRIANGULAR MATRIX -----
C
         CALL SQTOTR(F(PTTRI),T1,NUMS,NNPS)
C
         PTTRI=PTTRI+NNPS
         PTSQR=PTSQR+NUMS**2
    1 CONTINUE
C
C
      RETURN
      END
