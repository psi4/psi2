      SUBROUTINE VECS(D,NBF,IOUT,ACRCY,X,IX)
C
C  THIS SUBROUTINE READS IN THE SCF VECTOR AND ADJUSTS IT TO THE
C  DRT REPRESENTATION.
C
C  X AND IX ARE IMPLICITLY EQUIVALENCED
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
C
      INTEGER FCB10(16)
C
      COMMON /OUT/    IOUT6,ICHECK,IDBG,ICNT
      COMMON /TAPES/  IT34,IT58,ITAPE7,IT52,IT91,IT93,IT95
      COMMON /TAPE30/ I10(200),ITAP30
C
      DIMENSION IOUT(NBF),IX(1)
      DIMENSION D(NBF,NBF)
      DIMENSION X(1)
      DIMENSION NLAMDA(8)
C
      WRITE(IOUT6,701) ACRCY
  701 FORMAT (1X,'THRESHOLD FOR ELIMINATION OF VECTOR ELEMENTS = ',
     #1PE10.2,/)
C
      CALL ZERO(D,NBF**2)
C
C
C     ----- OPEN TAPE30 AND GET CONSTANTS AND POINTER TO CALC ----
C
      ITAP30=30
      CALL RFILE(ITAP30)
      CALL WREADW(ITAP30,I10,200,101,JUNK)
      MPOINT=I10(2)
      MCONST=I10(3)
      MCALCS=I10(4)
      NCALCS=I10(5)
      NSYMHF=I10(41)
      MXCF=I10(42)
C
      JUNK=101+MCONST+MPOINT
      CALL WREADW(ITAP30,I10,MCALCS,JUNK,JUNK)
C
      LOCCAL=I10(NCALCS)
      JUNK=LOCCAL+60
      CALL WREADW(ITAP30,LOCVEC,1,JUNK,JUNK)
C
C     ----- READ VECTOR -----
C
      CALL WREADW(ITAP30,IX,INTOWP(MXCF),LOCVEC,LOCVEC)
C
      LOCVEC=LOCVEC+INTOWP(NBF)+NSYMHF
      CALL WREADW(ITAP30,NLAMDA,NSYMHF,LOCVEC,LOCVEC)
C
      CLOSE (ITAP30)
C
C     ----- TRANSFER VECTOR TO D MATRIX -----
C
      DO 1 I=1,MXCF
           IF (ABS(X(I)).LT.ACRCY) X(I)=0.0D+00
    1 CONTINUE
      IOF=0
      IJ=0
      DO 10 ISYM=1,NSYMHF
           N=NLAMDA(ISYM)
           DO 9 J=1,N
                DO 8 I=1,N
                     IJ=IJ+1
                     D(IOF+I,IOF+J)=X(IJ)
    8           CONTINUE
    9      CONTINUE
           IOF=IOF+N
   10 CONTINUE
clj
c     write(6,*) 'vecs: d:'
c     call matout(d,nbf,nbf,nbf,nbf,6)
C
      RETURN
      END
