      SUBROUTINE SYMH(D2T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION TH(3,3),WH(3,3),RH(3,3),WK(3,3)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOM,N3N,NATRI,NSYM
      COMMON/GAUSB/KATOM(200),KTYPE(200),KSTART(200),KLOC(200),
     1             KMIN(200),KMAX(200),KNG(200)
CTPH  COMMON/GAUSC/T(36),PTR(3,3,8),DTR(6,6,8)
      COMMON/GAUSC/T(225),PTR(3,3,8),DTR(6,6,8),FTR(10,10,8),
     1             GTR(15,15,8)
      COMMON/GAUSD/INVT(8),ISO(200,8),LBLAT(2,50),ICT(50,8)
      COMMON/PARA1/NSHELL,NT,MXT,NISO
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TOLER/DLIM
      DIMENSION D2T(N3N,N3N)
      DATA ZERO,ONE / 0.0D+00, 1.0D+00 /
C
      IF(NT.EQ.1) RETURN
C
C   SYMMETRIZE FORCE-CONSTANT MATRIX
C
      DO 2000 IC=1,NATOM
      DO 101 IT=1,NT
      ICNEW=ICT(IC,IT)
      IF(ICNEW.GT.IC) GO TO 2000
  101 CONTINUE
      DO 1000 JC=1,IC
      DO 102 IT=1,NT
      ICNEW=MAX0(ICT(IC,IT),ICT(JC,IT))
      JCNEW=MIN0(ICT(IC,IT),ICT(JC,IT))
      IF(ICNEW.GT.IC) GO TO 1000
      IF(ICNEW.EQ.IC.AND.JCNEW.GT.JC) GO TO 1000
  102 CONTINUE
      IJPR1=IOFF(IC)+JC
C
C   SUMMATION OF ALL PARTIAL CONTRIBUTION TO CORRECT MATRIX
      DO 103 I=1,3
      DO 103 J=1,3
  103 WH(J,I)=ZERO
      DO 115 IT=1,NT
      ICNEW=MAX0(ICT(IC,IT),ICT(JC,IT))
      JCNEW=MIN0(ICT(IC,IT),ICT(JC,IT))
      IJPR2=IOFF(ICNEW)+JCNEW
      IINDX=(ICNEW-1)*3
      JINDX=(JCNEW-1)*3
      IF(ICT(JC,IT).GT.ICT(IC,IT)) GO TO 201
      DO 104 I=1,3
      DO 104 J=1,3
  104 TH(J,I)=D2T(IINDX+J,JINDX+I)
      GO TO 202
  201 CONTINUE
      DO 105 I=1,3
      DO 105 J=1,3
  105 TH(I,J)=D2T(IINDX+J,JINDX+I)
  202 CONTINUE
      DO 106 I=1,3
      DO 106 J=1,3
  106 RH(J,I)=PTR(J,I,IT)
      DO 108 I=1,3
      DO 108 J=1,3
      SUM=ZERO
      DO 107 K=1,3
  107 SUM=SUM+TH(J,K)*RH(K,I)
  108 WK(J,I)=SUM
      DO 110 I=1,3
      DO 110 J=1,3
      SUM=ZERO
      DO 109 K=1,3
  109 SUM=SUM+RH(K,J)*WK(K,I)
  110 WH(J,I)=WH(J,I)+SUM
  115 CONTINUE
C
      IINDX=(IC-1)*3
      JINDX=(JC-1)*3
      DO 116 I=1,3
      DO 116 J=1,3
  116 D2T(IINDX+J,JINDX+I)=WH(J,I)
C
C   TRANSFER CORRECT BLOCK INTO EQUIVALENT BLOCKS
C
      DO 125 IT=1,NT
      ICNEW=MAX0(ICT(IC,IT),ICT(JC,IT))
      JCNEW=MIN0(ICT(IC,IT),ICT(JC,IT))
      IJPR2=IOFF(ICNEW)+JCNEW
      IF(IJPR2.EQ.IJPR1) GO TO 125
      IF(IT.EQ.NT) GO TO 203
      IT1=IT+1
      DO 117 JT=IT1,NT
      ICSAM=MAX0(ICT(IC,JT),ICT(JC,JT))
      JCSAM=MIN0(ICT(IC,JT),ICT(JC,JT))
      IJPR3=IOFF(ICSAM)+JCSAM
      IF(IJPR3.EQ.IJPR2) GO TO 125
  117 CONTINUE
  203 CONTINUE
      N=INVT(IT)
      DO 118 I=1,3
      DO 118 J=1,3
  118 RH(J,I)=PTR(J,I,N)
      DO 120 I=1,3
      DO 120 J=1,3
      SUM=ZERO
      DO 119 K=1,3
  119 SUM=SUM+WH(J,K)*RH(K,I)
  120 WK(J,I)=SUM
      DO 122 I=1,3
      DO 122 J=1,3
      SUM=ZERO
      DO 121 K=1,3
  121 SUM=SUM+RH(K,J)*WK(K,I)
  122 TH(J,I)=SUM
      IINDX=(ICNEW-1)*3
      JINDX=(JCNEW-1)*3
      IF(ICT(JC,IT).GT.ICT(IC,IT)) GO TO 204
      DO 123 I=1,3
      DO 123 J=1,3
  123 D2T(IINDX+J,JINDX+I)=TH(J,I)
      GO TO 125
  204 CONTINUE
      DO 124 I=1,3
      DO 124 J=1,3
  124 D2T(IINDX+J,JINDX+I)=TH(I,J)
C
  125 CONTINUE
C
 1000 CONTINUE
 2000 CONTINUE
C
      FACT=ONE/DFLOAT(NT)
      DO 130 IC=1,N3N
      DO 130 JC=1,N3N
  130 D2T(IC,JC)=D2T(IC,JC)*FACT
      DO 131 I=1,N3N
      DO 131 J=1,I
  131 D2T(J,I)=D2T(I,J)
C
      RETURN
      END
