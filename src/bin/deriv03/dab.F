      SUBROUTINE DAB(D,DT,Q4)
      IMPLICIT REAL*8 (A-H,O-Z)
ctph  COMMON/ABDEN/ABDENS(1296)
C11-14-88  (For  7Mb) COMMON/ABDEN/ABDENS(10000)
C11-14-88  (For 15Mb) COMMON/ABDEN/ABDENS(50625)
      COMMON/ABDEN/ABDENS(10000)
      COMMON/BASIS/NBASIS,NBFAO,NBATRI,NBATR2
      COMMON/CALCS/ISTYPE,ICTYPE,IRAMAN,IAFOCK,IDFOCK,IDINT
      COMMON/COUPL/ALP(5,5),BET(5,5)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOM,N3N,NATRI,NSYM
      COMMON/PARA4/MMIJ,MMKL,ISAME(100)
ctph  COMMON/POSIX/IPOS(100),JPOS(100),KPOS(100),LPOS(100),IMUL(100)
      COMMON/POSIX/IPOS(225),JPOS(225),KPOS(225),LPOS(225),IMUL(225)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TCSCF/SOCC1,SOCC2,SOCC12
      COMMON/TOLER/DLIM
      DIMENSION D(NBATRI,NTYPEP),DT(NBATRI)
      DATA ZERO,HALF,TWO,FOUR,EIGHT / 0.0D+00 , 0.5D+00, 2.0D+00 ,
     1                                4.0D+00 , 8.0D+00 /
C
      DMAX=ZERO
C
      NN=0
      DO 103 II=1,MMIJ
      IT=IPOS(II)
      JT=JPOS(II)
      NFAC=IMUL(II)
      DO 102 JJ=1,MMKL
      KT=KPOS(JJ)
      LT=LPOS(JJ)
      NN=NN+1
      IJT=IOFF(MAX0(IT,JT))+MIN0(IT,JT)
      KLT=IOFF(MAX0(KT,LT))+MIN0(KT,LT)
      IF(IJT.LT.KLT) GO TO 301
      I=MAX0(IT,JT)
      J=MIN0(IT,JT)
      K=MAX0(KT,LT)
      L=MIN0(KT,LT)
      GO TO 302
  301 CONTINUE
      I=MAX0(KT,LT)
      J=MIN0(KT,LT)
      K=MAX0(IT,JT)
      L=MIN0(IT,JT)
C
  302 CONTINUE
      MIJ=IOFF(MAX0(I,J))+MIN0(I,J)
      MKL=IOFF(MAX0(K,L))+MIN0(K,L)
      MIK=IOFF(MAX0(I,K))+MIN0(I,K)
      MJL=IOFF(MAX0(J,L))+MIN0(J,L)
      MIL=IOFF(MAX0(I,L))+MIN0(I,L)
      MJK=IOFF(MAX0(J,K))+MIN0(J,K)
C
      GO TO (201,202,203,204,204,205),ISTYPE
C
C     ----- CLOSED SHELL SINGLET -----
C
  201 CONTINUE
      DFAC=DT(MIJ)*DT(MKL)*FOUR-DT(MIK)*DT(MJL)-DT(MIL)*DT(MJK)
      GO TO 210
C
C     ----- HIGH-SPIN OPEN-SHELL -----
C
  202 CONTINUE
      DFAC=DT(MIJ)*DT(MKL)*FOUR-DT(MIK)*DT(MJL)-DT(MIL)*DT(MJK)
     1                         -D(MIK,2)*D(MJL,2)-D(MIL,2)*D(MJK,2)
      GO TO 210
C
C     ----- OPEN SHELL SINGLET -----
C
  203 CONTINUE
      DFAC=(DT(MIJ)*DT(MKL)-D(MIJ,2)* D(MKL,2)
     1 +    D(MIJ,3)*D(MKL,4)+D(MIJ,4)*D(MKL,3))*FOUR
     2 -    DT(MIK)*DT(MJL)-DT(MIL)*DT(MJK)
     3 +    D(MIK,2)*D(MJL,2)+D(MIL,2)*D(MJK,2)
     4 +    (D(MIK,3)*D(MJL,4)+D(MIK,4)*D(MJL,3)
     5 +    D(MIL,3)*D(MJK,4)+D(MIL,4)*D(MJK,3))*TWO
      GO TO 210
C
C     ----- GENERAL OPEN SHELL SCF -----
C
  204 CONTINUE
      DFAC1=ZERO
      DFAC2=ZERO
      DO 101 ITYP=1,NTYPES
      DO 101 JTYP=1,NTYPES
      DFAC1=DFAC1+D(MIJ,ITYP)*D(MKL,JTYP)*ALP(ITYP,JTYP)
      DFAC2=DFAC2+(D(MIK,ITYP)*D(MJL,JTYP)+D(MIL,ITYP)*D(MJK,JTYP))*
     1      BET(ITYP,JTYP)
  101 CONTINUE
      DFAC=DFAC1*TWO+DFAC2
      GO TO 210
C
C     ----- TWO-CONFIGURATION SCF (GVB) -----
C
  205 CONTINUE
       DFAC=(DT(MIJ)*DT(MKL)-D(MIJ,2)*D(MKL,2)
     1  +   D(MIJ,3)*D(MKL,3)*SOCC1+D(MIJ,4)*D(MKL,4)*SOCC2)*FOUR
     2                   -DT(MIK)* DT(MJL)- DT(MIL)* DT(MJK)
     3  +      D(MIK,2)*D(MJL,2)+D(MIL,2)*D(MJK,2)
     4  +    (D(MIK,3)*D(MJL,4)+D(MIK,4)*D(MJL,3)
     5  +     D(MIL,3)*D(MJK,4)+D(MIL,4)*D(MJK,3))*SOCC12
      GO TO 210
C
  210 CONTINUE
C
      IF(I.EQ.J) DFAC=DFAC*HALF
      IF(K.EQ.L) DFAC=DFAC*HALF
      DFAC=DFAC*Q4*DFLOAT(NFAC)
      IF(DABS(DFAC).GT.DMAX) DMAX=DABS(DFAC)
      ABDENS(NN)=DFAC
  102 CONTINUE
  103 CONTINUE
C
      IF(DMAX.LT.DLIM) Q4=ZERO
C
      RETURN
      END
