      SUBROUTINE POLDER(EIG,UA,UF,SSAF,SSFG,BA,BF,EAF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS)
      DIMENSION UA(NBASIS,NBASIS,N3N),UF(NBASIS,NBASIS,3)
      DIMENSION SSAF(NTRI,N3N,3),SSFG(NTRI,3,3)
      DIMENSION BA(NTRI,N3N),BF(NTRI,3),EAF(NTRI,N3N,3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/DIPOL/DIPDA(135),DIPDN(135)
      COMMON/EAFGS/AFG1(3,3,150),AFG2(3,3,150),AFGT(3,3,150)
      COMMON/FUNCS/NATOMS,N3N,N3NP3,N3NX3
      COMMON/SECTF/ISF(153),IHF(153),IFF(153),IBF(153),IUF(153)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP43,ITAP44
      COMMON/CI101/IHOMO,ILUMO
      DATA ZERO,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' BA MATRIX'/)
    2 FORMAT(//,2X,' BF MATRIX'/)
    3 FORMAT(//,2X,' DDER MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    4 FORMAT(2X,3I5,5X,6F10.6)
    5 FORMAT(//,2X,' AFG1 MATRIX, IABC = ',I5/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,N3N
      IBA=IBF(IABC)
      CALL RREAD(ITAP44,BA(1,IABC),NTRI2,IBA)
  101 CONTINUE
      DO 102 IXYZ=1,3
      IBX=IBF(IXYZ+N3N)
      CALL RREAD(ITAP44,BF(1,IXYZ),NTRI2,IBX)
  102 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(BA,NTRI,N3N,NTRI,N3N,6)
      WRITE(6,2)
      CALL MATOUT(BF,NTRI,3,NTRI,3,6)
  201 CONTINUE
C
C   READ IN DIPOLE DERIVATIVE MATRICES
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      DO 103 IXYZ=1,3
  103 CALL SREAD(ITAP43,EAF(1,1,IXYZ),NTRI2)
      DO 105 IABC=1,N3N
      DO 104 IXYZ=1,3
      CALL SREAD(ITAP43,EAF(1,IABC,IXYZ),NTRI2)
      IF(IPRNT.LE.3) GO TO 104
      WRITE(6,3) IABC,IXYZ
      CALL PRINT(EAF(1,IABC,IXYZ),NTRI,NBASIS,6)
  104 CONTINUE
  105 CONTINUE
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::F COORDINATE:::
      DO 120 JXYZ=1,3
C
C:::G COORDINATE:::
      DO 110 KXYZ=1,3
C
      VALU1=ZERO
      VALU2=ZERO
      VALU3=ZERO
      VALU4=ZERO
      VALU5=ZERO
C
      DO 108 I=1,IHOMO
      II=IOFF(I+1)
      VALU1=VALU1+SSAF(II,IABC,JXYZ)*BF(II,KXYZ)
     1           +SSFG(II,JXYZ,KXYZ)*BA(II,IABC)
     2           +SSAF(II,IABC,KXYZ)*BF(II,JXYZ)
      DO 107 J=1,NBASIS
      JJ=IOFF(J+1)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU2=VALU2+UF(J,I,JXYZ)*EAF(IJ,IABC,KXYZ)
     1           +UF(J,I,KXYZ)*EAF(IJ,IABC,JXYZ)
      VALU3=VALU3+UA(J,I,IABC)*UF(J,I,JXYZ)*BF(JJ,KXYZ)
     1           +UF(J,I,JXYZ)*UF(J,I,KXYZ)*BA(JJ,IABC)
     2           +UF(J,I,KXYZ)*UA(J,I,IABC)*BF(JJ,JXYZ)
      FAC4=SSAF(IJ,IABC,JXYZ)*UF(I,J,KXYZ)
     1    +SSFG(IJ,JXYZ,KXYZ)*UA(I,J,IABC)
     2    +SSAF(IJ,IABC,KXYZ)*UF(I,J,JXYZ)
      VALU4=VALU4+FAC4*EIG(I)
C
      DO 106 M=1,NBASIS
      FAC5=UA(J,I,IABC)*(UF(M,I,JXYZ)*UF(M,J,KXYZ)
     1                  +UF(M,J,JXYZ)*UF(M,I,KXYZ))
     2    +UF(J,I,JXYZ)*(UF(M,I,KXYZ)*UA(M,J,IABC)
     3                  +UF(M,J,KXYZ)*UA(M,I,IABC))
     4    +UF(J,I,KXYZ)*(UA(M,I,IABC)*UF(M,J,JXYZ)
     5                  +UA(M,J,IABC)*UF(M,I,JXYZ))
      VALU5=VALU5+FAC5*EIG(M)
  106 CONTINUE
  107 CONTINUE
  108 CONTINUE
      VALU1=VALU1*TWO
      VALU2=-VALU2*FOUR
      VALU3=-VALU3*FOUR
      VALU4=-VALU4*FOUR
      VALU5=VALU5*FOUR
      VALUT=VALU1+VALU2+VALU3+VALU4+VALU5
      AFG1(JXYZ,KXYZ,IABC)=VALUT
      AFGT(JXYZ,KXYZ,IABC)=VALUT
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,4) IABC,JXYZ,KXYZ,VALU1,VALU2,VALU3,VALU4,VALU5,
     1           VALUT
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      DO 140 IABC=1,N3N
      WRITE(6,5) IABC
      CALL MATOUT(AFG1(1,1,IABC),3,3,3,3,6)
  140 CONTINUE
C
  202 CONTINUE
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
      RETURN
      END
