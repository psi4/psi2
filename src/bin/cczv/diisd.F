C
C-----------------------------------------------------------------------
C
      SUBROUTINE DIISD (T1O,T1N,T2,S,NO,NV,NDIMT2,NIT,IT,IECORR,
     .                  EPSI,NGO,NDIIS,MINDIM,MAXDIM,
     .                  CC,BB,ITAP98,ITAP99,ITC,BB2)
      IMPLICIT INTEGER (A-Z)
#if defined(SGI) || defined(Linux)
      REAL*8 DET
#else
      REAL*16 DET
#endif
      REAL*8 BB,EPS,CC,ONE,XM,XMAX,XEN,XON,XLAM,BB2
      REAL*8 T1N,T1O,S,T2,A0,XFAC,XADD
      DIMENSION CC(MAXDIM),BB(MAXDIM+1,MAXDIM+2),BB2(MAXDIM+1,MAXDIM+2)
      DIMENSION T1O(NO,NV),T1N(NO,NV),T2(NDIMT2),S(NDIMT2)
C
C    NGO,NDIIS,EPSI,MINDIM  ARE DUMMY!
C
      A0 = 0.0D0
      ONE=1.D0
      EPS=10.D0 **(-EPSI)
      IECORR=0
C
C     DIIS EXTRAPOLATION
C
      IFLAG=0
      NCOUNT=0
      NCOUNT=NCOUNT+1
      IT=IT+1
      IF(IT.GT.MAXDIM) IT = MAXDIM
      if(it.eq.1)itc=0
      ITC = ITC + 1
      IF(ITC.GT.MAXDIM) ITC = 1
C
      DO 2050 I=1,NO
      DO 2051 A=1,NV
      T1O(I,A) = T1N(I,A) - T1O(I,A)
 2051 CONTINUE
 2050 CONTINUE
C
      PT1 = (ITC-1)*INTOWP(NO*NV + NDIMT2) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      CALL WWRITW(ITAP99,T1O,INTOWP(NO*NV),PT1,JUNK)
      CALL WWRITW(ITAP98,T1N,INTOWP(NO*NV),PT1,JUNK)
C
      DO 2060 UVBG=1,NDIMT2
      T2(UVBG) = S(UVBG) - T2(UVBG)
 2060 CONTINUE
C
      CALL WWRITW(ITAP99,T2,INTOWP(NDIMT2),PT2,JUNK)
      CALL WWRITW(ITAP98,S,INTOWP(NDIMT2),PT2,JUNK)
C
      DO 2090 N=1,IT
C
      PT1 = (N-1)*INTOWP(NO*NV + NDIMT2) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      CALL WREADW(ITAP99,T1N,INTOWP(NO*NV),PT1,JUNK)
      CALL WREADW(ITAP99,S,INTOWP(NDIMT2),PT2,JUNK)
C
      BB2(N,ITC) = A0
C
      DO 2070 I=1,NO
      DO 2071 A=1,NV
      BB2(N,ITC) = BB2(N,ITC) + T1N(I,A)*T1O(I,A)
 2071 CONTINUE
 2070 CONTINUE
C
      DO 2080 UVBG=1,NDIMT2
      BB2(N,ITC) = BB2(N,ITC) + S(UVBG)*T2(UVBG)
 2080 CONTINUE
      BB2(ITC,N) = BB2(N,ITC)
 2090 CONTINUE
C
C  TRANSFER INTO THE BB ARRAY
C
      DO 10 N = 1,IT
      DO 20 M = 1,N-1
      BB(N,M) = BB2(N,M)
      BB(M,N) = BB2(M,N)
  20  CONTINUE
      BB(N,N) = BB2(N,N)
  10  CONTINUE
C
C     FIND THE MAXIMUM AND SCALE
      XM=DABS(BB(1,1))
      DO 2094 N=1,IT
      DO 2094 M=1,N
      XFAC=DABS(BB(N,M))
      XMAX=DMAX1(XM,XFAC)
      XM=XMAX
 2094 CONTINUE
      DO 2095 N=1,IT
      DO 2095 M=1,IT
 2095 BB(N,M)=BB(N,M)/XM
      IT1=IT+1
      IT2=IT+2
      DO 2100 N=1,IT
      BB(N,IT1)=-ONE
 2100 BB(IT1,N)=-ONE
      BB(IT1,IT1)= A0
      DO 2110 N=1,IT
 2110 BB(N,IT2)= A0
      BB(IT1,IT2)=-ONE
C     WRITE(*,*)'B MATRIX'
C     CALL MATOUT(BB,IT1,IT2,IT1,IT2,6)
      CALL FLINQ(BB,9,IT1,1,DET)
C     WRITE(6,*)'DET=',DET
C     WRITE(4,*)'DET=',DET
      XADD=A0
      DO 2120 N=1,IT
      CC(N)= BB(N,IT2)
      XADD=XADD+CC(N)
C     WRITE(6,*)'N=',N,'CC(N)=',CC(N)
 2120 CONTINUE
      XEN=BB(IT1,IT2)*XM
      XEN=DSQRT(XEN)
      XON=A0
C     WRITE(6,*)'CSUM=',XADD
C     WRITE(4,*)'CSUM=',XADD
C     WRITE(6,*)'  XEN = ',XEN,'  XON = ',XON
C     WRITE(4,*)'  XEN = ',XEN
C
      CALL ZERO(T1N,NO*NV)
      CALL ZERO(S,NDIMT2)
C
      DO 2200 N=1,IT
C
      PT1 = (N-1)*INTOWP(NO*NV + NDIMT2) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      CALL WREADW(ITAP98,T1O,INTOWP(NO*NV),PT1,JUNK)
      CALL WREADW(ITAP98,T2,INTOWP(NDIMT2),PT2,JUNK)
C
      DO 2170 I=1,NO
      DO 2171 A=1,NV
      T1N(I,A) = T1N(I,A) + CC(N)*T1O(I,A)
 2171 CONTINUE
 2170 CONTINUE
C
      DO 2180 UVBG=1,NDIMT2
      S(UVBG) = S(UVBG) + CC(N)*T2(UVBG)
 2180 CONTINUE
C
 2200 CONTINUE
      IFLAG=1
      IECORR=1
 2400 CONTINUE
      RETURN
      END
