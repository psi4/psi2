C     /////////////////////////////////////////////////////////////
      SUBROUTINE OUTPTX(SU,ALPHA,BETA,NLAMDA,NORBS,NC,NO,ITYP,CM,
     #EIG,QLAST,TWOCON,KLI,KLII,CI1,CI2,IALPHA,IBETA,ISU)
C
C
C ROUTINE TO WRITE FINAL SCF VECTOR, OCCUPATIONS, EIGENVALUES,
C ENERGY AND COUPLING CONSTANTS TO TAPE30     PWS 12 JULY 1983
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
      integer msfru,jl,nsym
      REAL*8 CM(2,MSFRU),SU(2)
      INTEGER ISU(1),EIG(1),IBETA(1),IALPHA(1)
      DOUBLE PRECISION BETA(JL),ALPHA(JL)
      INTEGER ITYP(20),NLAMDA(NSYM),NORBS(NSYM),NC(NSYM),NO(NSYM)
      LOGICAL TWOCON,OSS,TWO,HS
      INTEGER BLABEL,ALABEL
      INTEGER IELAST(1)
C
      EQUIVALENCE (IELAST,ELAST)
C
      COMMON /FLAGS/  IDEBUG,ISTO,IPUNCH,ISAVE,IRORDR
      COMMON /TFOC/   OSS,TWO,IOP1,IOP2,OCC1,OCC2,OLD1C,OLD2C,HS
      COMMON /SCFIN/  REPNUC,IOPEN,ISCF,IPRCT,IVECT,NBFLD,ICUT
      COMMON /INPT/   NSYM,IPMAX,NMAT,ITMAX,NSFMAX,NTOTAL,MXCOEF,
     #MSFRU
      COMMON /HOLL/   BLABEL(20),ALABEL(26),FMT(10),JL
      COMMON /TAPES/  NIN,IR,IOUT,ICHECK,IFLAG3,ITAPE,ITERML
      COMMON /TAPE30/ I10(200),ITAP30
      COMMON /LAGRG/ LOCLAG
C
C
C
      ELAST=QLAST
C
      OSS=.FALSE.
      IF (IOPEN.EQ.0) GOTO 921
      IF (DABS(BETA(2)) .EQ. 3.0D+00) OSS=.TRUE.
 921  CONTINUE
      TWO=.FALSE.
      IF (TWOCON) THEN
        ITWO=2
        TWO=.TRUE.
        ALPHA(1)=1.0D+00-2.0D+00/CM(2,KLI)
        ALPHA(2)=1.0D+00
        BETA(2)=-1.0D+00/(CI1*CI2)
        ALPHA(3)=1.0D+00-2.0D+00/CM(2,KLII)
CWA   ENSURE BETA(1),BETA(3) ARE ZEROED.
        BETA(1)=0.0D0
        BETA(3)=0.0D0
      END IF
      IOP1=KLI
      IOP2=KLII
      OCC1=CM(2,KLI)
      OCC2=CM(2,KLII)
      OLD1C=OCC1
      OLD2C=OCC2
      IF (OCC2 .GT. OCC1) THEN
        ITEMP=IOP1
        IOP1=IOP2
        IOP2=ITEMP
        VAL=OCC1
        OCC1=OCC2
        OCC2=VAL
      END IF
C
C     ----- GET ARRAY OF CONSTANTS FROM TAPE30 -----
C
      ITAP30=30
      CALL WREADW(ITAP30,I10,200,101,JUNK)
      WRITE(ICHECK,1152) (I10(III),III=1,200)
 1152 FORMAT(10I6)
      IEND=I10(1)
      MPOINT=I10(2)
      MCONST=I10(3)
      MCALCS=I10(4)
      NCALCS=I10(5)
      NAT=I10(19)
CWA   SAVE POINTER FOR WRITING OF LAGRANGIAN LATER.
      NLAG=I10(22)
      NLAG=NLAG*(NLAG+1)/2
      I10(41)=NSYM
      I10(42)=MXCOEF
      ITEMP=0
      IF (IOPEN.NE.0) ITEMP=JL
CWA   IF (TWOCON) ITEMP=-JL
      IF (TWOCON) ITEMP=-3
      I10(43)=ITEMP
C
C     ----- WRITE CONSTANTS BACK TO FILE -----
C
      CALL WWRITW(ITAP30,I10,200,101,JUNK)
C
C     ----- GET POINTERS TO CALCULATIONS AND TO THIS ONE -----
C
      JUNK=101+MCONST+MPOINT
      CALL WREADW(ITAP30,I10,MCALCS,JUNK,JUNK)
      LOCCAL=I10(NCALCS)
C
C     ----- POINTER TO SCF VECTOR IS FIRST IN CALCULATION POINTERS -
C
      JUNK=LOCCAL+60
      CALL WREADW(ITAP30,I10,20,JUNK,JUNK1)
CWA   NEWVEC=0
      LOCVEC=I10(1)
      IF (LOCVEC.LE.0) THEN
        LOCVEC=IEND
        I10(1)=IEND
        CALL WWRITW(ITAP30,I10,20,JUNK,JUNK1)
C       NEWVEC=1
      END IF
C
C     ----- PUT SCF ENERGY AS SECOND ENERGY -----
C
      JUNK=LOCCAL+60+20+INTOWP(3*NAT)+INTOWP(1)
      CALL WWRITW(ITAP30,IELAST,INTOWP(1),JUNK,JUNK)
C
C     ----- NOW FOR VARIOUS ARRAYS AND VECTOR -----
C
      WRITE (IOUT,*) ' WRITING VEC AT ',LOCVEC,' MXC ',MXCOEF
      CALL WWRITW(ITAP30,ISU,INTOWP(MXCOEF),LOCVEC,LOCVEC)
      CALL WWRITW(ITAP30,EIG,INTOWP(MSFRU),LOCVEC,LOCVEC)
      CALL WWRITW(ITAP30,ITYP,NSYM,LOCVEC,LOCVEC)
      CALL WWRITW(ITAP30,NLAMDA,NSYM,LOCVEC,LOCVEC)
      CALL WWRITW(ITAP30,NC,NSYM,LOCVEC,LOCVEC)
      WRITE(IOUT,*) IOPEN,JL,(NO(I),I=1,NSYM)
      IF (IOPEN.NE.0) THEN
        CALL WWRITW(ITAP30,NO,NSYM,LOCVEC,LOCVEC)
CWA  CHANGED TO ACCOMODATE CURRENT VERSIONS OF TCSCF DERIV PROGRAM.
        JL=3
C
        DO 1 I=1,JL
          BETA(I) = ALPHA(I) - BETA(I)
    1   CONTINUE
        CALL WWRITW(ITAP30,IALPHA,INTOWP(JL),LOCVEC,LOCVEC)
        CALL WWRITW(ITAP30,IBETA,INTOWP(JL),LOCVEC,LOCVEC)
      END IF
CWA   LEAVE ROOM FOR TFOCK TO WRITE OUT THE LAGRANGIAN.
      LOCLAG=LOCVEC
      LOCVEC=LOCVEC+INTOWP(NLAG)
      IF (IEND.LT.LOCVEC) THEN
C
C     ----- UPDATE IEND POINTER -----
C
        CALL WWRITW(ITAP30,LOCVEC,1,101,JUNK)
C
C     ----- SCF INFORMATION HAS BEEN PUT ON TAPE30 -----
C
        WRITE (IOUT,900) LOCVEC,I2SEC(LOCVEC)
  900   FORMAT (//,' TAPE30 EXTENDED TO ',I9,' WORDS OR ',I6,' SECTORS',
     *         //)
      END IF
C
C
C
      RETURN
      END
