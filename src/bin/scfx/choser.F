C     ///////////////////////////////////////////////////////////////
      SUBROUTINE CHOSER(SUQ,EIG,S,SUND,NQ,NVIRQ,KKQ,NMAT,IQ,IR,IPRINT)
C
CWA   DECIDE WHETHER TO REORDER THE NEW OPEN SHELLS BASED ON THE
CWA   OVERLAP WITH THE PREVIOUS QALP ORBITAL.
      IMPLICIT REAL*8 (A-H,O-Z)
C
      DOUBLE PRECISION SUQ(NQ),EIG(NVIRQ),S(NMAT),SUND(NQ,NVIRQ)
      DOUBLE PRECISION SQ1,SQ2,VAL1,VAL2
      INTEGER IQ,IR
      LOGICAL CHOSE,GSTATE,TERMIN,QRDAMP,MOFIX
      COMMON /CHSR/ CHOSE,GSTATE,TERMIN,QRDAMP,MOFIX
      COMMON/TAPES/NIN,IRTAPE,IOUT,ICHECK,IFLAG3,ITAPE,ITERML
C
      IF (IAND(IPRINT,8192) .NE. 0) THEN
        WRITE(ICHECK,200) IQ,IR,KKQ
 200    FORMAT(/'  IQ=',I5,'  IR=',I5,'  KKQ=',I5)
        WRITE(ICHECK,201) (SUQ(III),III=1,NQ)
        WRITE(ICHECK,202) (SUND(III,IQ),III=1,NQ)
        WRITE(ICHECK,203) (SUND(III,IR),III=1,NQ)
 201    FORMAT(/'  SUQ:',5F10.6/6X,5F10.6)
 202    FORMAT(/'  SUND(I,IQ):',5F10.6/12X,5F10.6)
 203    FORMAT(/'  SUND(I,IR):',5F10.6/12X,5F10.6)
        WRITE(ICHECK,210)
 210    FORMAT(/'  THE S MATRIX FOR THIS SYMM')
        NMATQ=NQ*(NQ+1)/2
        CALL PRINTS(S(KKQ+1),NMATQ,NQ,ICHECK)
      END IF
      A0=0.0D+00
      SQ1=A0
      SQ2=A0
      DO 100 I=1,NQ
        DO 150 J=1,NQ
          I1=MAX0(I,J)
          J1=MIN0(I,J)
          IJ1=I1*(I1-1)/2+J1+KKQ
          VAL1=SUQ(I)*SUND(J,IQ)
          SQ1=SQ1+VAL1*S(IJ1)
          VAL2=SUQ(I)*SUND(J,IR)
          IF (IAND(IPRINT,8192) .NE. 0) THEN
            WRITE(ICHECK,204) I,J,IJ1,S(IJ1),VAL1,VAL2
 204        FORMAT(3I5,3F10.6)
          END IF
          SQ2=SQ2+VAL2*S(IJ1)
 150    CONTINUE
 100  CONTINUE
      SQ1=DABS(SQ1)
      SQ2=DABS(SQ2)
      WRITE(ICHECK,206) SQ1,SQ2,EIG(IQ),EIG(IR)
      IF (TERMIN)
     1    WRITE(ITERML,206) SQ1,SQ2,EIG(IQ),EIG(IR)
 206  FORMAT(4F16.10)
      IF(.NOT.CHOSE) RETURN
      IF (SQ1 .GT. SQ2) RETURN
      WRITE(ICHECK,205) SQ1,SQ2
CWA
 205  FORMAT(' RE-ORDERING THE OPEN SHELLS IN CHOSER; OVERLAPS:',
     1  '  (Q,IQ)=',F10.6,'  (Q,IR)=',F10.6)
      VAL=EIG(IQ)
      EIG(IQ)=EIG(IR)
      EIG(IR)=VAL
      DO 110 I=1,NQ
        VAL=SUND(I,IQ)
        SUND(I,IQ)=SUND(I,IR)
 110  SUND(I,IR)=VAL
      RETURN
      END
