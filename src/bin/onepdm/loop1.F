C/////////////////////////////////////////////////////////////
      SUBROUTINE LOOP1(NABC,NLWKS,WGHT,ARC,ISYM
     $,                ISEGM,JSEGM,IMAIN,ISUB,IUWKMN,IUWKSB,ACOEF
     $,                HDWGT,HDSEG,HDPNT,drtdm,vector)
C   THIS SUBROUTINE IS A STRIPPED DOWN VERSION OF LOOPY THAT ONLY
C   HANDLES THE ONE-ELECTRON LOOPS
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL PAGEIN
      CHARACTER SYMLBL*3
      DOUBLE PRECISION ACF,DRTVER,ACOEF,COEFFS,CFS,A
      INTEGER BMAX,PUWK,PRPFLG,PRINT,ROOTI,ROOTF,SYMOFF,IRSYM
      INTEGER DRTLBL,SCFLBL,SYMDIF
      COMMON /TAPES/IFILE3,IFILE5,IFILE6,ITAPE7,IFIL58,IFIL54,IFIL50
     $,             IFIL51,IDEV,IFIL30
      COMMON /DIMS/ NBF,NSYM,NORBS,NROWS,NROWS4,NWKS,NWKS2,NLEVS
     $,             NWKSMX,NWKMX2,NLWKMX,BMAX,NROOTS,NORBSQ,NBFSQ
     $,             NOCCMX,NREFS,IEND
      COMMON /INTS/ NMAX,NMAX2,NGROUP,NBLKOC,NUMIJ,NUMKL,INTSRT
      COMMON /LOOPS/ACF,IUWK,JUWK,NLWK,IORB,JORB,PUWK
      COMMON /PAGE/ IWORD3,PAGEIN,NOFFI,NOFFJ,NPASS,NWORDL
      COMMON /OPS/  PRINT,MAX,NOFLG,PRPFLG,ROOTI,ROOTF,IRSYM
      COMMON /SYMM/ SYMOFF(8),NTOTS(8,2),SYMDIF,SYMLBL(15)
      COMMON /LBLS/ DRTVER,DRTLBL(26),INTLBL(26),SCFLBL(26)
C
      DIMENSION NABC(NROWS),ARC(NROWS4),NLWKS(NROWS),WGHT(NROWS4)
C
      DIMENSION ISEGM(NLEVS),JSEGM(NLEVS),IUWKMN(NLEVS),IUWKSB(NLEVS)
      DIMENSION ACOEF(NLEVS),IMAIN(NLEVS),ISUB(NLEVS),ISYM(NORBS)
C
      DIMENSION HDWGT(NLEVS),HDSEG(NLEVS),HDPNT(NLEVS)
C
      DIMENSION JSEGNR(3),JSEGPT(3),IARCMN(21),IARCSB(21),NXTSEG(21)
      DIMENSION COEFFS(20,5),CFS(100)
      EQUIVALENCE (COEFFS,CFS)
      INTEGER ARC,WGHT,HDSEG,HDWGT,HDPNT,HPT,HSEG,HARPT,SEGWGT
      DATA JSEGNR/7,14,21/
      DATA NXTSEG/0,0,0,2,2,3,3,0,0,2,2,2,2,3,0,0,3,3,3,3,2/
      DATA IARCMN/2,3,4,3,4,2,4,1,2,1,2,3,4,2,1,3,1,2,3,4,3/
      DATA IARCSB/2,3,4,1,2,1,3,3,4,1,2,3,4,3,2,4,1,2,3,4,2/
      JSEGPT(1)=0
      DO 130 I=1,2
  130 JSEGPT(I+1)=JSEGNR(I)
      DO 135 I=1,2
      DO 135 J=1,5
  135 COEFFS(I,J)=0.0D0
C   THESE ARE THE POSSIBLE SEGMENT COEFFICIENTS
      DO 140 I=3,20
      A=FLOAT(I-2)
      COEFFS(I,1)=1.0D0/A
      COEFFS(I,2)=-1.0D0/A
      COEFFS(I,3)=DSQRT((A+1.0D0)/A)
      COEFFS(I,4)=DSQRT(A/(A+1.0D0))
      COEFFS(I,5)=DSQRT(A*(A+2.0D0))/(A+1.0D0)
  140 CONTINUE
C   START THE MAIN LOOP
      DO 500 I=1,NORBS
      PUWK=1
      NPT=1
      LEVI=I+1
      NOFFI=0
      NOFFJ=0
      LEVV=NLEVS
      PAGEIN=.FALSE.
      IF(NWKS.LE.NWKSMX) PAGEIN=.TRUE.
      IF(LEVI.EQ.NLEVS) GO TO 1050
C   GENERATE HEAD SEGMENTS TO LOOPS
      LEVH=NLEVS+1
      HDWGT(NLEVS)=0
      HDPNT(NLEVS)=1
 1000 LEVH=LEVH-1
      SEGWGT=0
      HSEG=0
      HPT=NPT
 1010 HSEG=HSEG+1
      IF(HSEG.LE.4) GO TO 1030
 1020 LEVH=LEVH+1
      IF(LEVH.GT.NLEVS) GO TO 500
      PUWK=PUWK-HDWGT(LEVH)
      HSEG=HDSEG(LEVH)
      HPT=HDPNT(LEVH)
      GO TO 1010
 1030 CONTINUE
      HARPT=(HPT-1)*4+HSEG
      NPT=ARC(HARPT)
      IF(NPT.EQ.0) GO TO 1010
      PUWK=PUWK-SEGWGT
      SEGWGT=WGHT(HARPT)
      PUWK=PUWK+SEGWGT
      HDWGT(LEVH)=SEGWGT
      HDSEG(LEVH)=HSEG
      LEV=LEVH-1
      HDPNT(LEV)=NPT
      IUWK=0
      JUWK=0
      NLWK=NLWKS(NPT)
      IF(PAGEIN) GO TO 1035
      CALL PAGED(vector)
      IF(PAGEIN) LEVV=LEV
      GO TO 1040
 1035 CONTINUE
      IF(LEVV.GT.LEV) GO TO 1040
      PAGEIN=.FALSE.
      CALL PAGED(vector)
      LEVV=NLEVS
      IF(PAGEIN) LEVV=LEV
 1040 CONTINUE
      IF(LEV.GT.LEVI) GO TO 1000
 1050 LEV=LEVI
      LEVM=LEV-1
      JSM=ISYM(LEVM)
      IAD=LEVM
      ISEGM(LEV)=1
      ISEG=1
      IMN=NPT
      ISB=NPT
      KSEG=0
      KSEGMX=JSEGNR(ISEG)
      IUWKMN(LEV)=PUWK
      IUWKSB(LEV)=PUWK
      IMAIN(LEV)=NPT
      ISUB(LEV)=NPT
      ACOEF(LEV)=1.0D0
C   SEARCH FOR NEXT LOOP SEGMENT
  200 KSEG = KSEG + 1
      IF(KSEG.GT.KSEGMX) GO TO 440
      KMN = IARCMN(KSEG)
      IARPT = 4*(IMN-1)+KMN
      KMN = ARC(IARPT)
      IF(KMN.EQ.0) GO TO 200
      KSB = IARCSB(KSEG)
      JARPT = 4*(ISB-1)+KSB
      KSB = ARC(JARPT)
      IF(KSB.EQ.0) GO TO 200
      JSEGM(LEV) = KSEG
      IUWKMN(LEVM) = IUWKMN(LEV) + WGHT(IARPT)
      IUWKSB(LEVM) = IUWKSB(LEV) + WGHT(JARPT)
C   HAVING FOUND A VALID SEGMENT, UPDATE THE VALUE OF THE COEFFICIENT
      GO TO(1,1,4,1,12,1,8,1,7,1,17,2,2,3,1,13,1,2,19,2,6),KSEG
   1  ACOEF(LEVM) = ACOEF(LEV)
      GO TO 120
   2  ACOEF(LEVM) = -ACOEF(LEV)
      GO TO 120
   3  IA=NABC(IMN)+2
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
    4 ACOEF(LEVM)=ACOEF(LEV)+ACOEF(LEV)
      GOTO 120
    6 IA=NABC(IMN)+24
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
    7 IA=NABC(IMN)+42
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
    8 IA=NABC(IMN)+43
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
   12 IA=NABC(IMN)+62
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
   13 IA=NABC(IMN)+63
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
   17 IA=NABC(IMN)+81
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
   19 IA=NABC(IMN)+83
      ACOEF(LEVM)=ACOEF(LEV)*CFS(IA)
      GO TO 120
  120 CONTINUE
C   CHECK TO SEE IF PAGE OF VECTOR IS CORRECT
 1100 IF(PAGEIN) GO TO 1110
      IUWK=IUWKSB(LEVM)
      JUWK=IUWKMN(LEVM)
      NLWKI=NLWKS(KSB)
      NLWKJ=NLWKS(KMN)
      CALL PAGES(vector)
      IF(PAGEIN) LEVV=LEVM
      GO TO 1130
 1110 CONTINUE
      IF(LEVM.LT.LEVV) GO TO 1130
      IUWK=IUWKSB(LEVM)
      JUWK=IUWKMN(LEVM)
      NLWKI=NLWKS(KSB)
      NLWKJ=NLWKS(KMN)
      IF(LEVM.GT.LEVV) GO TO 1120
      CALL PAGEIJ(vector)
      LEVV=NLEVS
      IF(PAGEIN) LEVV=LEVM
      GO TO 1130
 1120 CONTINUE
      PAGEIN=.FALSE.
      GO TO 1100
 1130 CONTINUE
      IF(NXTSEG(KSEG).GT.0) GO TO 400
      IF(ISYM(LEVM).NE.JSM)GOTO 200
      IF(KMN-KSB) 300,380,300
C   UPDATE ALL VARIABLES FOR THE NEW SEGMENT
  300 LEVL=LEVM
      KSEGMX=4
  310 LEV=LEVM
      LEVM=LEV-1
      IF(LEVM.GT.0) GO TO 315
      WRITE(6,313)
  313 FORMAT(' PROBLEMS WITH PARTIAL SPACE')
      CALL MABORT
  315 CONTINUE
      KSEG=0
      IMN=KMN
      IMAIN(LEV)=KMN
      ISB=KSB
      ISUB(LEV)=KSB
  320 KSEG = KSEG + 1
      IF(KSEG.GT.KSEGMX) GO TO 360
      IARPT = 4*(IMN-1)+KSEG
      KMN = ARC(IARPT)
      IF(KMN.LE.0) GO TO 320
      JARPT = 4*(ISB-1)+KSEG
      KSB = ARC(JARPT)
      IF(KSB.LE.0) GO TO 320
      JSEGM(LEV) = KSEG
      IMAIN(LEVM)=KMN
      IUWKMN(LEVM)=IUWKMN(LEV)+WGHT(IARPT)
      ISUB(LEVM)=KSB
      IUWKSB(LEVM)=IUWKSB(LEV)+WGHT(JARPT)
      IF(KMN-KSB) 310,340,310
C   A LOOP HAS BEEN CONSTRUCTED_ FIND ITS CONTRIBUTION AND ADD
C   TO THE DENSTIY MATRIX
  340 IORB = IAD
      JORB = LEVL
      NLWK = NLWKS(KMN)
      IUWK = IUWKMN(LEVM)
      JUWK = IUWKSB(LEVM)
      ACF = ACOEF(LEVL)
      CALL MAKEDM(drtdm,vector)
      GO TO 320
  360 IF(LEV.EQ.LEVL) GO TO 440
      LEVM=LEV
      LEV=LEVM+1
      IMN=IMAIN(LEV)
      ISB=ISUB(LEV)
      KSEG=JSEGM(LEV)
      GO TO 320
C    A LOOP HAS BEEN CONSTRUCTED_ ADD ITS CONTRIBUTION TO THE DENSITY
C    MATRIX
  380 IORB = IAD
      JORB = LEVM
      NLWK = NLWKS(KMN)
      IUWK = IUWKMN(LEVM)
      JUWK = IUWKSB(LEVM)
      ACF = ACOEF(LEVM)
      CALL MAKEDM(drtdm,vector)
      GO TO 200
  400 CONTINUE
      LEV = LEVM
      LEVM = LEV - 1
      ISEG = NXTSEG(KSEG)
      ISEGM(LEV) = ISEG
      KSEG = JSEGPT(ISEG)
      IMN = KMN
      IMAIN(LEV) = KMN
      ISB = KSB
      ISUB(LEV) = KSB
      KSEGMX = JSEGNR(ISEG)
      GO TO 200
  440 CONTINUE
      IF(LEV.EQ.LEVI) GO TO 1010
      LEVM = LEV
      LEV = LEVM + 1
      ISEG = ISEGM(LEV)
      IMN = IMAIN(LEV)
      ISB = ISUB(LEV)
      KSEG = JSEGM(LEV)
      KSEGMX = JSEGNR(ISEG)
      GO TO 200
  500 CONTINUE
      RETURN
      END
