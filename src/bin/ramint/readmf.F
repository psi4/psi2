      SUBROUTINE READMF
      IMPLICIT REAL*8 (A-H,O-Z)
      COMMON/BASIS/NBASIS,NBFAO,NBATRI,NBATR2
      COMMON/CALCS/ISTYPE,ICTYPE
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/COUPL/ALPA(5,5),BETA(5,5)
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOM,N3N,NATRI,NSYM
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/MFSEC/MASTER,NSECT
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOPEN,IOCC,JOCC,KOCC
      DATA ZERO,QURT,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NBATRI = ',I8/
     * 2X,' NATOM  = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' NATRI  = ',I8/
     * 2X,' IOCC   = ',I8/
     * 2X,' JOCC   = ',I8/
     * 2X,' KOCC   = ',I8/
     * 2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' NTREAD = ',I8/
     * 2X,' ISTYPE = ',I8/
     * 2X,' ICTYPE = ',I8/
     * 2X,' IPRNT  = ',I8/)
    2 FORMAT(//,2X,' ALPA MATRIX'/)
    3 FORMAT(//,2X,' BETA MATRIX'/)
    4 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    5 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
C
      CALL INITMF(1)
C
C   READ IN SCF PARAMETERS
      NBASIS=LPARA(3)
      NBFAO=LPARA(11)
      NBATRI=IOFF(NBFAO+1)
      NBATR2=NBATRI*2
      IOCC=LPARA(7)
      KOCC=LPARA(8)
      JOCC=LPARA(9)
      NATOM=LPARA(10)
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      NTREAD=NTYPEP
      IF(NTYPES.EQ.1) NTREAD=1
      IF(ISTYPE.EQ.3) NTREAD=7
      ENUC=APARA(1)
      ESCF=APARA(2)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1) NBASIS,NBFAO,NBATRI,NATOM,N3N,NATRI,IOCC,JOCC,KOCC,
     1  NTYPES,NTYPEP,NTREAD,ISTYPE,ICTYPE,IPRNT
C
C   READ IN OCCUPATION NUMBERS AND COUPLING CONSTANTS
  201 CONTINUE
      DO 101 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  101 CONTINUE
      DO 102 I=1,NTYPEP
      DO 102 J=1,NTYPEP
      ALPA(I,J)=ZERO
      BETA(I,J)=ZERO
  102 CONTINUE
      IJ=0
      DO 103 I=1,NTYPES
      DO 103 J=1,I
      IJ=IJ+1
      ALPA(I,J)= (ONE-APARA(10+IJ))*FOCC(I)*FOCC(J)*HALF
      BETA(I,J)=-(ONE-APARA(25+IJ))*FOCC(I)*FOCC(J)*QURT
      IF(I.EQ.J) GO TO 103
      ALPA(J,I)=ALPA(I,J)
      BETA(J,I)=BETA(I,J)
  103 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,2)
      CALL MATOUT(ALPA,5,5,NTYPEP,NTYPEP,6)
      WRITE(6,3)
      CALL MATOUT(BETA,5,5,NTYPEP,NTYPEP,6)
  202 CONTINUE
C
C   FORM OCCUPATION ARRAYS
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 104 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  104 CONTINUE
      DO 106 ITYP=1,NTYPEP
      DO 105 I=NSTR(ITYP),NEND(ITYP)
  105 MOTYP(I)=ITYP
  106 CONTINUE
      IF(IPRNT.LE.3) GO TO 203
      WRITE(6,4)
      DO 107 I=1,NTYPEP
  107 WRITE(6,5) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
  203 CONTINUE
C
      CALL SREW(MASTER)
      RETURN
      END
