      SUBROUTINE DENS40(DS,DST,EIG,OCC,EAO)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DS(NBATRI,NTYPEP),DST(NBATRI)
      DIMENSION EIG(NBASIS),OCC(NBASIS),EAO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NBFAO,NBATRI,NBATR2
      COMMON/CALCS/ISTYPE,ICTYPE
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOM,N3N,NATRI,NSYM
      COMMON/MFSEC/MASTER,NSECT
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA ZERO,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    2 FORMAT(//,2X,' TOTAL DENSITY MATRIX'/)
    3 FORMAT(//,2X,' SHELL DENSITY MATRIX, ITYP = ',I5/)
C
C   READ IN AO-MO EIGENVECTORS
      CALL SREW(MASTER)
      CALL MREAD(EIG,17)
      CALL MREAD(OCC,18)
      CALL MREAD(EAO,20)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL EIGOUT(EAO,EIG,OCC,NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C   FORM THE TOTAL DENSITY MATRIX
  201 CONTINUE
      IJ=0
      DO 102 I=1,NBFAO
      DO 102 J=1,I
      IJ=IJ+1
      VALU=ZERO
      DO 101 K=1,NBASIS
      IF(OCC(K).LE.ZERO) GO TO 101
      VALU=VALU+EAO(I,K)*EAO(J,K)*OCC(K)
  101 CONTINUE
      DST(IJ)=VALU
  102 CONTINUE
      IF(IPRNT.LE.3) GO TO 202
      WRITE(6,2)
      CALL PRINT(DST,NBATRI,NBFAO,6)
C
C   FORM THE SHELL DENSITY MATRICES
  202 CONTINUE
      DO 105 ITYP=1,NTYPES
      IJ=0
      COEF=TWO
      IF(ISTYPE.EQ.2.AND.ITYP.GT.1) COEF=COEF*FOCC(ITYP)
      IST=NSTR(ITYP)
      IED=NEND(ITYP)
      DO 104 I=1,NBFAO
      DO 104 J=1,I
      IJ=IJ+1
      VALU=ZERO
      DO 103 K=IST,IED
      VALU=VALU+EAO(I,K)*EAO(J,K)
  103 CONTINUE
      DS(IJ,ITYP)=VALU*COEF
  104 CONTINUE
      IF(IPRNT.LE.3) GO TO 105
      WRITE(6,3) ITYP
      CALL PRINT(DS(1,ITYP),NBATRI,NBFAO,6)
  105 CONTINUE
C
      CALL SREW(MASTER)
      RETURN
      END
