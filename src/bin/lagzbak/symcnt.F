C
C-----------------------------------------------------------------------
C
      SUBROUTINE SYMCNT(ORBSYM,FLOV,NIRRED,NO,NV,NT,FZT,
     .                  NSGOO,NSGOV,NSHOV,NSLOV,NSINT,N1INT,C2INT,D2INT,
     .                  IPQ,ITR,ITV,CCTSCF,
     .                  AOFF,AADD,COFF,CADD,DOFF,DADD,EOFF,EADD,
     .                  NT2,NTAO2,NOV,NV2,NO2,NM,IOFF,IADD,
     .                  IFZO,IFZV,I1OFF,I1ADD,C2OFF,C2ADD,D2OFF,D2ADD,
     .                  E3OFF,E3ADD,E3INT)
      IMPLICIT INTEGER (A-Z)
      INTEGER FLOV(NIRRED,2),ORBSYM(NT),FZT(NT),E3OFF(*),E3ADD(*),
     .        IPQ(NT2),ITR(NOV),ITV(NOV),AOFF(*),AADD(*),
     .        COFF(NV2),CADD(NO2),DOFF(NM),DADD(NM),EOFF(*),EADD(*),
     .        IOFF(NT2),IADD(NT2),CCTSCF(*),IFZO(*),IFZV(*),
     .        I1OFF(*),I1ADD(*),C2OFF(*),C2ADD(*),D2OFF(*),D2ADD(*)
C
C >>> FORM IPQ, ITV AND ITR POINTER ARRAYS
C
      DO 50 I=1,NTAO2
      IPQ(I)=I*(I-1)/2
   50 CONTINUE
C
      DO 55 I=1,NOV
      ITV(I)=(I-1)*NV
   55 CONTINUE
C
      DO 60 I=1,NOV
      ITR(I)=(I-1)*NO
   60 CONTINUE
C
C >>> FORM POINTERS ARRAYS FOR ZETO & ZETV
C
      ICNT=0
      DO 150 I=1,NO
         II=CCTSCF(I)
         IF(FZT(II).NE.1)GO TO 150
         ISYM=ORBSYM(II)
         DO 140 J=1,NO
            JJ=CCTSCF(J)
            IF(FZT(JJ).EQ.1)GO TO 140
            JSYM=ORBSYM(JJ)
C           IF(ISYM.NE.JSYM)GO TO 140
            ICNT=ICNT+1
            IJ=ITR(I)+J
            IFZO(IJ)=ICNT
  140    CONTINUE
  150 CONTINUE
C
      ICNT=0
      DO 170 A=1,NV
         AA=CCTSCF(A+NO)
         IF(FZT(AA).NE.1)GO TO 170
         ASYM=ORBSYM(AA)
         DO 160 B=1,NV
            BB=CCTSCF(B+NO)
            IF(FZT(BB).EQ.1)GO TO 160
            BSYM=ORBSYM(BB)
C           IF(ASYM.NE.BSYM)GO TO 160
            ICNT=ICNT+1
            AB=ITV(A)+B
            IFZV(AB)=ICNT
  160    CONTINUE
  170 CONTINUE
C
C >>> ACT-ACT-ACT-ACT  2E INTS AND 2PDM ELEMENTS  (PQ,RS)
C
      NIOFF=0
      DO 1960 P=1,NT
      IF(FZT(P).EQ.1)GO TO 1960
      PSYM=ORBSYM(P)
      DO 1950 Q=1,P
      IF(FZT(Q).EQ.1)GO TO 1950
      QSYM=ORBSYM(Q)
      PQSYM=IEOR(PSYM,QSYM)
      NIADD=0
      PQ=IPQ(P)+Q
      IOFF(PQ)=NIOFF
      DO 1930 R=1,P
      IF(FZT(R).EQ.1)GO TO 1930
      RSYM=ORBSYM(R)
      LS=R
      IF(R.EQ.P)LS=Q
      DO 1920 S=1,LS
      IF(FZT(S).EQ.1)GO TO 1920
      SSYM=ORBSYM(S)
      RS=IPQ(R)+S
      RSSYM=IEOR(RSYM,SSYM)
      IF(PQSYM.NE.RSSYM)GO TO 1920
      NIOFF=NIOFF+1
      NIADD=NIADD+1
      IADD(RS)=NIADD
      ITOT=IOFF(PQ)+IADD(RS)
C     WRITE(6,6575) P,Q,IOFF(PQ),R,S,IADD(RS),ITOT
 1920 CONTINUE
 1930 CONTINUE
 1950 CONTINUE
 1960 CONTINUE
 6575 FORMAT(I4,7(2X,I4))
      NSINT=ITOT
C
C >>> FZ-ACT-ACT-ACT  2E INTS
C
      N1OFF=0
      i1tot=0
      DO 1860 P=1,NT
      IF(FZT(P).NE.1)GO TO 1860
      PSYM=ORBSYM(P)
      DO 1850 Q=1,NT
      IF(FZT(Q).EQ.1)GO TO 1850
      QSYM=ORBSYM(Q)
      PQSYM=IEOR(PSYM,QSYM)
      N1ADD=0
      PQ=IPQ(MAX0(P,Q))+MIN0(P,Q)
      I1OFF(PQ)=N1OFF
      DO 1830 R=1,NT
      IF(FZT(R).EQ.1)GO TO 1830
      RSYM=ORBSYM(R)
      DO 1820 S=1,R
      IF(FZT(S).EQ.1)GO TO 1820
      SSYM=ORBSYM(S)
      RS=IPQ(R)+S
      RSSYM=IEOR(RSYM,SSYM)
      IF(PQSYM.NE.RSSYM)GO TO 1820
      N1OFF=N1OFF+1
      N1ADD=N1ADD+1
      I1ADD(RS)=N1ADD
      I1TOT=I1OFF(PQ)+I1ADD(RS)
C     WRITE(6,6575) P,Q,I1OFF(PQ),R,S,I1ADD(RS),I1TOT
 1820 CONTINUE
 1830 CONTINUE
 1850 CONTINUE
 1860 CONTINUE
      N1INT=I1TOT
C
C >>> FZ-FZ-ACT-ACT  2E INTS
C
      N2OFF=0
      c2tot=0
      DO 1760 P=1,NT
      IF(FZT(P).NE.1)GO TO 1760
      PSYM=ORBSYM(P)
      DO 1750 Q=1,P
      IF(FZT(Q).NE.1)GO TO 1750
      QSYM=ORBSYM(Q)
      PQSYM=IEOR(PSYM,QSYM)
      N2ADD=0
      PQ=IPQ(P)+Q
      C2OFF(PQ)=N2OFF
      DO 1730 R=1,NT
      IF(FZT(R).EQ.1)GO TO 1730
      RSYM=ORBSYM(R)
      DO 1720 S=1,R
      IF(FZT(S).EQ.1)GO TO 1720
      SSYM=ORBSYM(S)
      RS=IPQ(R)+S
      RSSYM=IEOR(RSYM,SSYM)
      IF(PQSYM.NE.RSSYM)GO TO 1720
      N2OFF=N2OFF+1
      N2ADD=N2ADD+1
      C2ADD(RS)=N2ADD
      C2TOT=C2OFF(PQ)+C2ADD(RS)
C     WRITE(6,6575) P,Q,C2OFF(PQ),R,S,C2ADD(RS),C2TOT
 1720 CONTINUE
 1730 CONTINUE
 1750 CONTINUE
 1760 CONTINUE
      C2INT=C2TOT
C
C >>> FZ-ACT-FZ-ACT  2E INTS
C
      N2OFF=0
      d2tot=0
      DO 1765 P=1,NT
      IF(FZT(P).NE.1)GO TO 1765
      PSYM=ORBSYM(P)
      DO 1755 Q=1,NT
      IF(FZT(Q).EQ.1)GO TO 1755
      QSYM=ORBSYM(Q)
      PQSYM=IEOR(PSYM,QSYM)
      N2ADD=0
      PQ=IPQ(MAX0(P,Q))+MIN0(P,Q)
      D2OFF(PQ)=N2OFF
      DO 1735 R=1,P
      IF(FZT(R).NE.1)GO TO 1735
      RSYM=ORBSYM(R)
crsg this is the spot where canonical ordering is not obeyed cuz
crsg the D2ADD array may have the largest index.
crsg specifically, for P.NE.Q, S may be greater than P or Q.
      LS=NT
      IF(P.EQ.R)LS=Q
      DO 1725 S=1,LS
      IF(FZT(S).EQ.1)GO TO 1725
      SSYM=ORBSYM(S)
      RS=IPQ(MAX0(R,S))+MIN0(R,S)
      RSSYM=IEOR(RSYM,SSYM)
      IF(PQSYM.NE.RSSYM)GO TO 1725
      N2OFF=N2OFF+1
      N2ADD=N2ADD+1
      D2ADD(RS)=N2ADD
      D2TOT=D2OFF(PQ)+D2ADD(RS)
C     WRITE(6,6575) P,Q,D2OFF(PQ),R,S,D2ADD(RS),D2TOT
 1725 CONTINUE
 1735 CONTINUE
 1755 CONTINUE
 1765 CONTINUE
      D2INT=D2TOT
C
C >>> ACT-FZ-FZ-FZ  2E INTS
C
      N3OFF=0
      e3tot=0
      DO 1665 P=1,NT
      IF(FZT(P).EQ.1)GO TO 1665
      PSYM=ORBSYM(P)
      DO 1655 Q=1,NT
      IF(FZT(Q).NE.1)GO TO 1655
      QSYM=ORBSYM(Q)
      PQSYM=IEOR(PSYM,QSYM)
      N3ADD=0
      PQ=IPQ(MAX0(P,Q))+MIN0(P,Q)
      E3OFF(PQ)=N3OFF
      DO 1635 R=1,NT
      IF(FZT(R).NE.1)GO TO 1635
      RSYM=ORBSYM(R)
      DO 1625 S=1,R
      IF(FZT(S).NE.1)GO TO 1625
      SSYM=ORBSYM(S)
      RS=IPQ(R)+S
      RSSYM=IEOR(RSYM,SSYM)
      IF(PQSYM.NE.RSSYM)GO TO 1625
      N3OFF=N3OFF+1
      N3ADD=N3ADD+1
      E3ADD(RS)=N3ADD
      E3TOT=E3OFF(PQ)+E3ADD(RS)
C     WRITE(6,6575) P,Q,E3OFF(PQ),R,S,E3ADD(RS),E3TOT
 1625 CONTINUE
 1635 CONTINUE
 1655 CONTINUE
 1665 CONTINUE
      E3INT=E3TOT
C
C >>> NOTE THAT EVEN WITH FROZEN MO'S CPHF NEEDS TO BE SOLVED FOR ALL
C     OCC*VIR PAIRS. THEREFORE C AND D INTS ARE NEEDED FOR  ALL INDICES
C >>> E INTS ARE NEEDED (ALL INDICES) FOR Z-CPHF CONTRIBUTIONS TO XLAG
C
C
C >>> A INTS   (OO,OO)  FOR Z-CPHF
C
      NOFF=0
      DO 1260 K=1,NO
      KK=CCTSCF(K)
      KSYM=ORBSYM(KK)
      DO 1250 L=1,K
      LL=CCTSCF(L)
      LSYM=ORBSYM(LL)
      KLSYM=IEOR(KSYM,LSYM)
      NADD=0
      KL=ITR(K)+L
      AOFF(KL)=NOFF
      DO 1230 I=1,K
      II=CCTSCF(I)
      ISYM=ORBSYM(II)
      LJ=I
      IF(I.EQ.K)LJ=L
      DO 1220 J=1,LJ
      JJ=CCTSCF(J)
      JSYM=ORBSYM(JJ)
      IJ=ITR(I)+J
      IJSYM=IEOR(ISYM,JSYM)
      IF(KLSYM.NE.IJSYM)GO TO 1220
      NOFF=NOFF+1
      NADD=NADD+1
      AADD(IJ)=NADD
      TOT=AOFF(KL)+AADD(IJ)
C     WRITE(6,6570) K,L,AOFF(KL),I,J,AADD(IJ),TOT
 1220 CONTINUE
 1230 CONTINUE
 1250 CONTINUE
 1260 CONTINUE
      NSGOO=TOT
C
C >>> C INTS   (VV,OO)  FOR Z-CPHF
C
      NOFF=0
      DO 1360 A=1,NV
      AA=CCTSCF(A+NO)
      ASYM=ORBSYM(AA)
      DO 1350 B=1,A
      BB=CCTSCF(B+NO)
      BSYM=ORBSYM(BB)
      ABSYM=IEOR(ASYM,BSYM)
      NADD=0
      AB=ITV(A)+B
      COFF(AB)=NOFF
      DO 1330 I=1,NO
      II=CCTSCF(I)
      ISYM=ORBSYM(II)
      DO 1320 J=1,I
      JJ=CCTSCF(J)
      JSYM=ORBSYM(JJ)
      IJ=ITR(I)+J
      IJSYM=IEOR(ISYM,JSYM)
      IF(ABSYM.NE.IJSYM)GO TO 1320
      NOFF=NOFF+1
      NADD=NADD+1
      CADD(IJ)=NADD
      TOT=COFF(AB)+CADD(IJ)
C     WRITE(6,6570) A+NO,B+NO,COFF(AB),I,J,CADD(IJ),TOT
 1320 CONTINUE
 1330 CONTINUE
 1350 CONTINUE
 1360 CONTINUE
      NSGOV=TOT
C
C >>> D INTS   (VO,VO)   FOR Z-CPHF
C
C     NEED TO PUT A1-A1 BLOCK FIRST!
C
      NOFF=0
      DO 1470 TSYM=1,NIRRED
      DO 1460 A=1,NV
      AA=CCTSCF(A+NO)
      ASYM=ORBSYM(AA)
      DO 1450 I=1,NO
      II=CCTSCF(I)
      ISYM=ORBSYM(II)
      AISYM=IEOR(ASYM,ISYM)
      IF(AISYM.NE.TSYM-1)GO TO 1450
      NADD=0
      AI=ITR(A)+I
      DOFF(AI)=NOFF
      DO 1430 B=1,A
      BB=CCTSCF(B+NO)
      BSYM=ORBSYM(BB)
      LJ=NO
      IF(A.EQ.B)LJ=I
      DO 1420 J=1,LJ
      JJ=CCTSCF(J)
      JSYM=ORBSYM(JJ)
      BJSYM=IEOR(BSYM,JSYM)
      IF(AISYM.NE.BJSYM)GO TO 1420
      BJ=ITR(B)+J
      NOFF=NOFF+1
      NADD=NADD+1
      DADD(BJ)=NADD
      TOT=DOFF(AI)+DADD(BJ)
C     WRITE(6,6570) A+NO,I,DOFF(AI),B+NO,J,DADD(BJ),TOT
 1420 CONTINUE
 1430 CONTINUE
 1450 CONTINUE
 1460 CONTINUE
 1470 CONTINUE
      NSHOV=TOT
C
C >>> E INTS   (VO,OO)
C
      NOFF=0
      DO 1560 A=1,NV
      AA=CCTSCF(A+NO)
      ASYM=ORBSYM(AA)
      DO 1550 K=1,NO
      KK=CCTSCF(K)
      KSYM=ORBSYM(KK)
      AKSYM=IEOR(ASYM,KSYM)
      NADD=0
      AK=ITR(A)+K
      EOFF(AK)=NOFF
      DO 1530 I=1,NO
      II=CCTSCF(I)
      ISYM=ORBSYM(II)
      DO 1520 J=1,I
      JJ=CCTSCF(J)
      JSYM=ORBSYM(JJ)
      IJ=ITR(I)+J
      IJSYM=IEOR(ISYM,JSYM)
      IF(AKSYM.NE.IJSYM)GO TO 1520
      NOFF=NOFF+1
      NADD=NADD+1
      EADD(IJ)=NADD
      TOT=EOFF(AK)+EADD(IJ)
C     WRITE(6,6570) A+NO,K,EOFF(AK),I,J,EADD(IJ),TOT
 1520 CONTINUE
 1530 CONTINUE
 1550 CONTINUE
 1560 CONTINUE
      NSLOV=TOT
C
 6570 FORMAT(7(I8,2X))
      RETURN
      END
