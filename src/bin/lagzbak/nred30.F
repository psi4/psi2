C
c----------------------------------------------------------------------
C
      SUBROUTINE NRED30(ITAP30,MPOINT,MCONST,MCALCS,NCALCS,NT,NTAO,
     .                  NSYMHF,NAT,MXCOEF,EIGVAL,NLAMDA,ITEMP,JOUT,
     .                  NC,NO,WTEMP,SCFTCC,ITYP,ORBSYM,FLOV,NIRRED,
     .                  ENUC,ESCF,CHAR,NORD,ZAN,COORD,CCTSCF,
     .                  KATOM,KTYPE,KLOC,NSHELL,
     .                  XVEC,DVEC,WVEC,ic1)
      IMPLICIT REAL*8 (A-H,O-Z)
#include <error.h>
      integer errcod, frdc
      CHARACTER*4 ITYP(NSYMHF),ICSYM,CHAR
      INTEGER NORD
      REAL*8 EIGVAL(NT),WTEMP(NT),ZAN(NAT),COORD(3,NAT),A30(200),
     .       XVEC(NTAO*NT),DVEC(NT,NT),WVEC(NTAO,NT)
      INTEGER NLAMDA(NSYMHF),ITEMP(MCALCS),NC(NSYMHF),SCFTCC(NT),
     .        ORBSYM(NT),FLOV(NIRRED,2),SYMORB,CCTSCF(NT),IA30(400),
     .        LABEL(20),KATOM(NSHELL),KTYPE(NSHELL),KLOC(NSHELL)
      EQUIVALENCE(IA30,A30)
C
C >>> GET ATOMIC NUMBERS, LABEL, COORDINATES AND ENERGIES FROM FILE30
C
      JPOINT=101+MCONST
      CALL WREADW(ITAP30,IA30,MPOINT,JPOINT,JEND)
      KPOINT=IA30(1)
      JSOTAO=IA30(29)
      CALL WREADW(ITAP30,ZAN,INTOWP(NAT),KPOINT,JEND)
      JPOINT=101+MCONST+MPOINT+NCALCS-1
      CALL WREADW(ITAP30,KPOINT,1,JPOINT,JEND)
      CALL WREADW(ITAP30,LABEL,20,KPOINT,LOCAL)
      WRITE(6,905)LABEL
  905 FORMAT(1X,/,'  LABEL FROM FILE30 : ',20A4,/)
      LOCAL=LOCAL+60
      CALL WREADW(ITAP30,COORD,INTOWP(3*NAT),LOCAL,LOCAL)
C
      WRITE(6,*)' GEOMETRY FROM FILE30'
      WRITE(6,*)'                     '
      DO 123 I=1,NAT
      WRITE(6,909)I,ZAN(I),COORD(1,I),COORD(2,I),COORD(3,I)
  123 CONTINUE
  909 FORMAT(1X,I4,3X,F5.2,5X,3(4X,F13.10))
C
C >>> GET ENERGIES AND WRITE OUT
C
      CALL WREADW(ITAP30,IA30,INTOWP(10),LOCAL,LOCAL)
      ENUC=A30(1)
      ESCF=A30(2)
      EE=ESCF-ENUC
      WRITE(6,900)
  900 FORMAT(/,2X,'ENERGIES FROM FILE30 ',/)
      WRITE(6,901) ENUC
  901 FORMAT(1X,' NUCLEAR REPULSION ENERGY =',F20.12)
      WRITE(6,902) ESCF
  902 FORMAT(1X,' SCF ENERGY               =',F20.12)
      WRITE(6,903) EE
  903 FORMAT(1X,' TOTAL ELECTRONIC  ENERGY =',F20.12)
C
C >>> GET BASIS SET INFO. POINTERS START AT 301 AND EXTEND FOR 200
C                         INTEGER WORDS.
C
      CALL WREADW(ITAP30,IA30,200,301,JEND)
      KATPON=IA30( 8)
      KTYPON=IA30( 9)
      KLOPON=IA30(11)
      CALL WREADW (ITAP30,KATOM,NSHELL,KATPON,JEND)
      CALL WREADW (ITAP30,KTYPE,NSHELL,KTYPON,JEND)
      CALL WREADW (ITAP30,KLOC ,NSHELL,KLOPON,JEND)
C
      WRITE(6,*)' '
      WRITE(6,*)'     SHELL    KATOM    KTYPE    KLOC '
      DO 135 I=1,NSHELL
      WRITE(6,912)I,KATOM(I),KTYPE(I),KLOC(I)
  135 CONTINUE
  912 FORMAT(4(3X,I6))
C
C >>> READ SCF EIGENVALUES, PACKED SCF EIGENVECTOR, NLAMDA AND NC
C
      JPOINT = 101 + MCONST + MPOINT
      CALL WREADW (ITAP30,ITEMP,MCALCS,JPOINT,JPOINT)
      LOCCAL = ITEMP(NCALCS)
      JPOINT = LOCCAL + 60
      CALL WREADW (ITAP30,LOCVEC,1,JPOINT,JPOINT)
      CALL WREADW (ITAP30,XVEC,INTOWP(MXCOEF),LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,EIGVAL,INTOWP(NT),LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,ITYP,NSYMHF,LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,NLAMDA,NSYMHF,LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,NC,NSYMHF,LOCVEC,LOCVEC)
C
C >>> GET THE TOTAL NUMBER OF OCCUPIED MOS
C
      NO=0
      DO 555 I=1,NSYMHF
         NO=NO+NC(I)
  555 CONTINUE
C
C >>> SCFTCC CONTAINS THE SCF TO CC ORDERING
C
      ICNT=0
      IOF = 0
      DO 558 I=1,NSYMHF
         IF(I.NE.1) IOF = IOF + NLAMDA(I-1)
         NOI=NC(I)
         DO 557 J=1,NOI
            ICNT=ICNT+1
            IPT=IOF+J
            SCFTCC(IPT)=ICNT
  557    CONTINUE
  558 CONTINUE
C
      IOF = 0
      DO 578 I=1,NSYMHF
         IF(I.NE.1) IOF = IOF + NLAMDA(I-1)
         NVI=NLAMDA(I)-NC(I)
         DO 575 J=1,NVI
            ICNT=ICNT+1
            IPT=IOF+NC(I) + J
            SCFTCC(IPT)=ICNT
  575    CONTINUE
  578 CONTINUE
C
C >>> EIGENVALUES ARE SORT TO CC  ORDERING
C     CCTSCF CONTAINS THE CC TO SCF ORDERING
C
      DO 589 I=1,NT
         IPT = SCFTCC(I)
         CCTSCF (IPT)=I
         WTEMP(IPT)=EIGVAL(I)
  589 CONTINUE
      DO 599 I=1,NT
         EIGVAL(I)=WTEMP(I)
  599 CONTINUE
C
c     WRITE(6,*)
c     WRITE(6,*)'SCFTCC'
c     WRITE(6,'(12I6)')(SCFTCC(IJK),IJK=1,NT)
C
c     WRITE(6,*)
c     WRITE(6,*)'CCTSCF'
c     WRITE(6,'(12I6)')(CCTSCF(IJK),IJK=1,NT)
C
C >>> GET SYMMETRY LABEL FROM INPUT FILE
C
      ic1=0
      CALL LOCATE (5,'# INPUT ###',IERR)
      IF(IERR.NE.0) THEN
         errcod = frdc('SYMMETRY',char)
         if (errcod.ne.0) then
           write(6,*) 'ERROR: problem reading ''SYMMETRY'' from input'
           call pabort
           call qabort
           endif
         call consym(char,nord)
      ELSE
         write(6,*) 'WARNING: using ''# INPUT ###'' for symmetry'
         READ(5,924)LABEL
         READ(5,*)
         READ(5,925)CHAR,NORD
      ENDIF
      if(char.eq.'C1  ')ic1=1
  924 FORMAT(20A4)
  925 FORMAT(A4,5X,I1)
C
C >>> ORBSYM AND FLOV ARE CONSTRUCTED FOR SCF ORDERING
C
      DO 805 ISYM = 1,NIRRED
      FLOV(ISYM,1) = 0
      FLOV(ISYM,2) = -1
  805 CONTINUE
C
      WRITE(6,*)' '
      WRITE(6,*)'       ISYM    FIRST MO    LAST MO  '
      ICNT=0
      DO 820 ISYM=1,NSYMHF
         ICSYM=ITYP(ISYM)
         ICNTH=ICNT+1
         NI=NLAMDA(ISYM)
         DO 810 I=1,NI
            ICNT=ICNT+1
            ORBSYM(ICNT)=ISYM-1
  810    CONTINUE
         IF(ICNTH-1.NE.ICNT) THEN
            SYMORB = ORBSYM(ICNT) + 1
            FLOV(SYMORB,1)=ICNTH
            FLOV(SYMORB,2)=ICNT
         ENDIF
         WRITE(6,*)ISYM,FLOV(ISYM,1),FLOV(ISYM,2)
  820 CONTINUE
C
C >>> UNPACK SO TO MO EIGENVECTOR
C
      ACRCY=+1.0D-15
      DO 701 I=1,MXCOEF
           IF (ABS(XVEC(I)).LT.ACRCY) XVEC(I)=0.0D+00
  701 CONTINUE
C
      CALL ZERO(DVEC,NT*NT)
      IOF=0
      IJ=0
      DO 710 ISYM=1,NSYMHF
         N=NLAMDA(ISYM)
         DO 709 J=1,N
            DO 708 I=1,N
               IJ=IJ+1
               DVEC(IOF+I,IOF+J)=XVEC(IJ)
  708       CONTINUE
  709    CONTINUE
         IOF=IOF+N
  710 CONTINUE
C
c     WRITE(6,*)' '
c     WRITE(6,*)'UNPACKED SO TO MO EIGENVECTOR FROM FILE30'
c     CALL MATOUT(DVEC,NT,NT,NT,NT,6)
C
      CALL WREADW(ITAP30,XVEC,INTOWP(NTAO*NT),JSOTAO,JSOTAO)
c     WRITE(6,*) ' '
c     WRITE(6,*) ' SO TO AO TRANSFORMATION MATRIX '
c     CALL MATOUT(XVEC,NTAO,NT,NTAO,NT,6)
C
      CALL ZERO(WVEC,NTAO*NT)
      CALL MXMB(XVEC,1,NTAO,DVEC,1,NT,WVEC,1,NTAO,NTAO,NT,NT)
C
C ... A(N1,N2)*B(N2,N3)=C(N1,N3)
C ... CALL MXMB(A,1,N1,B,1,N2,C,1,N1,N1,N2,N3)
C
c     WRITE(6,*) ' '
c     WRITE(6,*) ' AO TO MO EIGENVECTOR FROM TAPE 30 '
c     CALL MATOUT(WVEC,NTAO,NT,NTAO,NT,6)
C
      RETURN
      END
