      SUBROUTINE SMIJKL(EV,IJKU,IJTU,ISTU,LBLI,BUFI,LBLO,BUFO)
      IMPLICIT REAL*8 (A-H,O-Z)
C   SO INTEGRAL TO MO INTEGRAL TRANSFORMATION ... VIA FOUR QUARTER
C   TRANSFORMATIONS.
C
C     SO (RS/TU) > (IS/TU) > (IJ/TU) > (IJ/KU) > MO (IJ/KL)
C
      REAL*8 EV(NBASIS,NBASIS)
      REAL*8 IJKU(NBASIS),IJTU(NTRI),ISTU(NBASIS*NTRI)
      INTEGER R,S,T,U
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2),LBLO(MAXBF4),BUFO(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SDATA/ISMAT(8,8)
      COMMON/SIGNS/IOFF(257),IPRNT
      COMMON/SYMMS/MOSYM(256),MOVIR(256)
      COMMON/TAPES/ITAP35,ITAP36
      DATA A00 / 0.0D+00 /
      DATA CRIT / 1.0D-10 /
    1 FORMAT(2X,6I5,F15.8)
    2 FORMAT(2X,5I5,F15.8)
C
      CALL RFILE(ITAP35)
      CALL RFILE(ITAP36)
C
      NN=NTRI
      MM=NBASIS*NN
      LLIM=1
      IULIM=NBASIS
C
C::::::::::::::::::::::::::::::::::::::::
C:::BEGIN FIRST QUARTER TRANSFORMATION:::
C::::::::::::::::::::::::::::::::::::::::
      IBL=0
      DO 260 I=LLIM,IULIM
      ISYM=MOSYM(I)
      IVIR=MOVIR(I)
C   ZERO ISTU ARRAY
      CALL ZERO(ISTU,MM)
C   READ A BLOCK OF SO INTEGRALS
   85 CALL SREAD(ITAP36,LBLI,MAXBF4)
      DO 150 M=1,MAXBUF
      JA=LBLI(M+M-1)
      JAA=LBLI(M+M)
      IF(JA.EQ.0) GO TO 160
      CALL UNPACK(R,S,T,U,IX,JA,JAA)
      VINT=BUFI(MAXBUF+M)
C     IF(IPRNT.LE.4) GO TO 301
C     WRITE(6,1) M,R,S,T,U,IX,VINT
  301 CONTINUE
      IND=(S-1)*NN+IOFF(T)+U
      ISTU(IND)=ISTU(IND)+EV(R,I)*VINT
      GO TO(150,130,130,130,100,110,120,120,90,90,90,90,90,90),IX
   90 IND=(R-1)*NN+IOFF(T)+U
      ISTU(IND)=ISTU(IND)+EV(S,I)*VINT
      IND1=IOFF(R)+S
      IND=(U-1)*NN+IND1
      ISTU(IND)=ISTU(IND)+EV(T,I)*VINT
      IND=(T-1)*NN+IND1
      ISTU(IND)=ISTU(IND)+EV(U,I)*VINT
      GO TO 150
  100 IND=(R-1)*NN+IOFF(T)+U
      ISTU(IND)=ISTU(IND)+EV(S,I)*VINT
      GO TO 150
  110 IND1=IOFF(R)+S
      IND=(U-1)*NN+IND1
      ISTU(IND)=ISTU(IND)+EV(T,I)*VINT
      IND=(T-1)*NN+IND1
      ISTU(IND)=ISTU(IND)+EV(U,I)*VINT
      GO TO 150
  120 IND=(R-1)*NN+IOFF(T)+U
      ISTU(IND)=ISTU(IND)+EV(S,I)*VINT
      IND=(U-1)*NN+IOFF(R)+S
      ISTU(IND)=ISTU(IND)+EV(T,I)*VINT
      GO TO 150
  130 IND=(U-1)*NN+IOFF(R)+S
      ISTU(IND)=ISTU(IND)+EV(T,I)*VINT
      IF(R.EQ.S) GO TO 140
      IND=(R-1)*NN+IOFF(T)+U
      ISTU(IND)=ISTU(IND)+EV(S,I)*VINT
  140 IF(T.EQ.U) GO TO 150
      IND=(T-1)*NN+IOFF(R)+S
      ISTU(IND)=ISTU(IND)+EV(U,I)*VINT
  150 CONTINUE
      GO TO 85
C
C:::::::::::::::::::::::::::::::::::::::::
C:::BEGIN SECOND QUARTER TRANSFORMATION:::
C:::::::::::::::::::::::::::::::::::::::::
  160 CALL SREW(ITAP36)
      DO 250 J=LLIM,I
      JSYM=MOSYM(J)
      IJSYM=ISMAT(ISYM,JSYM)
      JVIR=MOVIR(J)
C   ZERO IJTU ARRAY
      CALL ZERO(IJTU,NN)
      IND=0
      DO 180 S=1,NBASIS
      CC=EV(S,J)
      DO 180 IND1=1,NN
      IND=IND+1
      IJTU(IND1)=IJTU(IND1)+CC*ISTU(IND)
  180 CONTINUE
C
C::::::::::::::::::::::::::::::::::::::::
C:::BEGIN THIRD QUARTER TRANSFORMATION:::
C::::::::::::::::::::::::::::::::::::::::
      DO 240 K=LLIM,I
      KSYM=MOSYM(K)
      KVIR=MOVIR(K)
      IVSUM=IVIR+JVIR+KVIR
      IF(IVSUM.GE.3) GO TO 240
C   ZERO IJKU ARRAY
      CALL ZERO(IJKU,NBASIS)
      IND=0
      DO 200 T=1,NBASIS
      CC=EV(T,K)
      DO 200 U=1,T
      IND=IND+1
      IJKU(U)=IJKU(U)+CC*IJTU(IND)
      IF(T.EQ.U) GO TO 200
      IJKU(T)=IJKU(T)+EV(U,K)*IJTU(IND)
  200 CONTINUE
C
C:::::::::::::::::::::::::::::::::::::::::
C:::BEGIN FOURTH QUARTER TRANSFORMATION:::
C:::::::::::::::::::::::::::::::::::::::::
      LIML=K
      IF(I.EQ.K) LIML=J
      DO 230 L=LLIM,LIML
      LSYM=MOSYM(L)
      KLSYM=ISMAT(KSYM,LSYM)
      LVIR=MOVIR(L)
      IVSUM=IVIR+JVIR+KVIR+LVIR
      IF(ISMAT(IJSYM,KLSYM).NE.1) GO TO 230
      IF(IVSUM.GE.3) GO TO 230
C*    WRITE(6,*) ' I,J,K,L,IJSYM,KLSYM,IVADD',I,J,K,L,IJSYM,KLSYM,IVSUM
      XINT=A00
      DO 210 U=1,NBASIS
  210 XINT=XINT+EV(U,L)*IJKU(U)
C
      IF(DABS(XINT).LT.CRIT) GO TO 230
      CALL PACK(I,J,K,L,IJKL,JB,JBB)
      IBL=IBL+1
C     IF(IPRNT.LE.4) GO TO 302
C     WRITE(6,2) IBL,I,J,K,L,XINT
  302 CONTINUE
      LBLO(IBL+IBL-1)=JB
      LBLO(IBL+IBL)=JBB
      BUFO(MAXBUF+IBL)=XINT
      IF(IBL.LT.MAXBUF) GO TO 220
      CALL SWRIT(ITAP35,LBLO,MAXBF4)
      IBL=0
C
  220 CONTINUE
  230 CONTINUE
  240 CONTINUE
  250 CONTINUE
  260 CONTINUE
C
C   WRITE LAST BUFFER
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(MAXBUF+IBL)=A00
      CALL SWRIT(ITAP35,LBLO,MAXBF4)
      CALL SREW(ITAP35)
C*    WRITE(6,*) ' IBL = ',IBL
C
      RETURN
      END
