      SUBROUTINE STABIX(NIJ,EIG,VEC,B,EVEC,EIVT,NEVT,NORD)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*3 ISMAT
      CHARACTER*3 ISYM,JSYM
      DIMENSION NIJ(NTRI,2),EIG(MXVEC)
      DIMENSION VEC(NVEC,NVEC),B(NIND),EVEC(NIND,NROOT)
      DIMENSION EIVT(NIND),NEVT(NIND),NORD(NIND)
      COMMON/BASIS/NBASIS,NTRI
      COMMON/POSIT/IABP(500)
      COMMON/ROOTS/NROOT,NPAIR
      COMMON/SIGNS/IOFF(32897),IPRNT
      COMMON/SDATA/ISMAT(8,8)
      COMMON/SYMTR/ISTYPE,MOSYM(256),MONUM(256)
      COMMON/TAPES/ITAP35,ITAP91,ITAP92
      COMMON/CI102/NIND,NDEP,NDTRI
      COMMON/CI103/MXITER,MXVEC,MXTRI
      COMMON/CI104/NVEC
      DATA CLIMIT / 1.0D-5 /
    1 FORMAT(2X,' EIG  ',/5F20.10)
    2 FORMAT(//,2X,' THE VEC MATRIX'/)
    3 FORMAT(//,2X,' THE B VECTOR',/5F20.10)
    4 FORMAT(//,2X,' THE EVEC MATRIX'/)
    5 FORMAT(//,2X,' ROOT NUMBER     = ',I5/)
    6 FORMAT(2X,' EIGENVALUE      = ',3X,F20.10/)
    7 FORMAT(7X,'I',6X,'IX',14X,'JX',17X,'EIGENVECTOR',10X,'EIGENVECTOR*
     1*2'/)
    8 FORMAT(3X,I5,3X,I5,'(',I3,A3,')',3X,I5,'(',I3,A3,')',1X,F20.10,
     1 1X,F20.10)
    9 FORMAT(/)
C
      NIND2=NIND*2
      IF(NROOT.GT.NIND) NROOT=NIND
      IF(NPAIR.GT.NIND) NPAIR=NIND
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1) (EIG(I),I=1,NROOT)
      WRITE(6,2)
      CALL MATOUT(VEC,NVEC,NVEC,NVEC,NVEC,6)
C
C   CALCULATE THE EIGENVECTORS
  201 CONTINUE
      CALL RFILE(ITAP91)
      CALL ZERO(EVEC,NIND*NROOT)
      DO 105 IROOT=1,NROOT
      CALL ZERO(EIVT,NIND)
      DO 102 IVEC=1,NVEC
      FAC=VEC(IVEC,IROOT)
      CALL RREAD(ITAP91,B,NIND2,IABP(IVEC))
      IF(IPRNT.GT.5) THEN
        WRITE(6,3) (B(I),I=1,NIND)
      END IF
      DO 101 I=1,NIND
      EIVT(I)=EIVT(I)+B(I)*FAC
  101 CONTINUE
  102 CONTINUE
      DO 103 I=1,NIND
      EVEC(I,IROOT)=EIVT(I)
  103 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.3) GO TO 202
      WRITE(6,4)
      CALL MATOUT(EVEC,NIND,NROOT,NIND,NROOT,6)
C
C   LIST LOW-LYING EIGENVALUES AND THEIR LARGEST COMPONENTS
C
  202 CONTINUE
      DO 110 II=1,NROOT
      WRITE(6,5) II
      WRITE(6,6) EIG(II)
      CALL ZERO(EIVT,NIND)
      CALL IZERO(NEVT,NIND)
      III=0
      DO 106 I=1,NIND
      TEMP=EVEC(I,II)
      IF(DABS(TEMP).LT.CLIMIT) GO TO 106
      III=III+1
      EIVT(III)=TEMP
      NEVT(III)=I
  106 CONTINUE
C
      CALL SORT(EIVT,NEVT,NORD,III)
C
      NSET=MIN(III,NPAIR)
      WRITE(6,7)
      DO 107 I=1,NSET
      NN=NEVT(I)
      IX=NIJ(NN,1)
      JX=NIJ(NN,2)
      TEMP=EIVT(I)
      TEMPSQ=TEMP*TEMP
      IS=MOSYM(IX)
      JS=MOSYM(JX)
      ISX=MONUM(IX)
      JSX=MONUM(JX)
      ISYM=ISMAT(ISTYPE,IS)
      JSYM=ISMAT(ISTYPE,JS)
      WRITE(6,8) I,IX,ISX,ISYM,JX,JSX,JSYM,TEMP,TEMPSQ
  107 CONTINUE
C     WRITE(6,9)
  110 CONTINUE
C
      CALL RCLOSE(ITAP91,3)
      RETURN
      END
