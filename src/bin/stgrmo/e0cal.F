      SUBROUTINE E0CAL(H,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION H(NTRI)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(32897),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP91,ITAP92
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' H MATRIX (MO BASIS)'/)
    2 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
C
C   CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(1)
C
      CALL MREAD(H,16)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(H,NTRI,NBASIS,6)
C
C   CALCULATE ONE-ELECTRON ENERGY
  201 CONTINUE
      ELEC1=A00
      DO 101 I=1,JOCC
      ITYP=MOTYP(I)
      II=IOFF(I+1)
      FAC=FOCC(ITYP)
      ELEC1=ELEC1+H(II)*FAC
  101 CONTINUE
C
C   CALCULATE TWO-ELECTRON ENRGY
C   SUM OVER OCCUPIED ORBITALS
C   I.E. SUM < ALPA(IJ)*(II/JJ) + BETA(IJ)*(IJ/IJ) >
      CALL RFILE(ITAP35)
      ELEC2=A00
      INTI=0
      NBLI=0
  300 CONTINUE
      CALL SREAD(ITAP35,LBLI,MAXBF4)
      NBLI=NBLI+1
      DO 102 IBL=1,MAXBUF
      JA=LBLI(IBL+IBL-1)
      IF(JA.EQ.0) GO TO 301
      JAA=LBLI(IBL+IBL)
      CALL UNPACK(I,J,K,L,IX,JA,JAA)
      IF(I.GT.JOCC) GO TO 102
      IF(J.GT.JOCC) GO TO 102
      VALU=BUFI(IBL+MAXBUF)
C .............1   2   3   4   5   6   7   8   9  10  11  12  13  14....
      GO TO (401,402,102,102,403,102,102,102,102,102,102,102,102,102),IX
C   (II/II)
  401 CONTINUE
      ITYP=MOTYP(I)
      ALP=A(ITYP,ITYP)
      BET=B(ITYP,ITYP)
      FAC=(ALP+BET)*VALU
      ELEC2=ELEC2+FAC
      GO TO 102
C   (II/KK)
  402 CONTINUE
      ITYP=MOTYP(I)
      KTYP=MOTYP(K)
      ALP=A(ITYP,KTYP)
      FAC=ALP*(VALU+VALU)
      ELEC2=ELEC2+FAC
      GO TO 102
C   (IJ/IJ)
  403 CONTINUE
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      BET=B(ITYP,JTYP)
      FAC=BET*(VALU+VALU)
      ELEC2=ELEC2+FAC
  102 CONTINUE
      INTI=INTI+MAXBUF
      GO TO 300
C
  301 CONTINUE
      INTI=INTI+IBL
      CALL RCLOSE(ITAP35,3)
C
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,2) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
      RETURN
      END
