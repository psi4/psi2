      SUBROUTINE FORMD(U,EIGVAL,FX,NVAR)
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 U(50,50),EIGVAL(1),FX(1)
      REAL*8 LAMDA,LAMDA0,LAMDA1,LAMDA2
      LOGICAL PRNT,NRFLAG,DUMP
      COMMON/IO/IN,IOUT
      COMMON/OPTEF/
     $             ES,DMAX,DD,CONVF,CNVFMX,CONVX,CNVXMX,
     $             EIGMAX,EIGMIN,vmode(50),
     $             X(50),XNAME(50),OLDF(50),D(50),HESS(50,50),
     $             IC(50),MODE,NSTEP,MXSTEP,IHESS,IS,
     $             NEGREQ,IUPDAT,
     $             MXHESS,PRNT,NRFLAG,DUMP,IDUM
      COMMON/LAMBDA/LAMDA,LAMDA0,IT
C
      DATA ZERO/0.0D0/, HALF/0.5D0/, TOLL/1.0D-8/
      DATA STEP/0.05D0/, BIG/1.0D+3/, MAXIT/999/
C
C
C  TS SEARCH    FORMS A STEP BY P-RFO THAT TRIES TO MAXIMIZE
C               ALONG THE DIRECTION OF A CHOSEN HESSIAN MODE
C               AND MINIMIZE ALONG ALL OTHER MODES
C  MIN SEARCH   FORMS A STEP BY SIMPLE RFO THAT ATTEMPTS TO
C               MINIMIZE ALONG ALL HESSIAN MODES
C
C
      NUMIT=0
      IT=0
      IF(NEGREQ.EQ.0) GO TO 10
C
C  (A) MAXIMIZATION ALONG ONE OF THE HESSIAN MODES
C
      IF(MODE.NE.0) THEN
        CALL OVRLAP(U,NMODE,NVAR)
C
C  ON RETURN FROM OVRLAP, NMODE IS THE MODE ALONG WHICH
C  THE ENERGY IS TO BE MAXIMIZED
C
        IF(NMODE.NE.MODE) WRITE(IOUT,1000) MODE,NMODE
        MODE=NMODE
cets071791        IF(PRNT) WRITE(IOUT,1010) MODE
        WRITE(IOUT,1010) MODE
        IT=MODE
C
C  IF THE MODE BEING FOLLOWED IS NOW THE LOWEST MODE,
C  THEN SWITCH OFF MODE FOLLOWING
C
        IF(MODE.EQ.1) THEN
          MODE=0
          WRITE(IOUT,1015)
        ENDIF
C
      ELSE
C
        IF(PRNT) WRITE(IOUT,1020)
        IT=1
      ENDIF
C
      LAMDA0=EIGVAL(IT)+DSQRT(EIGVAL(IT)**2 +4.0D0*FX(IT)**2)
      LAMDA0=HALF*LAMDA0
      IF(PRNT) WRITE(IOUT,1030) LAMDA0
      IF(NVAR.EQ.1) GO TO 40
C
C  (B) MINIMIZATION ALONG ALL OTHER MODES
C
   10 CONTINUE
      JT=1+IT
      IF(JT.GT.2) JT=1
C
      IF(PRNT.AND.NEGREQ.EQ.1) WRITE(IOUT,1040)
      IF(PRNT.AND.NEGREQ.EQ.0) WRITE(IOUT,1050)
C
C  SOLVE ITERATIVELY FOR LAMDA
C  INITIAL GUESS FOR LAMDA IS ZERO EXCEPT NOTE THAT
C  LAMDA SHOULD BE LESS THAH EIGVAL(JT)
C
      LAMDA=ZERO
      IF(EIGVAL(JT).LT.ZERO) THEN
        LAMDA=EIGVAL(JT)-STEP
        LAMDA1=EIGVAL(JT)
        LAMDA2=-BIG
      ENDIF
C
   20 NUMIT=NUMIT+1
      TEMP=ZERO
      DO 25 I=1,NVAR
        IF(I.EQ.IT) GO TO 25
        TEMP=TEMP+(FX(I)*FX(I))/(LAMDA-EIGVAL(I))
   25 CONTINUE
      IF(DUMP) WRITE(IOUT,1111) LAMDA,TEMP
C
C  CHECK FOR CONVERGENCE OF LAMDA
C
      IF(DABS(LAMDA-TEMP).LT.TOLL) GO TO 30
C
C  CHECK FOR MAXIMUM ITERATIONS EXCEEDED
C
      IF(NUMIT.GT.MAXIT) GO TO 91
C
C  (A) SIMPLE ITERATIVE SCHEME
C      (USED IF EIGVAL(JT) > ZERO)
C
      IF(EIGVAL(JT).GT.ZERO) THEN
        LAMDA=TEMP
        GO TO 20
C
      ELSE
C
C  (B) CAUTIOUS BRACKETING SCHEME
C      (USED IF EIGVAL(JT) < ZERO)
C
        IF(TEMP.LT.LAMDA) LAMDA1=LAMDA
        IF(TEMP.GT.LAMDA) LAMDA2=LAMDA
        IF(LAMDA2.GT.-BIG) LAMDA=HALF*(LAMDA1+LAMDA2)
        IF(LAMDA2.EQ.-BIG) LAMDA=LAMDA-STEP
        GO TO 20
      ENDIF
C
C  AT THIS POINT WE HAVE AN ACCEPTABLE VALUE FOR LAMDA
C  MAKE FINAL CHECK ON ACCEPTABILITY
C
   30 IF(PRNT) WRITE(IOUT,1030) LAMDA
      IF(LAMDA.GT.EIGVAL(JT)) GO TO 90
      IF(EIGVAL(JT).GT.ZERO.AND.LAMDA.GT.ZERO) GO TO 90
C
C  CALCULATE THE STEP
C
   40 CALL ACLEAR(NVAR,D)
      DO 50 I=1,NVAR
        TEMP=FX(I)/(LAMDA-EIGVAL(I))
        IF(I.EQ.IT) TEMP=FX(I)/(LAMDA0-EIGVAL(I))
        DO 60 J=1,NVAR
          D(J)=D(J)+TEMP*U(J,I)
   60   CONTINUE
   50 CONTINUE
C
      RETURN
C
C  ...........................................................
C    ERROR SECTION
C
   90 CONTINUE
      WRITE(IOUT,1060)
c     CALL LNK1E
      stop
   91 CONTINUE
      WRITE(IOUT,1070)
c     CALL LNK1E
      stop
C
1000  FORMAT(' WARNING!! MODE SWITCHING. WAS FOLLOWING MODE ',I3,
     $       ' NOW FOLLOWING MODE ',I3)
1010  FORMAT(' SEARCHING FOR LAMDA THAT MAXIMIZES ALONG MODE ',I3)
1015  FORMAT(' MODE FOLLOWING SWITCHED OFF')
1020  FORMAT(' SEARCHING FOR LAMDA THAT MAXIMIZES ALONG THE',
     $       ' LOWEST MODE')
1030  FORMAT(' VALUE TAKEN    LAMDA= ',F12.8)
1040  FORMAT(' SEARCHING FOR LAMDA THAT MINIMIZES ALONG ALL',
     $       ' OTHER MODES')
1050  FORMAT(' SEARCHING FOR LAMDA THAT MINIMIZES ALONG ALL MODES')
1060  FORMAT(//' *****************************************'/
     $         ' ** ERROR IN DETERMINING LAMDA IN FORMD **'/
     $         ' *****************************************'//)
1070  FORMAT(//' ****************************************'/
     $         ' ** UNABLE TO DETERMINE LAMDA IN FORMD **'/
     $         ' ****************************************'//)
1111  FORMAT(' IN ITERATIVE CYCLE:  LAMDA= ',F12.8,' TEMP= ',F12.8)

      END
