      SUBROUTINE UPDATE(SVEC,TVEC)
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 SVEC(1),TVEC(1)
      LOGICAL PRNT,NRFLAG,DUMP
      COMMON/IO/IN,IOUT
      COMMON/GRDNT/ENERGY,F(50),FRCNST(1275),NVAR,IGETFC
      COMMON/OPTEF/
     $             ES,DMAX,DD,CONVF,CNVFMX,CONVX,CNVXMX,
     $             EIGMAX,EIGMIN,vmode(50),
     $             X(50),XNAME(50),OLDF(50),D(50),HESS(50,50),
     $             IC(50),MODE,NSTEP,MXSTEP,IHESS,IS,
     $             NEGREQ,IUPDAT,
     $             MXHESS,PRNT,NRFLAG,DUMP,IDUM
C
      DATA ZERO/0.0D0/
C
C  UPDATING OF THE HESSIAN
C  DEPENDS ON CURRENT GRADIENTS, OLD GRADIENTS AND THE
C  CORRECTION VECTOR USED ON THE LAST CYCLE
C  SVEC & TVEC ARE FOR TEMPORARY STORAGE
C
C  2 UPDATING PROCEDURES ARE POSSIBLE
C  (I)   THE POWELL UPDATE
C        THIS PRESERVES THE SYMMETRIC CHARACTER OF THE HESSIAN
C        WHILST ALLOWING ITS EIGENVALUE STRUCTURE TO CHANGE.
C        IT IS THE DEFAULT UPDATE
C  (II)  THE BFGS UPDATE
C        THIS UPDATE HAS THE IMPORTANT CHARACTERISTIC OF RETAINING
C        POSITIVE DEFINITENESS (NOTE: THIS IS NOT RIGOROUSLY
C        GUARANTEED) WHICH CAN BE USEFUL FOR A MINIMUM SEARCH.
C        SHOULD BE USED IN CONJUNCTION WITH "MIN".
C
C
      CALL DMDOTV(TVEC,HESS,D,NVAR)
C
      IF(IUPDAT.EQ.0) THEN
C
C   (I) POWELL UPDATE
C
      IF(PRNT) WRITE(IOUT,1000)
C
      DO 9 I=1,NVAR
      TVEC(I)=F(I)-OLDF(I)-TVEC(I)
    9 CONTINUE
      DDS=DD*DD
      DDTD=DVTV(TVEC,D,NVAR)
      DDTD=DDTD/DDS
C
      DO 10 I=1,NVAR
      DO 11 J=1,I
      TEMP=TVEC(I)*D(J) + D(I)*TVEC(J) - D(I)*DDTD*D(J)
      HESS(I,J)=HESS(I,J)+TEMP/DDS
      HESS(J,I)=HESS(I,J)
   11 CONTINUE
   10 CONTINUE
C
      ELSE
C
C  (II) BFGS UPDATE
C
      IF(PRNT) WRITE(IOUT,2000)
C
      DO 19 I=1,NVAR
      SVEC(I)=F(I)-OLDF(I)
   19 CONTINUE
      DDS=DVTV(SVEC,D,NVAR)
C
C  IF DDS IS NEGATIVE, RETENTION OF POSITIVE DEFINITENESS IS NOT
C  GUARANTEED. PRINT A WARNING AND SKIP THE UPDATE THIS TIME IF
C  REQUESTED
C
      IF(DDS.LT.ZERO) THEN
      WRITE(IOUT,1500)
      IF(IUPDAT.EQ.2) THEN
      WRITE(IOUT,1600)
      RETURN
      ENDIF
      ENDIF
C
      DDTD=DVTV(D,TVEC,NVAR)
C
      DO 20 I=1,NVAR
      DO 21 J=1,I
      TEMP= (SVEC(I)*SVEC(J))/DDS - (TVEC(I)*TVEC(J))/DDTD
      HESS(I,J)=HESS(I,J)+TEMP
      HESS(J,I)=HESS(I,J)
   21 CONTINUE
   20 CONTINUE
      ENDIF
C
      RETURN
C
1000  FORMAT(' HESSIAN UPDATED USING POWELL UPDATE')
C
1500  FORMAT(' WARNING! HEREDITARY POSITIVE DEFINITENESS ENDANGERED')
C
1600  FORMAT(' UPDATE SKIPPED THIS CYCLE')
C
2000  FORMAT(' HESSIAN UPDATED USING BFGS UPDATE')
C
      END
