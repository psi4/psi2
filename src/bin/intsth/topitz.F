      SUBROUTINE TOPITZ(NF,NC,NIR,NCON,NRCR,LMNP1,NSO,ND,NSOPR,NBLPR,
     1  LA,IGC,NTL,NTU,NT,NCT,MCONS,MAORDS,MGCS,ICA,LB,MS,MNL,IDP,
     2  MTYPE,ITYP,CHG,ZET,ETA,XYZ,NIRL,ZAN,C,ICT,NS,KS,EX,CSPD,
     3  NGNCN,KSTART,KATOM,KTYPE,KNG,KMIN,KMAX,ISC,IPC,SLAB,CC,LAB,
     4  LOC,LOC2,KLASS,LAMBDA,IADDR,LBLAT,NPERSH)
C
C CHANGE HONDO ARRAYS TO PITZER TYPE ARRAYS FOR USE IN PITZERS
C SYMMETRY INTEGRAL PROGRAM.                 PWS 15 JUNE 1983
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
C
      COMMON /OUT/    IOUT,ICHECK,IFLAG3,NSECT,NINTS
      COMMON /PSIZE/  MSU,KAORDS,MCONSU,MGCSU,MSTU,MRU,MCSU,MCTU,MCONU,
     1                MCU,MSFU,MGU,MSFRU,MNSFU,MPRU,MCXU,MCCU,MBLU,MRCRU
      COMMON /MAXDIM/ MXAT,MXTRN,MXPR,MXSH,MXSYM,MXCF,MXT,MXISO,MXPSYM
      COMMON /INFOA/  NAT,NUM,NQ,NX,NE,NA,NB
      COMMON /NSHEL/  NSHELL
      REAL*8 IGC(MCTU,MCSU,MGCSU),CHG(MSU),ZET(MCONU,MCONSU),
     1  ETA(MRCRU,MCONU,MCONSU),XYZ(MCU,MSU,3),ZAN(MXAT),C(3,MXAT),
     2  EX(MXPR),CSPD(MRCRU,MXPR,5),CC(MXCF)
      INTEGER NF(MSU),NC(MSU),NIR(KAORDS),NCON(MCONSU),NRCR(MCONSU),
     1  LMNP1(MCONSU),NSO(MSTU),ND(MSTU),NSOPR(MSTU),NBLPR(MSTU),
     2  LA(MRU,KAORDS),NTL(MSFU),NTU(MSFU),NT(MSFU),NCT(MSFU),
     3  MCONS(MSFU),MAORDS(MGCSU),MGCS(MSFU),ICA(MCU,MSU,MGU),
     4  LB(MSFRU),MS(MSFRU),MNL(MSFRU),IDP(MSTU,MSTU,MSTU),
     5  MTYPE(2,MSU),ITYP(MSTU),NIRL(KAORDS),ICT(MXAT,MXTRN),
     6  NS(MXAT),KS(MXAT),NGNCN(MXSH),KSTART(MXSH),KATOM(MXSH),
     7  KTYPE(MXSH),KNG(MXSH),KMIN(MXSH),KMAX(MXSH),ISC(MXSH,MXPSYM),
     8  IPC(MXSH,MXPSYM),SLAB(MXSYM),LAB(MXCF),LOC(MXAT,5),
     9  LOC2(MXAT,5),KLASS(MXAT),LAMBDA(MXSYM),IADDR(MXSYM),
     A  LBLAT(2,MXAT),NPERSH(5)
C
      WRITE(ICHECK,900)
  900 FORMAT (//'    PITZER ARRAYS'//)
C
      WRITE(ICHECK,920) ((ICT(IQ,IQQ),IQ=1,MXAT),IQQ=1,MXTRN)
  920 FORMAT (/'  ICT:',(20I4))
C
C
      IATOM=1
      DO 130 IS=1,MSU
        NC(IS)=0
        DO 120 IT=1,MGU
          JATOM=ICT(IATOM,IT)
          DO 110 JT=1,IT-1
            IF (JATOM.EQ.ICT(IATOM,JT)) GO TO 120
  110     CONTINUE
          NC(IS)=NC(IS)+1
  120   CONTINUE
        IATOM=IATOM+NC(IS)
  130 CONTINUE
C
      IF (IATOM.NE.NAT+1) THEN
        WRITE(IOUT,600) IATOM-1,NAT
        call qabort
      END IF
  600 FORMAT (//' ERROR IN INTEGRALS(TOPITZ): IATOM--',I4,' NAT',I4//)
C
      IBIAS=0
      DO 160 IS=1,MSU
        DO 150 IATOM=IBIAS+1,IBIAS+NC(IS)
          DO 140 IT=1,MGU
            ICA(IATOM-IBIAS,IS,IT)=ICT(IATOM,IT)-IBIAS
  140     CONTINUE
  150   CONTINUE
        IBIAS=IBIAS+NC(IS)
  160 CONTINUE
C
      WRITE(ICHECK,901)(NC(IS),IS=1,MSU)
  901 FORMAT (/' NC=',(20I5))
      DO 180 IS=1,MSU
        WRITE(ICHECK,902) IS
  902   FORMAT (//' IS= ',I4)
        DO 170 IATOM=1,NC(IS)
          WRITE(ICHECK,903) (ICA(IATOM,IS,IGU),IGU=1,MGU)
  903     FORMAT (1X,20I5)
  170   CONTINUE
  180 CONTINUE
C
      IATOM=1
      ISFU=0
      ICONS=0
      IGCS=0
      DO 250 IS=1,MSU
        IC=NC(IS)
        NF(IS)=NS(IATOM)
        ISTART=KS(IATOM)
        ITEST=-1
        DO 200 ISHELL=ISTART,ISTART+NS(IATOM)-1
          ISFU=ISFU+1
          ICONS=ICONS+1
          ITYPE=KTYPE(ISHELL)
          LMNP1(ICONS)=KTYPE(ISHELL)
          IF (ITYPE.NE.ITEST) THEN
            ITEST=ITYPE
            IGCS=IGCS+1
          END IF
          MGCS(ISFU)=IGCS
          NCON(ICONS)=KNG(ISHELL)
          NRCR(ICONS)=NGNCN(ISHELL)
ctph  write(6,*) 'nrcr icons ngncn ishell',nrcr(icons),icons,
ctph 1            ngncn(ishell),ishell
          NTL(ISFU)=KMIN(ISHELL)
          NTU(ISFU)=KMAX(ISHELL)
          NT(ISFU)=KMAX(ISHELL)-KMIN(ISHELL)+1
          NCT(ISFU)=NT(ISFU)*IC
          I1=KSTART(ISHELL)
          I2=I1+KNG(ISHELL)-1
          ICONU=0
          DO 190 I=I1,I2
            ICONU=ICONU+1
            ZET(ICONU,ICONS)=EX(I)
            DO 185 IRCR=1,NRCR(ICONS)
              ETA(IRCR,ICONU,ICONS)=CSPD(IRCR,I,ITYPE)
ctph  write(6,*) ' eta  ircr  iconu  icons',eta(ircr,iconu,icons),ircr,
ctph 1             iconu,icons
  185       CONTINUE
  190     CONTINUE
  200   CONTINUE
        CHG(IS)=ZAN(IATOM)
        MTYPE(1,IS)=LBLAT(1,IATOM)
        MTYPE(2,IS)=LBLAT(2,IATOM)
        DO 220 JATOM=IATOM,IATOM+IC-1
          DO 210 I=1,3
            XYZ(JATOM-IATOM+1,IS,I)=C(I,JATOM)
  210     CONTINUE
  220   CONTINUE
        IATOM=IATOM+IC
  250 CONTINUE
C
      IF (ISFU.NE.MSFU) THEN
        WRITE(IOUT,*) ' ISFU.NE.MSFU IN TOPITZ'
        call qabort
      END IF
      IF (ICONS.NE.MCONSU) THEN
        WRITE(IOUT,*) ' ICONS.NE.MCONSU IN TOPITZ'
        call qabort
      END IF
      IF (IGCS.NE.MGCSU) THEN
        WRITE(IOUT,*) ' IGCS.NE.MGCSU IN TOPITZ'
        call qabort
      END IF
C
C     ----- AT THE MINUTE, THERE IS A ONE-TO-ONE CORRESPONDENCE
C           BETWEEN SHELLS ON ATOMS AND ORDER OF CONTRACTION SETS.
C           THEREFORE, MCONS IS A UNIT VECTOR. MAORDS IS ALSO.
C
      DO 260 I=1,MSFU
        MCONS(I)=I
  260 CONTINUE
C
      DO 270 I=1,MGCSU
        MAORDS(I)=I
  270 CONTINUE
C
      WRITE(ICHECK,904) NF
      WRITE(ICHECK,905) LMNP1
      WRITE(ICHECK,906) NCON
      WRITE(ICHECK,907) NTL
      WRITE(ICHECK,908) NTU
      WRITE(ICHECK,909) NT
      WRITE(ICHECK,910) NCT
      WRITE(ICHECK,911) MGCS
  904 FORMAT ('     NF:',(T10,20I4))
  905 FORMAT ('  LMNP1:',(T10,20I4))
  906 FORMAT ('   NCON:',(T10,20I4))
  907 FORMAT ('    NTL:',(T10,20I4))
  908 FORMAT ('    NTU:',(T10,20I4))
  909 FORMAT ('     NT:',(T10,20I4))
  910 FORMAT ('    NCT:',(T10,20I4))
  911 FORMAT ('   MGCS:',(T10,20I4))
CC
      DO 280 ICONS=1,MCONSU
        MX=NCON(ICONS)
        WRITE(ICHECK,912) (ZET(I,ICONS),
     1    (ETA(IRCR,I,ICONS),IRCR=1,NRCR(ICONS)),I=1,MX)
  280 CONTINUE
  912 FORMAT (//(1X,2F20.8))
      DO 290 ISU=1,MSU
        MX=NC(ISU)
        WRITE(ICHECK,913) ((XYZ(I,ISU,J),J=1,3),I=1,MX)
  290 CONTINUE
  913 FORMAT (//(1X,3F12.6))
C
C     ----- WORK ON SYMMETRY INFORMATION -----
C
      CALL TOGCS(IGC,LA,NIR,NIRL,ND,ITYP,LAMBDA,SLAB,ICT,
     1  KLASS,KATOM,KTYPE,KMIN,NPERSH,ISC,IPC,LOC,LOC2,LAB,CC)
C
C     ----- FILL OUT ARRAYS TO DO WITH SO'S -----
C
      DO 300 I=1,MSTU
        NSO(I)=0
  300 CONTINUE
C
      DO 310 I=1,MSFRU
        LB(I)=-999999
  310 CONTINUE
C
      ISF=0
      ISFR=0
      DO 350 IS=1,MSU
        DO 340 IF=1,NF(IS)
          ISF=ISF+1
          ICONS=MCONS(ISF)
          IRCRU=NRCR(ICONS)
          IAORD=MAORDS(MGCS(ISF))
ctph  write(6,*)'is,if,icons,ircru,iaord,nir(iaord)'
ctph .          ,is,if,icons,ircru,iaord,nir(iaord)
          DO 330 IRCR=1,IRCRU
          DO 330 IR=1,NIR(IAORD)
            ISFR=ISFR+1
            NSO(LA(IR,IAORD))=NSO(LA(IR,IAORD))+1
            LB(ISFR)=NSO(LA(IR,IAORD))
  330     CONTINUE
  340   CONTINUE
  350 CONTINUE
C
      IF (ISF.NE.MSFU) THEN
        WRITE(IOUT,700) ISF,MSFU
        call qabort
      END IF
  700 FORMAT (//' ERROR IN INTEGRALS(TOPITZ): ISF.NE.MSFU ',2I5//)
C
      IF (ISFR.NE.MSFRU) THEN
        WRITE(IOUT,701) ISFR,MSFRU
        call qabort
      END IF
  701 FORMAT (//' ERROR IN INTEGRALS(TOPITZ): ISFR.NE.MSFRU ',2I5//)
C
      I=0
      DO 360 ISTU=1,MSTU
        NSOPR(ISTU)=I
        I=I+NSO(ISTU)
  360 CONTINUE
C
C     ----- IDP IS SYMMETRY MULTIPLICATION TABLE ----
C
      DO 390 K=1,MSTU
        DO 380 J=1,MSTU
          DO 370 I=1,MSTU
            IDP(I,J,K)=0
  370     CONTINUE
          I=IEOR(K-1,J-1)+1
          IDP(I,J,K)=1
  380   CONTINUE
  390 CONTINUE
C
C
      WRITE(ICHECK,*) '  NSOPR:', NSOPR
      WRITE(ICHECK,*) '     LB:', LB
      WRITE(ICHECK,*) '    NSO:', NSO
CTJL  WRITE(ICHECK,'(/,'  NSOPR:',(20I5))') NSOPR
CTJL  WRITE(ICHECK,'(/,'     LB:',(20I5))') LB
CTJL  WRITE(ICHECK,'(/,'    NSO:',(20I5))') NSO
C
C
      RETURN
      END
