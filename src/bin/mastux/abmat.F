      SUBROUTINE ABMAT(ALPA,BETA,ALP,BET,LORD)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NBATRI),BETA(NBATRI)
      DIMENSION ALP(NBATRI),BET(NBATRI),LORD(NBASIS)
      CHARACTER*8 SCFTYP,CITYP,DERTYP
      COMMON/BASIS/NBASIS,NTRI,NST,NSYM,NBFAO,NBFSO,NBATRI
      COMMON/COUPL/ALPC(15),BETC(15),ALPT(15),BETT(15)
      COMMON/FUNCS/NTYPES,NATOMS,N3N
      COMMON/GRSCF/FOCC(10),NSORB(10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TYPES/SCFTYP,CITYP,DERTYP
      DATA A00,QURT,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' ALPA MATRIX'/)
    2 FORMAT(//,2X,' BETA MATRIX'/)
    3 FORMAT(//,2X,' ALP  MATRIX'/)
    4 FORMAT(//,2X,' BET  MATRIX'/)
C
C   IF CI THEN CHANGE BETA COUPLINGS BY ALPHA-BETA
      IF(CITYP.EQ.'CI      ') THEN
        DO 10 I=1,15
          BETC(I)=ALPC(I)-BETC(I)
   10   CONTINUE
      ENDIF
C
C   SET UP ALPHA AND BETA MATRICES
      CALL ZERO(ALPA,NTRI)
      CALL ZERO(BETA,NTRI)
      CALL ZERO(ALP,NTRI)
      CALL ZERO(BET,NTRI)
C
      NSTRI=0
      NENDI=0
      DO 105 ITYP=1,NTYPES
      MM=NSORB(ITYP)
      IF(MM.EQ.0) GO TO 105
      FI=FOCC(ITYP)
      NSTRI=NENDI+1
      NENDI=NSTRI+MM-1
      NSTRJ=0
      NENDJ=0
      DO 104 JTYP=1,ITYP
      NN=NSORB(JTYP)
      IF(NN.EQ.0) GO TO 104
      FJ=FOCC(JTYP)
      NSTRJ=NENDJ+1
      NENDJ=NSTRJ+NN-1
      DO 102 I=NSTRI,NENDI
      DO 102 J=NSTRJ,NENDJ
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      IIJJ=IOFF(MAX0(ITYP,JTYP))+MIN0(ITYP,JTYP)
      ALPA(IJ)=(ONE-ALPC(IIJJ))*FI*FJ*HALF
      BETA(IJ)=-(ONE-BETC(IIJJ))*FI*FJ*QURT
  102 CONTINUE
  104 CONTINUE
  105 CONTINUE
C
CTPH  NOW ROTATE ALPA AND BETA TO SCF ORDERING
C
      CALL MREAD(LORD,4)
      DO 110 I=1,NBASIS
        M=LORD(I)
        DO 109 J=1,I
          N=LORD(J)
          IJ=IOFF(I)+J
          MN=IOFF(MAX(M,N))+MIN(M,N)
          ALP(MN)=ALPA(IJ)
          BET(MN)=BETA(IJ)
  109   CONTINUE
  110 CONTINUE
C
      CALL MWRIT(ALP,40)
      CALL MWRIT(BET,41)
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1)
      CALL PRINT(ALPA,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(BETA,NTRI,NBASIS,6)
      WRITE(6,3)
      CALL PRINT(ALP,NTRI,NBASIS,6)
      WRITE(6,4)
      CALL PRINT(BET,NTRI,NBASIS,6)
C
  205 RETURN
      END
