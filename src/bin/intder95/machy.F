C     /////////////////////////////////////////////////////////////
C     /////////////////////////////////////////////////////////////////
      SUBROUTINE MACHY(NAD,NC,NS,NSX,IOPT,XA,TYPE,IA,A,S,U,IU,Y,SR)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER R,P,Q,NSX,RP,IOPT(30)
      CHARACTER TYPE*5
      DIMENSION TYPE(NS),IA(NS,6),S(NS),U(NS,1),IU(NS,0:1)
      DIMENSION XA(NAD,3),A(NC,NC),Y(NC,NC,NC),SR(NC,NC,NC)
      DIMENSION H111(3,3,3),H112(3,3,3),H221(3,3,3),H222(3,3,3)
      DIMENSION H113(3,3,3),H123(3,3,3),H223(3,3,3),H331(3,3,3)
      DIMENSION H332(3,3,3),H333(3,3,3),H411(3,3,3),H421(3,3,3)
      DIMENSION H422(3,3,3),H431(3,3,3),H432(3,3,3),H433(3,3,3)
      DIMENSION H441(3,3,3),H442(3,3,3),H443(3,3,3),H444(3,3,3)
      DIMENSION E21(3)
      PARAMETER(ZERO=0.0D0,ONE=1.0D0,TWO=2.0D0,THREE=3.0D0)
      COMMON /IO/ IIN1,IOUT,IIN2,ICHECK,NPRT,
     $   I11,I15,I17,I20,I24,
     $   I12,I16,I18,I21,I25,
     $   I31,I32,I33,I35,I36,I37,
     $   ISCR1,ISCR2,ISCR3,ISCR4,ISCR5,ISCR6,ISCR7,ISCR8,ISCR9,ISCR10,
     $   ISCR11,ISCR12,ISCR13,ISCR14
    1 FORMAT(/,1X,'NUMERICAL SR(I,J,K) AND Y(M,N,P) MATRICES USED FOR',
     $ /, ' SIMPLE INTERNAL COORDINATE',I5)
    2 FORMAT(/,1X,'SR(I,J,K) AND Y(M,N,P) MATRICES SET TO ZERO FOR',
     $ /, ' SIMPLE INTERNAL COORDINATE',I5)
      NSYM=IOPT(3)
      IF(NSYM.NE.0) THEN
         ISCR=ISCR4
      ELSE
         ISCR=ISCR3
      END IF
      DO 500  R=1,NS
         DO 10  P=1,NC
         DO 10  N=1,NC
         DO 10  M=1,NC
 10      Y(M,N,P)=ZERO
         DO 11  I=1,NC
         DO 11  J=1,NC
         DO 11  K=1,NC
 11      SR(I,J,K)=ZERO
      IF(IA(R,6).EQ.-2) GO TO 275
      K1=IA(R,1)
      K2=IA(R,2)
      K3=IA(R,3)
      K4=IA(R,4)
      L1=3*(K1-1)
      L2=3*(K2-1)
      L3=3*(K3-1)
      L4=3*(K4-1)
C
      IF(TYPE(R).NE.' STRE') GO TO 25
      CALL HIJKS1(NAD,K1,K2,XA,H111)
C  OPTION
      CALL HSRY2(NC,L1,L2,H111,SR)
C  OPTION
      CALL AHY2(NC,NSX,L1,L2,H111,A,Y)
      GO TO 300
 25   IF(TYPE(R).NE.' BEND') GO TO 75
      CALL HIJKS2(NAD,K1,K2,K3,XA,H111,H112,H113,H123,H221,
     $    H222,H223,H331,H332,H333)
C  OPTION
      CALL HSRY3(NC,L1,L2,L3,H111,H112,H113,H123,H221,
     $    H222,H223,H331,H332,H333,SR)
C  OPTION
      CALL AHY3(NC,NSX,L1,L2,L3,H111,H112,H113,H123,H221,H222,
     $      H223,H331,H332,H333,A,Y)
         GO TO 300
 75      IF(TYPE(R).NE.' LIN1') GO TO 100
      CALL HIJKS3(NAD,K1,K2,K3,K4,XA,H111,H112,H113,H123,
     $            H221,H222,H223,H331,H332,H333)
C  OPTION
      CALL HSRY3(NC,L1,L2,L3,H111,H112,H113,H123,H221,
     $    H222,H223,H331,H332,H333,SR)
C  OPTION
      CALL AHY3(NC,NSX,L1,L2,L3,H111,H112,H113,H123,H221,H222,
     $      H223,H331,H332,H333,A,Y)
      GO TO 300
 100  IF(TYPE(R).NE.'  SPF') GO TO 125
           CALL VECT1(NAD,K1,K2,E21,XA,T21)
           CALL HIJKS1(NAD,K1,K2,XA,H111)
           FACT1=(ONE-S(R))*THREE/T21
           FACT2=TWO/(T21*T21)
           DO 102  K=1,3
           DO 102  J=1,3
           DO 102  I=1,3
 102       H111(I,J,K)=FACT1*(H111(I,J,K)+FACT2*E21(I)*E21(J)*E21(K))
C  OPTION
      CALL HSRY2(NC,L1,L2,H111,SR)
C  OPTION
      CALL AHY2(NC,NSX,L1,L2,H111,A,Y)
      GO TO 300
 125  IF(TYPE(R).NE.' TORS') GO TO 150
      CALL HIJKS6(NAD,K1,K2,K3,K4,XA,H111,H112,H221,H222,H113,H123,
     $      H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $      H442,H443,H444)
C  OPTION
      CALL HSRY4(NC,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,SR)
C  OPTION
      CALL AHY4(NC,NSX,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,A,Y)
      GO TO 300
 150  IF(TYPE(R).NE.'  OUT') GO TO 175
      CALL HIJKS7(NAD,K1,K2,K3,K4,XA,H111,H112,H221,H222,H113,H123,
     $      H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $      H442,H443,H444)
C  OPTION
      CALL HSRY4(NC,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,SR)
C  OPTION
      CALL AHY4(NC,NSX,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,A,Y)
      GO TO 300
 175  IF(TYPE(R).NE.' LINX'.AND.TYPE(R).NE.' LINY') GO TO 200
      IF(TYPE(R).EQ.' LINX') CALL HIJKS8(NAD,K1,K2,K3,K4,XA,H111,
     $      H112,H221,H222,H113,H123,H223,H331,H332,H333,H411,
     $      H421,H422,H431,H432,H433,H441,H442,H443,H444)
      IF(TYPE(R).EQ.' LINY') CALL HIJKS9(NAD,K1,K2,K3,K4,XA,H111,
     $      H112,H221,H222,H113,H123,H223,H331,H332,H333,H411,
     $      H421,H422,H431,H432,H433,H441,H442,H443,H444)
C  OPTION
      CALL HSRY4(NC,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,SR)
C  OPTION
      CALL AHY4(NC,NSX,L1,L2,L3,L4,H111,H112,H221,H222,H113,H123,
     $     H223,H331,H332,H333,H411,H421,H422,H431,H432,H433,H441,
     $     H442,H443,H444,A,Y)
      GO TO 300
 200  IF(IABS(IA(R,6)).NE.2) THEN
         WRITE(IOUT,2) R
         GO TO 300
      END IF
 275  CALL YIN(NC,NC,SR,-R,ISCR7)
      CALL YIN(NC,NSX,Y,R,ISCR7)
      WRITE(IOUT,1) R
 300  CONTINUE
      CALL FILL3A(NC,NSX,Y)
      CALL YOUT(NC,NC,SR,-R,ISCR)
      CALL YOUT(NC,NSX,Y,R,ISCR)
 500  CONTINUE
 600  IF(NSYM.EQ.0) RETURN
      DO 650  R=1,NSYM
         DO 610  P=1,NSYM
         DO 610  N=1,P
         DO 610  M=1,N
 610     Y(M,N,P)=ZERO
         L=IU(R,0)
         DO 620  I=1,L
         RP=IU(R,I)
         CALL YIN(NC,NSYM,SR,RP,ISCR4)
         W1=U(R,I)
         DO 630  P=1,NSYM
         DO 630  N=1,P
         DO 630  M=1,N
 630     Y(M,N,P)=Y(M,N,P)+W1*SR(M,N,P)
 620     CONTINUE
         CALL FILL3A(NC,NSYM,Y)
         CALL YOUT(NC,NSYM,Y,R,ISCR3)
 650  CONTINUE
C   OPTION
      DO 750  R=1,NSYM
         DO 710  P=1,NC
         DO 710  N=1,P
         DO 710  M=1,N
 710     SR(M,N,P)=ZERO
         L=IU(R,0)
         DO 720  I=1,L
         RP=IU(R,I)
         CALL YIN(NC,NC,Y,-RP,ISCR4)
         W1=U(R,I)
         DO 730  P=1,NC
         DO 730  N=1,P
         DO 730  M=1,N
 730     SR(M,N,P)=SR(M,N,P)+W1*Y(M,N,P)
 720     CONTINUE
         CALL FILL3A(NC,NC,SR)
         CALL YOUT(NC,NC,SR,-R,ISCR3)
 750  CONTINUE
      END
